//===== Cronus Script ========================================
//= NPCs da Turbo Track
//===== Por: =================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.3/1.4
//===== Compatível Com: ======================================
//= Cronus SVN
//===== Descrição: ===========================================
//= [Conversão do Aegis]
//= NPCs geral da Turbo Track, inclusos Kafra e mercador de utilidades
//===== Comentários Adicionais: ==============================
//= 1.1 Traduzido para o português [Sensei/Poringão]
//= 1.2 Revisado e convertido para o Cronus [Kamper]
//= 1.3 Correção nos nomes dos npcs. [SoulBlaker]
//=	- Truncado para apenas 24 caracteres de acordo com as
//=	  sintaxe.
//============================================================

turbo_room,130,92,3	script	Funcionária Kafra#tt	115,{
	cutin "kafra_03",2;
	show "[Funcionária Kafra]";
	show "Bem-vindo, ao Serviço Kafra";
	show "estamos sempre do seu lado. Deixe-me";
	show "lembrar que ao você usar";
	show "o Serviço de Salvamento, seu Ponto de Retorno";
	show "será mudado para Al de Baran.";
	show "Agora, como posso ajudar?";
	callfunc "F_Kafra",5,8,1,40,0;

	M_Save:
		savepoint "aldebaran",168,112;
		callfunc "F_KafEnd",0,1,"in Al De Baran";

}

turbo_room,124,86,0	shop	Mercador da Turbo Track	90,501:-1,502:-1,503:-1,504:-1,645:-1,656:-1,1065:-1,1750:-1

turbo_room,99,120,4	script	Guia TBT#Broadcast	124,{
	show "[Guia da Turbo Track]";
	show "Bom dia!";
	show "Eu estou aqui para ajudá-lo fornecendo informações sobre a Turbo Track. Em que posso ajudar?";
	next;
	while(1) {
		switch(select("Regras do Jogo:Pistas do Jogo:Modos do Jogo:Itens e Habilidades Proibidas:Cancelar")) {
		case 1:
			show "[Guia da Turbo Track]";
			show "As regras são essencialmente";
			show "simples. Apenas passe por todos os obstáculos e chegue na Linha de Chegada o mais rápido possível.";
			next;
			show "[Guia da Turbo Track]";
			show "Claro, podem ocorrer";
			show "algumas situações inesperadas nas pistas, mas eu não posso ilustrar um exemplo para você.";
			next;
			show "[Guia da Turbo Track]";
			show "Classes que podem";
			show "montar um PecoPeco,";
			show "como Cavaleiros, Lordes,";
			show "Templários e Paladinos, devem desmontar de seus PecoPecos antes de entrar na Arena.";
			next;
			show "[Guia da Turbo Track]";
			show "Mas não se preocupe...";
			show "Você pode pegar o seu";
			show "PecoPeco de volta";
			show "de volta depois que o jogo terminar.";
			next;
			show "[Guia da Turbo Track]";
			show "O vencedor é a pessoa que primeiro alcançar a Linha de Chegada. Assim que alguém vencer, o";
			show "jogo terminará e todos os participantes serão automaticamente transportados para uma Sala de Espera.";
			next;
			show "[Guia da Turbo Track]";
			show "Além disso, quando o tempo atribuído à partida acabar, o jogo termina,";
			show "mesmo se um vencedor não tiver sido determinado.";
			next;
			show "[Guia da Turbo Track]";
			show "Não há nenhuma exigência de nível ou classe de personagem para participar da Turbo Track. Entretando, esse serviço requer uma taxa de";
			show "1.000 Zenys.";
			next;
			break;
		case 2:
			show "[Guia da Turbo Track]";
			show "A Turbo Track é formada por várias pistas: a ^4d4dffPonte de Mandeira, Caixas Empilhadas, do Deserto, Labirinto, Caracol, da Serpente, da Caverna e o Labirinto Invisível.";
			next;
			show "[Guia da Turbo Track]";
			show "As pistas que estarão presentes vão depender do modo de jogo. Basicamente, uma partida consiste de seis pistas, além das linhas de Largada e Chegada. Gostaria de ouvir mais sobre alguma pista em particular?";
			next;
			set .@exitloop,1;
			while (.@exitloop) {
				switch(select("Ponte de Madeira e Caixas Empilhadas:do Deserto e Labirinto:Caracol e Labirinto Invisível:da Serpente e da Caverna:Não, obrigado.")) {
				case 1:
					show "[Guia da Turbo Track]";
					show "Na pista da Ponte de Madeira,";
					show "você deve passar";
					show "por uma simples tora de lenha. Se cair, você será levado de volta para o começo da ponte.";
					next;
					show "[Guia da Turbo Track]";
					show "As Caixas Empilhadas é uma pista feita de uma colina de caixas. Você deve achar o caminho certo pelas caixas para prosseguir para a próxima pista. Não deve ser muito difícil, porém.";
					next;
					break;
				case 2:
					show "[Guia da Turbo Track]";
					show "O do Deserto é";
					show "uma pista curta e cheia de surpresas. Se for realmente azarado você pode acabar";
					show "confuso ou até amaldiçoado!		";
					next;
					show "[Guia da Turbo Track]";
					show "O Labirinto está cheio de armadilhas que vão te atrasar, então tenha cuidado. Talvez seja mais sábio não se apressar e evitar as armadilhas do que simplismente sair correndo apressado.";
					next;
					break;
				case 3:
					show "[Guia da Turbo Track]";
					show "O Caracol é um caminho estreito e espiral no formato de uma concha de caracol. Não há nenhum obstáculo difícil, mas será duro correr o mais rápido possível.";
					next;
					show "[Guia da Turbo Track]";
					show "O Labirinto Invisível parece uma enorme sala aberta, mas existem";
					show "muitas paredes invisíveis. Porém, você receberá algumas pistas, então não será nada impossível.";
					next;
					break;
				case 4:
					show "[Guia da Turbo Track]";
					show "A da Serpente";
					show "é uma pista";
					show "sinuosa, mais ou menos como o corpo de uma cobra. E ainda tem o fator da sorte nessa pista também, mas isso é segredo!";
					next;
					show "[Guia da Turbo Track]";
					show "A pista da Caverna é uma situação da Caverna de Payon.";
					show "Se você tá acostumando a se aventurar nas Cavernas de Payon, essa parte deve ser muito fácil.";
					next;
					break;
				case 5:
					show "[Guia da Turbo Track]";
					show "Entendo.";
					show "Certo, então,,";
					show "boa sorte pra";
					show "você nas corridas!";
					next;
					set .@exitloop,0;
				}
			}
			show "[Guia da Turbo Track]";
			show "Você gostaria de saber";
			show "qualquer coisa além";
			show "disso sobre a Turbo Track?";
			next;
			break;
		case 3:
			show "[Guia da Turbo Track]";
			show "A Turbo Track lhe fornece três modos diferentes de jogo: Modo Normal, Modo Avançado e Modo Solo.";
			next;
			show "[Guia da Turbo Track]";
			show "Os participantes são liberados para usar quase qualquer habilidades na Turbo Track. Diferente dos outros modos, o ^4d4dffModo Avançado permite aos jogadores fazerem PvP^000000. Esse modo é ideal para corridas entre grupos ou guildas.";
			next;
			show "[Guia da Turbo Track]";
			show "O Labirinto Invisível é um mapa especial que só é fornecido para os modos ^4d4dffNormal^000000 e ^4D4DFFAvançado^000000 de 16 pessoas.";
			next;
			show "[Guia da Turbo Track]";
			show "Você pode experimentar qualquer pista no Modo Solo. O nome e o tempo";
			show "transcorrido do jogador mais rápido também serão gravados em nossos recordes. Por que você não aceita o desafio?";
			next;
			break;
		case 4:
			show "[Guia da Turbo Track]";
			show "Agora na Turbo Track, os";
			show "sequinte items são proibidos";
			show "de uso: Poção Verde, Panacea";
			show "e Asa de Mosca. Se você tentar usá-los, eles não irão funcionar até que você deixe a corrida.";
			next;
			show "[Guia da Turbo Track]";
			show "A seguinte habilidades não podem";
			show "ser usadas durante a corrida da";
			show "Turbo Track: Rapto, Teleporte,";
			show "Portal, Esconderijo, Furtividade, Espreitar, Medicar, Barreira de Gelo, Berserk, Basilica, Sheltering Bless and Spear Dynamo.";
			next;
			//this aopears to suposed to fall through.
		case 5:
			show "[Guia da Turbo Track]";
			show "Por favor, aproveite o seu";
			show "tempo aqui na";
			show "Turbo Track! Obrigada.";
			close;
		}
	}

OnInit:
	// This will only ever be run ONCE!
	// Unless you eradicate the variable.
	//
	// This OnInit initiallizes and sets the starting
	// data for the Turbo Track's "Record" Logs.
	// WARNING/ATTENTION: DO NOT MODIFY ANY OF THE DATA.
	// DOING SO WILL CAUSE INTERNAL PROBLEMS IN THE SCRIPT.
	if ($ttnames$[0] == "") {
		setarray $ttnames$[0],"Breezy Havana";	// Solo Mode/Time Attack
		setarray $ttnames$[1],"RS125";	// Normal 4 Player
		setarray $ttnames$[2],"Hollgrehenn";	// Normal 8 Players
		setarray $ttnames$[3],"Antonio";	// Noomal 16 Players

		setarray $ttnames$[4],"Aragham";	// Expert 4 Players
		setarray $ttnames$[5],"Kafra Jasmine";	// Expert 8 Players
		setarray $ttnames$[6],"Chris";	// Expert 16 Players

		setarray $ttnames$[7],"Breezy Havana";	// Honor Place 1
		setarray $ttnames$[8],"RS125";	// Honor Place 2

		setarray $ttnames$[9],"Breezy Havana";	// Top 1
		setarray $ttnames$[10],"Nari";	// Top 2
		setarray $ttnames$[11],"Senorita Sylvia";	// Top 3
		setarray $ttnames$[12],"Joo Jahk";	// Top 4
		setarray $ttnames$[13],"RS125";	// Top 5

		setarray $ttranks[0],999999,0,0,0,0,0;
	}
	end;

Onend_n1:
	mapannounce "turbo_room","[Modo Normal - Solo] O jogo terminará brevemente.",bc_map,"0xadff2f";
	end;

Onend_n4:
	mapannounce "turbo_room","[Modo Normal - 4 Pessoas] O jogo terminará brevemente.",bc_map,"0xadff2f";
	end;

Onend_n8:
	mapannounce "turbo_room","[Modo Normal - 8 Pessoas] O jogo terminará brevemente.",bc_map,"0xadff2f";
	end;

Onend_n16:
	mapannounce "turbo_room","[Modo Normal - 16 Pessoas] O jogo terminará brevemente.",bc_map,"0xadff2f";
	end;

Onend_e4:
	mapannounce "turbo_room","[Modo Avançado - 4 Pessoas] O jogo terminará brevemente.",bc_map,"0xadff2f";
	end;

Onend_e8:
	mapannounce "turbo_room","[Modo Avançado - 8 Pessoas] O jogo terminará brevemente.",bc_map,"0xadff2f";
	end;

Onend_e16:
	mapannounce "turbo_room","[Modo Avançado - 16 Pessoas] O jogo terminará brevemente.",bc_map,"0xadff2f";
	end;

Onnew_top1:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[9] + " classificou-se como Número Um no Hall da Fama da Turbo Track!",bc_map,"0x00ff00";
	end;

Onnew_top2:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[10] + " classificou-se como Segundo no Hall da Fama da Turbo Track!",bc_map,"0x00ff00";
	end;

Onnew_top3:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[11] + " classificou-se como Terceiro no Hall da Fama da Turbo Track!",bc_map,"0x00ff00";
	end;

Onnew_top4:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[12] + " classificou-se como Quarto no Hall da Fama da Turbo Track!",bc_map,"0x00ff00";
	end;

Onnew_top5:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[13] + " classificou-se como Quinto no Hall da Fama da Turbo Track!",bc_map,"0x00ff00";
	end;

Onwin_n4:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[1] + " venceu o [Modo Normal - 4 Pessoas]!",bc_map,"0x70DBDB";
	end;

Onwin_n8:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[2] + " venceu o [Modo Normal - 8 Pessoas]!",bc_map,"0x70DBDB";
	end;

Onwin_n16:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[3] + " venceu o [Modo Normal - 16 Pessoas]!",bc_map,"0x70DBDB";
	end;

Onwin_e4:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[4] + " venceu o [Modo Avançado - 4 Pessoas]!",bc_map,"0x70DBDB";
	end;

Onwin_e8:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[5] + " venceu o [Modo Avançado - 8 Pessoas]!",bc_map,"0x70DBDB";
	end;

Onwin_e16:
	mapannounce "turbo_room","Parabéns! " + $ttnames$[6] + " venceu o [Modo Avançado - 16 Pessoas]!",bc_map,"0x70DBDB";
	end;
}

turbo_room,102,117,3	script	Hall da Fama#tt	857,{
	show "^2F4F4FHall da Fama da Turbo Track^000000";
	show " ";
	show "O Primeiro:";
	show "^4d4dff" + $ttnames$[7] + "^000000";
	show " ";
	show "O Último:";
	show "^4d4dff" + $ttnames$[8] + "^000000";
	close;
}

turbo_room,77,115,5	script	Modo Solo#tt::TurboSoloNames	857,{
	show "^2F4F4FRecorde do Modo Solo^000000";
	show " ";
	show "O Melhor Jogador";
	show "do Modo Solo!";
	show "^4d4dff" + $ttnames$[0] + "^000000 !";
	close;
}

turbo_room,87,114,5	script	Recordes do Modo Normal::TurboNormalNames	857,{
	show "<<Melhores Jogadores Recentes>>";
	show "Vencedores do Modo Normal - 4 Pessoas";
	show "^4d4dff" + $ttnames$[1] + "^000000";
	show "Vencedores do Modo Normal - 8 Pessoas";
	show "^4d4dff" + $ttnames$[2] + "^000000";
	show "Vencedores do Modo Normal - 16 Pessoas";
	show "^4d4dff" + $ttnames$[3] + "^000000";
	close;
}

turbo_room,112,114,3	script	Recordes do Modo Avança.::TurboExpertNames	857,{
	show "<<Melhores Jogadores Recentes>>";
	show "Vencedores do Modo Avançado - 4 Pessoas";
	show "^4d4dff" + $ttnames$[4] + "^000000";
	show "Vencedores do Modo Avançado - 8 Pessoas";
	show "^4d4dff" + $ttnames$[5] + "^000000";
	show "Vencedores do Modo Avançado - 16 Pessoas";
	show "^4d4dff" + $ttnames$[6] + "^000000";
	close;
}

turbo_room,97,117,5	script	Hall da Fama do TBT#t2	857,{
	show "^2F4F4FHall da Fama da Turbo Track^000000";
	show " ";
	show "1º: ^4d4dff" + $ttnames$[9] + "^000000";
	show "2º: ^4d4dff" + $ttnames$[10] + "^000000";
	show "3º: ^4d4dff" + $ttnames$[11] + "^000000";
	show "4º: ^4d4dff" + $ttnames$[12] + "^000000";
	show "5º: ^4d4dff" + $ttnames$[13] + "^000000";
	close;
}

turbo_room,75,95,5	duplicate(TurboSoloNames)	Modo Solo#1	857
turbo_room,67,95,5	duplicate(TurboNormalNames)	Recordes do Modo Nor#2	857
turbo_room,71,95,5	duplicate(TurboExpertNames)	Recordes do Modo Ava#2	857

// Turbo Track Item Exchanger
turbo_room,93,117,5	script	Distribuidor de Pontos	125,{
	if ((MaxWeight-Weight) < 2000 || checkweight(1201,1) == 0) {
		show "^3355FFEspere um segundo!";
		show "Certo, você esta carregando";
		show "muitos items com você. Por";
		show "favor volte depois que você";
		show "tiver colocado alguns de";
		show "seus itens no baú, usando o Serviço Kafra.^000000";
		close;
	}
	if (tt_point < 10) {
		show "[Distribuidor de Pontos]";
		show "Bom dia,";
		show "" + strcharinfo(0) + ".";
		show "Você tem um total de";
		show "" + tt_point + " pontos da Turbo Track.";
		next;
		show "[Distribuidor de Pontos]";
		show "Se você tiver pelo menos";
		show "10 Pontos da Turbo Track,";
		show "é possível trocar esses";
		show "pontos por itens. Você";
		show "gostaria de ver a lista de";
		show "itens que você pode ganhar com Pontos de Turbo Track?";
		next;
		if (select("Sim.:Cancelar.") == 1) {
			show "[Distribuidor de Pontos]";
			show "1 Vale-Teletransporte";
			show "da Kafra: ^3355FF10 pts^000000";
			show "1 Pergaminho Mágico de Nível 5: ^3355FF12 TTP^000000";
			show "Pontos de Experiência: ^3355FF40 TTP^000000";
			next;
			show "[Distribuidor de Pontos]";
			show "1 Diamande de 3 Quilates: ^3355FF150 pts^000000";
			show "1 Caixa de Presente: ^3355FF300 pts^000000";
			show "1 Poção do Vento: ^3355FF400 pts^000000";
			next;
			show "[Distribuidor de Pontos]";
			show "Você também pode converter Pontos de Turbo Track em Pontos de Arena. Para mais informações, por favor, fale com o Encarregado da Arena. Obrigado.";
			close;
		}
		show "[Distribuidor de Pontos]";
		show "Eu espero que você divirta-se";
		show "aqui na Turbo Track.";
		show "Tente ganhar o máximo de pontos";
		show "possíveis para você poder";
		show "trocá-los por coisas úteis";
		show "mais tarde! Boa corrida.";
		close;
	}
	else {
		show "[Distribuidor de Pontos]";
		show "Bom dia,";
		show "" + strcharinfo(0) + ".";
		show "Você tem um total de";
		show "" + tt_point + " pontos da Turbo Track";
		show "Você gostaria de trocar";
		show "esses pontos por itens?";
		next;
		switch(select("Trocar.:Cancelar.")) {
		case 1:
			show "[Distribuidor de Pontos]";
			show "Por favor escolha o item pelo";
			show "o qual você gostaria de trocar";
			show "seus Pontos da Turbo Track.";
			next;
			switch(select("Vale-Teletransporte:Pergaminhos Mágico Nível 5:Pontos de Experiência:Diamante de 3 quilates:Caixa de Presente:Poção do Vento:Cancelar.")) {
			case 1:
				show "[Distribuidor de Pontos]";
				show "Cada Vale-Teletransporte";
				show "custa 10 Pontos da Turbo";
				show "Track. Por favor entre";
				show "com a quantidade que você";
				show "deseja ter. Para cancelar,";
				show "por favor coloque ''^3355FF0^000000.''";
				next;
				digit .@input;
				if (.@input == 0) {
					show "[Distribuidor de Pontos]";
					show "Você";
					show "cancelou";
					show "seu pedido.";
					close;
				}
				else if (.@input < 0 || .@input > 50) {
					show "[Distribuidor de Pontos]";
					show "Seu pedido excedeu";
					show "o limite máximo. Você";
					show "pode receber o máximo de";
					show "50 vales de cada vez.";
					close;
				}
				else {
					set .@total_point,10 * .@input;
					if (.@total_point > tt_point) {
						show "[Distribuidor de Pontos]";
						show "Sinto muito, mais você";
						show "não tem Pontos da Turbo";
						show "Track o bastante. Por favor";
						show "cheque a quantia de Pontos da Turbo";
						show "Track que você ganhou antes";
						show "de gastar seus pontos novamente.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					getitem 7060,.@input; //Warp_Free_Ticket
					show "[Distribuidor de Pontos]";
					show "Obrigado por";
					show "sua preferência.";
					show "Nós esperamos que";
					show "desfrute de seu tempo";
					show "aqui na Turbo Track~";
					close;
				}
			case 2:
				show "[Distribuidor de Pontos]";
				show "Cada Pergaminho Mágico custa";
				show "12 Pontos da Turbo Track. Por favor,";
				show "escolha o Pergaminho Mágico que você gostaria de receber. Cada Pergaminho habilita uma vez o uso de magia nível 5 ou habilidade.";
				next;
				switch(select("Coluna de Pedra:Lanças de Gelo:Lanças de Fogo:Relâmpago:Espíritos Anciões:Bolas de Fogo:Barreira de Fogo:Rajada Congelante:Curar")) {
				case 1:
					set .@scroll,687; //Earth_Scroll_1_5
					set .@name$,"Coluna de Pedra";
					break;
				case 2:
					set .@scroll,689; //Cold_Scroll_1_5
					set .@name$,"Lanças de Gelo";
					break;
				case 3:
					set .@scroll,691; //Fire_Scroll_1_5
					set .@name$,"Lanças de Fogo";
					break;
				case 4:
					set .@scroll,693; //Wind_Scroll_1_5
					set .@name$,"Relâmpago";
					break;
				case 5:
					set .@scroll,695; //Ghost_Scroll_1_5
					set .@name$,"Espíritos Anciões";
					break;
				case 6:
					set .@scroll,697; //Fire_Scroll_2_5
					set .@name$,"Bolas de Fogo";
					break;
				case 7:
					set .@scroll,699; //Fire_Scroll_3_5
					set .@name$,"Barreira de Fogo";
					break;
				case 8:
					set .@scroll,12000; //Cold_Scroll_2_5
					set .@name$,"Rajada Congelante";
					break;
				case 9:
					set .@scroll,12002; //Holy_Scroll_1_5
					set .@name$,"Curar";
					break;
				}
				show "[Distribuidor de Pontos]";
				show "Por favor entre com o número";
				show "de ^4d4dff"+.@name$+"^000000 Pergaminhos Mágicos";
				show "que você quer receber. Para";
				show "cancelar, coloque ''^3355FF0^000000.''";
				next;
				digit .@input;
				if (.@input == 0) {
					show "[Distribuidor de Pontos]";
					show "Você";
					show "cancelou";
					show "seu pedido.";
					close;
				}
				else if (.@input < 0 || .@input > 50) {
					show "[Distribuidor de Pontos]";
					show "Sinto muito, mas seu pedido";
					show "excedeu o limite máximo.";
					show "Você só pode pedir acima";
					show "de 50 pergaminhos de cada vez. ^FFFFFFcobo^000000";
					close;
				}
				else {
					set .@total_point,12 * .@input;
					if (.@total_point > tt_point) {
						show "[Distribuidor de Pontos]";
						show "Sinto muito, mais você";
						show "não tem Pontos da Turbo";
						show "Track o bastante. Por favor";
						show "cheque a quantia de Pontos da Turbo";
						show "Track que você ganhou antes";
						show "de gastar seus pontos novamente.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					additem .@scroll,.@input;
					show "[Distribuidor de Pontos]";
					show "Obrigado por";
					show "sua preferência.";
					show "Nós esperamos que";
					show "desfrute de seu tempo";
					show "aqui na Turbo Track~";
					close;
				}
			case 3:
				show "[Distribuidor de Pontos]";
				show "Você pode trocar";
				show "40 Pontos da Turbo Track";
				show "por Experiência de Base.";
				show "Você gostaria de";
				show "trocar seus pontos da Turbo Track";
				show "por Experiência?";
				next;
				if (select("Não:Sim") == 2) {
					if (tt_point < 40) {
						show "[Distribuidor de Pontos]";
						show "Sinto muito, mais você";
						show "não tem Pontos da Turbo";
						show "Track o bastante. Por favor";
						show "cheque a quantia de Pontos da Turbo";
						show "Track que você ganhou antes";
						show "de gastar seus pontos novamente.";
						close;
	
					}
					set tt_point,tt_point-40;
					if (BaseLevel < 70)
						getexp 3000,0;
					else if (BaseLevel < 80)
						getexp 9000,0;
					else if (BaseLevel < 90)
						getexp 10000,0;
					else if (BaseLevel < 100)
						getexp 30000,0;
					show "[Distribuidor de Pontos]";
					show "Obrigado, Seus";
					show "Pontos daTurbo Track";
					show "foram convertido em";
					show "Experiência de Base.";
					close;
				}
				show "[Distribuidor de Pontos]";
				show "Você";
				show "cancelou";
				show "seu pedido.";
				close;
			case 4:
				show "[Distribuidor de Pontos]";
				show "Você pode trocar";
				show "150 Pontos da Turbo Track";
				show "por ^3131FF1 Diamante de 3 quilates^000000.";
				show "Por favor, entre com o número de";
				show "diamantes que você gostaria de receber.";
				show "Para cancelar, coloque ''^3355FF0^000000.''";
				next;
				digit .@input;
				if (.@input == 0) {
					show "[Distribuidor de Pontos]";
					show "Você";
					show "cancelou";
					show "seu pedido.";
					close;
				}
				else if (.@input < 0 || .@input > 10) {
					show "[Distribuidor de Pontos]";
					show "Seu pedido excedeu";
					show "o limite máximo. Você";
					show "só pode receber um máximo";
					show "de 10 diamantes de cada vez.";
					close;
				}
				else {
					set .@total_point,150 * .@input;
					if (.@total_point > tt_point) {
						show "[Distribuidor de Pontos]";
						show "Sinto muito, mas você";
						show "não tem Pontos da Turbo";
						show "Track o bastante. Por favor";
						show "cheque a quantia de Pontos da Turbo";
						show "Track que você ganhou antes";
						show "de gastar seus pontos novamente.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					additem 732,.@input; //Crystal_Jewel__
					show "[Distribuidor de Pontos]";
					show "Obrigado por";
					show "sua preferência.";
					show "Nós esperamos que";
					show "desfrute de seu tempo";
					show "aqui na Turbo Track~";
					close;
				}
			case 5:
				show "[Distribuidor de Pontos]";
				show "Você pode trocar";
				show "300 Pontos da Turbo Track";
				show "por ^3131FF1 Caixa de Presente^000000. Por favor,";
				show "entre o número de Caixas de";
				show "Presente que você gostaria";
				show "de receber. Pra cancelar, coloque ''^3355FF0^000000.''";
				next;
				digit .@input;
				if (.@input == 0) {
					show "[Distribuidor de Pontos]";
					show "Você";
					show "cancelou";
					show "seu pedido.";
					close;
				}
				else if (.@input < 0 || .@input > 10) {
					show "[Distribuidor de Pontos]";
					show "Seu pedido excedeu o";
					show "limite máximo. Você só";
					show "pode receber um máximo";
					show "de 10 Caixas de Presente de cada vez.";
					close;
				}
				else {
					set .@total_point,300 * .@input;
					if (.@total_point > tt_point) {
						show "[Distribuidor de Pontos]";
						show "Sinto muito, mais você";
						show "não tem Pontos da Turbo";
						show "Track o bastante. Por favor";
						show "cheque a quantia de Pontos da Turbo";
						show "Track que você ganhou antes";
						show "de gastar seus pontos novamente.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					additem 644,.@input; //Gift_Box
					show "[Distribuidor de Pontos]";
					show "Obrigado por";
					show "sua preferência.";
					show "Nós esperamos que";
					show "desfrute de seu tempo";
					show "aqui na Turbo Track~";
					close;
				}
			case 6:
				show "[Distribuidor de Pontos]";
				show "Você pode trocar 400";
				show "Pontos da Turbo Track";
				show "por ^3131FF1 Poção do Vento^000000. Por favor,";
				show "entre o número de poções";
				show "que você gostaria de receber.";
				show "Para cancelar, coloque ''^3355FF0^000000.''";
				next;
				digit .@input;
				if (.@input == 0) {
					show "[Distribuidor de Pontos]";
					show "Você";
					show "cancelou";
					show "seu pedido.";
					close;
				}
				else if (.@input < 0 || .@input > 10) {
					show "[Distribuidor de Pontos]";
					show "Seu pedido excedeu o";
					show "limite máximo. Você só";
					show "pode receber um máximo";
					show "de 10 poções de cada vez.";
					close;
				}
				else {
					set .@total_point,400 * .@input;
					if (.@total_point > tt_point) {
						show "[Distribuidor de Pontos]";
						show "Sinto muito, mais você";
						show "não tem Pontos da Turbo";
						show "Track o bastante. Por favor";
						show "cheque a quantia de Pontos da Turbo";
						show "Track que você ganhou antes";
						show "de gastar seus pontos novamente.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					additem 12016,.@input; //Speed_Up_Potion
					show "[Distribuidor de Pontos]";
					show "Obrigado por";
					show "sua preferência.";
					show "Nós esperamos que";
					show "desfrute de seu tempo";
					show "aqui na Turbo Track~";
					close;
				}
			case 7:
				show "[Distribuidor de Pontos]";
				show "Este Serviço de Troca";
				show "de item é trazido para";
				show "você pela...";
				next;
				show "[Distribuidor de Pontos]";
				show "Este Serviço de Troca de item";
				show "é trazido para você pela";
				show "Corporação Kafra, Sindicato dos";
				show "Ferreiros, Cassino de Comodo";
				show "e a Administração dos Castelos de Luina";
				show "em Al De Baran.";
				close;
			}
		case 2:
			show "[Distribuidor de Pontos]";
			show "Pontos da Turbo Track podem ser";
			show "convertidos em Pontos de Arena.";
			show "Você pode salvar o máximo de";
			show "^4D4DFF29,000 Pontos da Turbo Track.";
			next;
			show "[Distribuidor de Pontos]";
			show "Antes de alcançar o máximo";
			show "da quantia de Pontos da Turbo";
			show "Track, você poderia querer";
			show "gastar alguns deles de forma";
			show "que você possa continuar obtendo sua recompensa depois de vencer as corridas da Turbo Track.";
			close;
		}
	}
}

turbo_room,106,117,3	script	Encarregado da TBT#tt	833,{
	show "[Encarregado]";
	show "Bom dia.";
	show "Você curtia a sua";
	show "visita à Turbo Track?";
	show "Em que posso";
	show "ajudar?";
	next;
	switch(select("Verificar Pontos:Converter Pontos:^660000Informação de Conversão^000000")) {
	case 1:
		show "[Encarregado]";
		show "" + strcharinfo(0) + ",";
		show "você tem atualmente";
		show "" + tt_point + " Pontos da Turbo Track";
		show "e " + arena_point + " Pontos de Arena.";
		close;
	case 2:
		show "[Encarregado]";
		show "" + strcharinfo(0) + ",";
		show "você tem atualmente";
		show "" + tt_point + " Pontos da Turbo Track";
		show "e " + arena_point + " Pontos de Arena.";
		next;
		show "[Encarregado]";
		show "Por favor escolha entre os seguintes tipos de conversão de Pontos de Arena. Ao converter mais que";
		show "10 pontos de arena de uma vez, você";
		show "só pode converter Pontos de Arena";
		show "em ^4D4DFFmúltiplos de 10^000000.";
		next;
		switch(select("2 PA -> 1 PTT:4 PA -> 2 PTT:6 PA -> 3 PTT:8 PA -> 4 PPT:10 PA ou mais:Cancelar")) {
		case 1:
			callsub S_ExchangePoints,28999,2,1;
		case 2:
			callsub S_ExchangePoints,28998,4,2;
		case 3:
			callsub S_ExchangePoints,28997,6,2;
		case 4:
			callsub S_ExchangePoints,28996,8,4;
		case 5:
			show "[Encarregado]";
			show "Por favor entre o número de vezes que você deseja converter 10 Pontos de Arena em Pontos da Turbo Track. O máximo que você pode colocar";
			show "é 20. Para cancelar, coloque '^3355FF0^000000.'";
			next;
			digit .@input;
			if (.@input == 0) {
				show "[Encarregado]";
				show "Você";
				show "cancelou";
				show "seu pedido.";
				close;
			}
			else if (.@input > 20) {
				show "[Encarregado]";
				show "Seu pedido excedeu";
				show "o limite máximo. Por favor";
				show "coloque um valor não maior que 20.";
				close;
			}
			else {
				set .@want_point1,10 * .@input;
				set .@want_point,5 * .@input;
				set .@my_turbo_all,tt_point+.@want_point;
				set .@my_arena_all,arena_point-.@want_point1;
				if (.@my_turbo_all > 28999) {
					show "[Encarregado]";
					show "Infelizmente, seus Pontos da Turbo Track excederão o limite máximo se nós procedermos com a conversão de pontos. Por favor gaste mais";
					show "de seus Pontos da Turbo Track antes de usar este serviço. Obrigado.";
					close;
				}
				if (.@my_arena_all < 0) {
					show "[Encarregado]";
					show "Eu sinto muito, mas você não";
					show "tem Pontos de Arena suficientes";
					show "para executar esta conversão de";
					show "Pontos da Turbo Track.";
					close;
				}
				else {
					show "[Encarregado]";
					show "Você converteu";
					show "10 Pontos de Arena em";
					show "Pontos da Turbo Track " + .@input + " vezes.";
					show "Um total de " + .@want_point1 + " Pontos de Arena";
					show "foram convertidos em";
					show "" + .@want_point +" Pontos da Turbo Track.";
					set arena_point,.@my_arena_all; //arena_point
					set tt_point,.@my_turbo_all;
					next;
					show "[Encarregado]";
					show "" + strcharinfo(0) + ",";
					show "você agora tem";
					show "^00688B" + tt_point + "^000000 Pontos da Turbo Track";
					show "e ^4682B4" + arena_point + "^000000 Pontos de Arena.";
					show "Obrigado pelo seu patrocínio.";
					close;
				}
			}
		case 6:
			show "[Encarregado]";
			show "Você";
			show "cancelou";
			show "seu pedido.";
			close;
		}
	case 3:
		// There is no dialog for this option.
		// It's possible it's missing, or maybe
		// the dialog was never translated. XD
		close;
	}

S_ExchangePoints:
	if (tt_point > getarg(0)) {
		show "[Encarregado]";
		show "Infelizmente, seus Pontos da Turbo Track excederão o limite máximo se nós procedermos com a conversão de pontos. Por favor gaste mais";
		show "de seus Pontos da Turbo Track antes de usar este serviço. Obrigado.";
		close;
	}
	if (arena_point >= getarg(1)) {
		show "[Encarregado]";
		show "Como pedido,";
		show "2 Pontos de Arena";
		show "foram convertidos";
		show "em 1 Ponto da Turbo Track.";
		set arena_point,arena_point-getarg(1);
		set tt_point,tt_point+getarg(2);
		next;
		show "[Encarregado]";
		show "" + strcharinfo(0) + ",";
		show "você agora tem";
		show "^00688B" + tt_point + "^000000 Pontos da Turbo Track";
		show "e ^4682B4" + arena_point + "^000000 Pontos de Arena.";
		show "Obrigado pelo seu patrocínio.";
		close;
	}
	else {
		show "[Encarregado]";
		show "Sinto muito, mas você não tem Pontos de Arena suficientes. Você precisa";
		show "de pelo menos 2 Pontos de Arena para";
		show "poder usar este serviço.";
		close;
	}
}

alde_gld,183,204,0	script	en_turbo	45,1,1,{
OnTouch:
	if (checkweight(1201,1) == 0) {
		show "^3355FFEspere um segundo!";
		show "Agora mesmo, você está carregando";
		show "muitos items com você.";
		show "Por favor volte depois que ";
		show "colocar algumas de suas coisas no armazém";
		show "usando o Serviço Kafra.^000000";
		close;
	}
	set .@now_weight,MaxWeight-Weight;
	if ((BaseJob == Job_Knight || BaseJob == Job_Crusader) && checkriding()) {
		if (.@now_weight < 20000) {
			show "PecoPecos são proibidos dentro da";
			show "Arena de Turbo Track. Mas qualquer";
			show "pessoa montando num PecoPeco";
			show "receberá um Bilhete para Passeio de";
			show "Peco na Entrada da Arena da Turbo";
			show "Track e desmontará automaticamente.";
			close2;
			warp "alde_gld",183,199;
			end;
		}
		else {
			setriding 0;
			additem 7310,1; //Free_Peco_Ticket
			warp "turbo_room",100,65;
		}
	}
	else
		warp "turbo_room",100,65;
	end;
}

alde_gld,186,199,3	script	Guia do Turbo Track#Entr	845,{
	show "[Guia]";
	show "Bem-vindo à";
	show "Turbo Track de";
	show "Al de Baran!";
	next;
	show "[Guia]";
	show "PecoPecos são proibidos dentro da";
	show "Arena de Turbo Track. Mas qualquer";
	show "pessoa montando num PecoPeco";
	show "receberá um Bilhete para Passeio de";
	show "Peco na Entrada da Arena da Turbo";
	show "Track e desmontará automaticamente.";
	next;
	show "[Guide]";
	show "[Guia]";
	show "Bem, então,";
	show "divirta-se";
	show "na Turbo Track!";
	close;
}

alde_gld,181,199,5	script	Encarregada do PecoPeco	845,{
	show "[Encarregada]";
	if (BaseJob == Job_Knight || BaseJob == Job_Crusader) {
		show "Bem-vindo, você gostaria de";
		show "receber seu PecoPeco?";
		show "Por favor me mostre seu";
		show "Bilhete para Passeio de Peco. Você";
		show "também pode pagar uma taxa de aluguel";
		show "de 2,500 zeny.";
		next;
		switch(select("Usar:Cancelar")) {
		case 1:
			if (getskilllv(63) == 0) {
				show "[Encarregada]";
				show "Eu sinto muito, mas você não está";
				show "elegível para este";
				show "serviço. Por favor vá aprender";
				show "primeiro a habilidade de Montaria.";
				close;
			}
			else {
				if (checkriding()) {
					show "[Encarregada]";
					show "Você já está";
					show "montado em um";
					show "PecoPeco.";
					show "Obrigada.";
					close;
				}
				else {
					if (countitem(7310) < 1 && Zeny < 2500) {
						show "[Encarregada]";
						show "Sinto muito, mas você";
						show "não tem um Bilhete para";
						show "Passeio de Peco ou 2,500 zeny.";
						show "para usar o serviço de aluguel de Peco.";
						close;
					}
					else {
						if (countitem(7310) > 0)
							delitem 7310,1; //Free_Peco_Ticket
						else
							set zeny,zeny-2500;
						setriding;
						show "[Encarregada]";
						show "Obrigada";
						show "pelo seu patrocínio!";
						close;
					}
				}
			}
		case 2:
			show "[Encarregada]";
			show "Você vai";
			show "voltar para a corrida";
			show "na Turbo Track?";
			show "Boa Sorte!";
			close;
		}
	}
	else {
		show "Obrigada por";
		show "visitar a Turbo Track";
		show "de Al De Baran!";
		close;
	}
}

alde_gld,178,180,0	script	Sinal#TBT	837,{
	show "[Turbo Track de Al De Baran]";
	show "Por aqui...";
	show "Arena de Turbo Track";
	show "de Al de Baran!";
	next;
	show "[Turbo Track de Al De Baran]";
	show "Turbo Track é uma arena onde";
	show "participantes competem para serem";
	show "os primeiros a atingir a Linha de";
	show "Chegada! Não perca a chance de";
	show "correr contra seus amigos!";
	close;
}
