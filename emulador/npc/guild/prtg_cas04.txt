//===== Cronus Script =========================================
//= Guerra do Emperium (Prontera)
//===== Por: ==================================================
//= L0ne_W0lf
//===== Versão Atual: =========================================
//= 1.2/1.1
//===== Compatível com: ========================================
//= Cronus SVN
//===== Descrição: =============================================
//= NPCs relativos ao 4º Castelo de Prontera
//===== Comentários Adicionais: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//= 1.2 Traduzido ao Português-BR de acordo com o bRo [Asus]
//= 1.2 Corrigido erros [Soulblaker]
//============================================================

// Guild Flag (Cidade)
//============================================================
prontera,167,203,6	script	Skoegul#p4-1::Skoegul	722,{
	end;

OnRecvCastlePt04:
	FlagEmblem GetCastleData("prtg_cas04",1);
	end;
}

// Guild Flag (Fora do Castelo)
//============================================================
prt_gld,120,243,6	script	Skoegul#p4-2::Skoegul2	722,{
	set .@GID, GetCastleData("prtg_cas04",1);
	if (.@GID == 0) {
		show "[ Decreto do Reino de Rune Midgard ]";
		show " ";
		show "1. Conforme o Pacto do Reino de Rune Midgard e o Feudo da Valquíria,";
		show "Declaramos que não há nenhum";
		show "dono formal deste castelo.";
		show " ";
		show "2. Se sua guild conseguir";
		show "passar por todos os desafios";
		show "e destruír o Emperium deste castelo,";
		show "você será nomeado como o dono deste castelo.";
	}
	else {
		if (getcharid(2) == .@GID) {
			show "Bravo guerreiro...";
			show "Você quer retornar ao seu honorável lugar?";
			next;
			if (select("Voltar ao castelo de sua Guild.:Sair.") == 1) {
				if (getcharid(2) == GetCastleData("prtg_cas04",1)) {
					warp "prtg_cas04",258,247;
					end;
				}
			}
			close;
		}
		show "[ Decreto do Reino de Rune Midgard ]";
		show " ";
		show "1. Conforme o Pacto do Reino de Rune Midgard e o Feudo da Valquíria,";
		show "declaro que este castelo pertence";
		show "ao Clã ^ff0000" + GetGuildName(.@GID) + "^000000.";
		show " ";
		show "2. O Líder do Clã ^ff0000"+ GetGuildName(.@GID) + "^000000 se chama";
		show "^FF0000" + GetGuildMaster(.@GID) + "^000000";
		show "Se alguém se opõe a este decreto";
		show "desembainhe sua espada para fazer valer sua vontade.";
	}
	close;

OnRecvCastlePt04:
	FlagEmblem GetCastleData("prtg_cas04",1);
	end;
}

prt_gld,120,236,6	duplicate(Skoegul2)	Skoegul#p4-3	722
prt_gld,122,243,6	duplicate(Skoegul2)	Skoegul#p4-4	722
prt_gld,122,236,6	duplicate(Skoegul2)	Skoegul#p4-5	722

// Guild Flag (Dentro do Castel)
//============================================================
prtg_cas04,82,29,4	script	Skoegul#p4-6::Skoegul3	722,{
	end;

OnRecvCastlePt04:
	FlagEmblem GetCastleData("prtg_cas04",1);
	end;
}

prtg_cas04,75,29,4	duplicate(Skoegul3)	Skoegul#p4-7	722
prtg_cas04,75,27,4	duplicate(Skoegul3)	Skoegul#p4-8	722
prtg_cas04,82,27,4	duplicate(Skoegul3)	Skoegul#p4-9	722
prtg_cas04,59,29,4	duplicate(Skoegul3)	Skoegul#p4-10	722
prtg_cas04,67,29,4	duplicate(Skoegul3)	Skoegul#p4-11	722
prtg_cas04,258,25,4	duplicate(Skoegul3)	Skoegul#p4-12	722
prtg_cas04,258,20,4	duplicate(Skoegul3)	Skoegul#p4-13	722
prtg_cas04,263,20,4	duplicate(Skoegul3)	Skoegul#p4-14	722
prtg_cas04,263,27,4	duplicate(Skoegul3)	Skoegul#p4-15	722

// AGIT Manager
//============================================================
prtg_cas04,292,14,0	duplicate(Gld_Agit_Manager)	Agit#prtg_cas04	-1

// Steward
//============================================================
prtg_cas04,259,265,4	duplicate(Gld_Mngr_Template)	Van Dreth#prtg_cas04	55

// Guardians
//============================================================
prtg_cas04,259,265,4	duplicate(Gld_Guard_Template)	Guardian#prtg_cas04	-1

// Serviço Kafra
//============================================================
prtg_cas04,258,247,4	duplicate(Gld_Kafra_Template)	Kafra#prtg_cas04	117

// Saída da Sala do Mestre
//============================================================
prtg_cas04,275,160,0	script	#switch_Pt04	111,{
	show " ";
	show "Existe uma pequena alavanca. Será que você puxa-la?";
	next;
	switch(select("Puxar.:Não puxar.")) {
	case 1:
		warp "prtg_cas04",259,265;
		break;
	case 2:
		break;
	}
	end;
}

// Guild Treasure Protection
//============================================================
prtg_cas04,271,162,0	duplicate(Gld_Trea_Protect)	ban_warp#prtg_cas04	-1,6,6

// Guild Treasure Spawns
//============================================================
prtg_cas04,271,162,0	duplicate(Gld_Trea_Spawn)	Treasure#prtg_cas04	-1

// Guild Dungeon Switch
//============================================================
prtg_cas04,56,283,0	duplicate(Gld_Dun_Template)	#prtg_cas04	111
