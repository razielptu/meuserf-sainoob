//===== Cronus Script ========================================
//= Loja de Alquimista
//===== Por: =================================================
//= eAthena Team
//===== Versão Atual: ========================================
//= 1.1
//===== Compatível Com: ======================================
//= Cronus
//===== Descrição: ===========================================
//= Vendedor de Materiais e Manuais de Alquimia.
//===== Comentários Adicionais: ==============================
//= 1.0 Traduzido o NPC. [Kaells]
//= 1.1 Atualizado, reestruturado e corrigido erros. [SoulBlaker]
//============================================================
alde_alche,24,188,3	script	Comerciante da Guilda#01	740,{
	if (checkweight(1201,1) == 0) {
		show "- Espere um minuto! -";
		show "- Atualmente você está carregando -";
		show "- muitos itens com você. -";
		show "- Por favor, volte novamente -";
		show "- apos deixar alguns itens no Armazen da Kafra. -";
		close;
	}
	show "[Gever Al Sharp]";
	show "Bem-vindo a";
	show "União dos Alquimista.";
	show "Como posso ajudar você?";
	next;
	switch(select("Comprar materiais.:Comprar manual de produção.:Cancelar.")) {
	case 1:
		show "[Gever Al Sharp]";
		show "O que você gostária de comprar?";
		next;
		if (select("Vasilha de Mistura - 8 Zeny:Cancelar.") == 1) {
			show "[Gever Al Sharp]";
			show "Quantas você deseja?";
			show "Digite '0' se você deseja cancelar.";
			next;
			while(1) {
				digit .@input,0,2001;
				if (.@input == 0) {
					show "[Gever Al Sharp]";
					show "A operação foi cancelada.";
					show "Na próxima vez, volte aqui novamente.";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					show "[Gever Al Sharp]";
					show "O número deve";
					show "ser inferior a 2000.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				show "[Gever Al Sharp]";
				show "Vejo que você não";
				show "tem zeny suficiente.";
				show "Verifique quanto você";
				show "tem de zeny primeiro.";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				show "[Gever Al Sharp]";
				show "Me parece que você não";
				show "pode carregar tudo.";
				show "Verifique o espaço";
				show "em seu iventário.";
				close;
			}
			set zeny,zeny-.@sell;
			additem 7134,.@input; //Medicine_Bowl
			show "[Gever Al Sharp]";
			show "Obrigado.";
			show "Volte sempre.";
			close;
		}
		show "[Gever Al Sharp]";
		show "Pois bem,";
		show "volte novamente caso";
		show "você nescessitar comprar";
		show "materiais relacionados a";
		show "Alquimia, certo?";
		close;
	case 2:
		show "[Gever Al Sharp]";
		show "Do que você precisa?";
		show "Os manuais geralmente são 100,000 zeny. Mas há uma par de manuais especial que irá custar muito mais.";
		next;
		switch(select("Manual de Criação de Poções:Manual de Criação de Alcool:Manual de Criação de Fogo Grego:Manual de Criação de Pote de Acido:Manual de Criação de Plantas:Manual de Criação de Esfera Marinha:Manual de Criação de Revestimentos:Manual de Criação de Poções Condensadas:Cancelar.")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			show "[Gever Al Sharp]";
			show "Pois bem...";
			show "Volte, caso você tenha";
			show "nescessidade de comprar";
			show "manual de produção.";
			close;
		}
	case 3:
		show "[Gever Al Sharp]";
		show "Certo, então";
		show "tenha um bom dia.";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		show "[Gever Al Sharp]";
		show "Vejo que você não";
		show "tem zeny suficiente.";
		show "Verifique quanto você";
		show "tem de zeny primeiro.";
		close;
	}
	show "[Gever Al Sharp]";
	show ""+getitemname(getarg(0))+"?";
	show "Isso vai custar "+getarg(1)+" zeny.";
	next;
	switch(select("Comprar.:Cancelar.")) {
		set zeny,zeny-getarg(1);
		additem getarg(0),1;
		show "[Gever Al Sharp]";
		show "Obrigado pelo";
		show "seu patrocínio.";
		close;
	}
	show "[Gever Al Sharp]";
	show "Volte novamente";
	show "outra hora.";
	close;
}