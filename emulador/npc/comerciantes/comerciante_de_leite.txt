//===== Cronus Script =======================================
//= Comerciante de Leite
//===== Por: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: =====================================
//= 1.5/1.6
//===== Compatível com: =====================================
//= Cronus SVN
//===== Descrição: =========================================
//= Troca Garrafas vazias por Leite!!
//= [ Oficial do bRO ]
//===== Comentários Adicionais: =================================
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Raised the price to close zeny exploit [Lupus]
//= 1.2a Switched to Lupus's "loopless" technique.[kobra_k88]
//= 1.3 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4 Implemented checkweight. [L0ne_W0lf]
//= 1.5 Fixed missed variable. (bugreport:1523) [L0ne_W0lf]
//= 1.6 Realizado a tradução baseado no bRO [Asus]
//============================================================

prontera,73,140,0	script	Vendedor de Leite	86,{
	if (checkweight(1201,1) == 0) {
		show "^3355FFEspere um momeno!";
		show "Eu não posso oferecer meu serviço";
		show "à você porque está";
		show "carregendo itens demais com você";
		show "Volte após depositá-los na Kafra";
		close;
	}
	show "[Leiteiro]";
	show "Se você trouxer uma garrafa vazia e 15 Zeny, eu troco por 1 Leite!!";
	next;
	if (select("Trocar todas minhas garrafas.:Cancelar") == 1) {
		if (countitem(713) <= 0) {
			show "[Leiteiro]";
			show "Er...Eu preciso de Garrafas Vazias";
			show "para encher de leite...";
			show "Traga algumas e fazenis a troca, ok?";
			close;
		}
		set .@bottles,countitem(713);
		set .@total_weight,.@bottles * 50;
		set .@total_cost,.@bottles * 15;
		if (zeny < .@Total_cost) {
			show "[Leiteiro]";
			show "Er... Você não tem a quantia";
			show "Você precisa ter";
			show "pelo menos " + .@total_cost + " zeny.";
			close;
		}
		if ((maxweight-weight) < .@total_weight) {
			show "[Leiteiro]";
			show "Hmm...";
			show "Você teria um pouco mais";
			show "de espaço antes de entregar";
			show "todo esse leite?";
			close;
		}
		set zeny,zeny-.@total_cost;
		delitem 713,.@bottles; //Garrafas Vazias
		additem 519,.@bottles; //Leite
		close;
	}
	close;
}