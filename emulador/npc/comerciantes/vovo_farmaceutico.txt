//===== Cronus Script =======================================
//= Vovô Farmacêutico
//===== Por: ==================================================
//= kobra_k88
//===== Versão Atual: =====================================
//= 1.2.3c
//===== Descrição: =========================================
//= Troca itens por poções
//===== Comentarios Adicionais: ================================= 
//= 1.2.1a Traduzido por old7
//= 1.2.2a Adaptado Tradução para bRO [Mehah]
//= 1.2.3b Corrigido alguns bugs [Mehah]
//= 1.2.3c Erros de português corrigido por @Coja ImortalRO Staff
//============================================================


alberta_in,16,28,4	script	Vovô Farmacêutico	61,{
	show "[Vovô Farmacêutico]";
	show "Hmmm... o que você quer...?";
	next;
	menu "Fazer poções",M_Make, "Conversar",M_Talk, "Informações",M_Info, "Cancelar",M_End;
	
	M_Make:
		show "[Vovô Farmacêutico]";
		show "Você preparou todos os itens necessários? Se sim que poção você quer?";
		M_Menu:
		next;
		menu "Poção Vermelha",M_0, "Poção Laranja",M_1, "Poção Amarela",M_2,
			 "Poção Branca",M_3, "Poção Azul",M_4, "Poção Verde",M_5,
			 "Nah, Eu mudei de idéia.",M_End;
		
		M_0:
			callsub sF_Make, 507, 0, 2, 501, "red";
			goto M_Menu;
		M_1:
			callsub sF_Make, 507, 508, 5, 502, "orange";
			goto M_Menu;
		M_2:
			callsub sF_Make, 508, 0, 10, 503, "yellow";
			goto M_Menu;
		M_3:
			callsub sF_Make, 509, 0, 20, 504, "white";
			goto M_Menu;
		M_4:
			callsub sF_Make, 510, 0, 30, 505, "blue";
			goto M_Menu;
		M_5:
			callsub sF_Make, 511, 0, 3, 506, "green";
			goto M_Menu;

	M_Talk:
		show "[Vovô Farmacêutico]";
		show "Certo tipo de ervas medicinais podem recuperar o HP ou o SP de uma pessoa";
		show "~Suspiro~ Eu estou começando relembrar minha juventude.... um sinal que eu";
		show "devo estar começando a ficar velho.....";
		next;
		show "[Vovô Farmacêutico]";
		show "... Uma poção é meramente uma forma de herva medicinal de fácil uso";
		show "..... nada mais e nada menos.";
		close;
	M_Info:
		show "[Vovô Farmacêutico]";
		show "~Suspiro~... vocês jovens gostam de me aborrecer..  Muito bem, eu";
		show "explico como os poções funcionam....";
		next;
		show "[Vovô Farmacêutico]";
		show "Apesar dos beneficios de consumir várias ervas encontradas ao redor da";
		show "grande Rune-Midgard... Refinando elas em poções";
		show "Os efeitos das ervas, são dramaticamente aumentados.";
		next;
		show "[Vovô Farmacêutico]";
		show "Eu criei um processo especial para refinar ervas em poções.";
		show "Por uma pequena quantia faço quantas poçoes desejar.";
		next;
		show "[Vovô Farmacêutico]";
		show "^FF5533Red Potion^000000 - 2 Ervas Vermelha, 1 Garrafa Vazia, 2 Zeny de taxa.";
		show "^FF8000Orange Potion^000000 - 1 Erva Vermelha, 1 Erva Amarela, 1 Garrafa Vazia, 5 Zeny de taxa.";
		show "^E8CF20Yellow Potion^000000 - 2 Ervas Amarela, 1 Garrafa Vazia, 10 Zeny de taxa.";
		next;
		show "[Vovô Farmacêutico]";
		show "^999999White Potion^000000 -  2 Ervas Verde, 1 Garrafa Vazia, 20 Zeny de taxa.";
		show "^3355FFBlue Potion^000000 - 2 Ervas Azul, 1 Garrafa Vazia, 30 Zeny de taxa.";
		show "^00B000Green Potion^000000 - 2 Ervas Verde, 1 Garrafa Vazia, 3 Zeny de taxa.";
		close;
	M_End:
		show "[Vovô Farmacêutico]";
		show "Você não tinha algo para me dizer?!";
		close;

// Subfunção para fazer poções
//================================
sF_Make:
	set @herbnum, 2;
	if(getarg(1) != 0) set @herbnum, 1;
	if(countitem(getarg(0)) < @herbnum) goto L_NdHerbs;
	if(getarg(1) != 0) if(countitem(getarg(1)) < @herbnum) goto L_NdHerbs;
	if(countitem(713) < 1) goto L_NdBottle;
	if(Zeny < getarg(2)) goto L_NdZeny;

	show "[Vovô Farmacêutico]";
	show "Quantas?";
	next;
	menu "Quantas for possível.",sM_0a, "Eu ajustarei a quantidade.",sM_0b, "Nah, esquece",M_End;

	sM_0a:
		set @amount, 1000;
		if(zeny/getarg(2) < @amount) set @amount, zeny/getarg(2);
		if(countitem(getarg(0))/@herbnum < @amount) set @amount, countitem(getarg(0))/@herbnum;
		if(getarg(1) != 0) if (countitem(getarg(1))/@herbnum < @amount) set @amount, countitem(getarg(1))/@herbnum;
		if(countitem(713) < @amount) set @amount, countitem(713);
		if(@amount > 0) goto L_End;
		show "[Vovô Farmacêutico]";
		show "Jeez... você não tem os itens certos.....";
		close;
		
	sM_0b:
		digit @amount;
		if(@amount<1 || @amount>1000) goto L_BadAmnt;
		if(countitem(getarg(0))/@herbnum < @amount) goto L_NdHerbs;
		if(getarg(1) != 0) if (countitem(getarg(1))/@herbnum < @amount) goto L_NdHerbs;
		if(countitem(713) < @amount) goto L_NdBottle;
		if(Zeny < (getarg(2)*@amount)) goto L_NdZeny;

	L_End:
		set Zeny, Zeny - (getarg(2)*@amount);
		delitem getarg(0), (@amount*@herbnum);
		if(getarg(1) != 0) delitem getarg(1), (@amount*@herbnum);
		delitem 713, @amount;
		additem getarg(3), @amount;
		show "[Vovô Farmacêutico]";
		show "Aqui estão suas " +getarg(4)+ " poções.";
		close;

	L_NdBottle:
		show "[Vovô Farmacêutico]";
		show "Você não tem garrafas vazias suficientes para pôr a solução. Você é um idiota!!";
		return;

	L_NdHerbs:
		show "[Vovô Farmacêutico]";
		show "Seu trapaceiro!  Que você espera de mim?  Você não trouxe todas as ervas certas!";
		return;

	L_NdZeny:
		show "[Vovô Farmacêutico]";
		show "Voce não tem zeny suficiente para tantas poções.";
		return;

	L_BadAmnt:
		show "[Vovô Farmacêutico]";
		show "O que?! Esta não é uma quantia válida!";	
		return;
}
