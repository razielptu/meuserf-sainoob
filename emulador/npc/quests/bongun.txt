//===== Cronus Script ========================================
//= Acessório de Munak e Bongun
//===== Por: =================================================
//= eAthea Team
//===== Versão Atual: ========================================
//= 1.4/1.2
//===== Compatível Com: ======================================
//= Cronus
//===== Descrição: ===========================================
//= Quest de Acessórios de Munak e Bongun
//===== Comentários Adicionais: ==============================
//= 1.2a Traduzido o NPC.          [ [NITRO]Fox ]
//= 1.2b Traduzido Itens da quest. [Dynasty]
//= 1.3 Corrigido nomes dos NPCs.  [Dynasty]
//= 1.4 Reestruturado e corrigido umas palavras que estavão
//=     em inglês. [SoulBlaker]
//============================================================

comodo,135,227,5	script	Sherri#acess_munak	93,{
	show "[Sherri]";
	show " ";
	show "*suspiros*";
	next;
	show "[Sherri]";
	show "Hã? Qual o problema? ...Bem, finalmente eu consegui envolver a alma de um garoto muito bonito...";
	next;
	show "[Sherri]";
	show "...então por que estou triste?";
	show " ";
	show "É porque eu nem sei como a personalidade dele é... diga, se você pode achar para mim um 'Diário de Menina'sobre ele, eu lhe darei o diário dele!";
	next;
	show "[Sherri]";
	show "Você pode me ajudar por favor?";
	next;
	switch(select("O que eu preciso novamente?:Aqui! Eu peguei o que você precisa!:Desculpa, eu não posso lhe ajudar.")){
		case 1:
			show "[Sherri]";
			show "O que eu desejo é...";
			show " ";
			show "Um 'Diário de Menina' somente ele...";
			close;
		case 2:
			show "[Sherri]";
			show "Sério!!Você irá me ajudar!";
			next;
			show "[Sherri]";
			if(countitem(1558)<1){ //Item: Diária de Menina
				show "Aahhhh, pare de brincar com meus sentimentos!";
				show " ";
				show "Vá embora!";
			}
			else {
				delitem 1558,1;//Items: Diário de Menina,
				additem 659,1;//Items: Coração Apaixonado,
				show "Sim!Eu estarei sempre lhe devendo essa!";
				show " ";
				show "Aqui está o diário dele como prometido!Novamente - Obrigada!";
				next;
				show "[Sherri]";
				show "Espere! Aqui está a espada dele, eu a encontrei perto dele. Mas está quebrada... Eu sei de um homem velho a leste de Geffen que pode consertar uma espada como essa!";
				additem 7110,1;//Items: Espada Chanfrada,
			}
			close;
		case 3:
			show "[Sherri]";
			show "*suspiros*";
			close;
	}
}

izlude_in,115,76,5	script	Evan#acess_munak	47,{
	show "[Evan]";
	show " ";
	show "*suspiros*";
	next;
	show "[Evan]";
	show "Hã? Qual o problema? ...Bem, eu finalmente consegui envolver a alma de uma linda garota...";
	next;
	show "[Evan]";
	show "...então por que eu estou triste?";
	show " ";
	show "é porque eu nem sei nem como ela se parece...diga,se você pode achar para mim 'Retrato Velho'dela, eu lhe darei a carta perdida do amado dela!";
	next;
	show "[Evan]";
	show "O que você me diz? ..parceiro?";
	next;
	switch(select("O que eu preciso novamente?:Aqui! Eu peguei o que você precisa!:Desculpa, eu não posso lhe ajudar")){
		case 1:
			show "[Evan]";
			show "Uma gravura do amável rosto dela...";
			show " ";
			show "Um 'Retrato Velho' de preferencia...";
			close;
		case 2:
			show "[Evan]";
			show "Sério!!Você irá me ajudar!";
			next;
			show "[Evan]";
			if(countitem(7014)<1){ //Items: Retrato Velho
				show "Aahhhhh, pare de brincar com meus sentimentos!";
				show " ";
				show "Vá embora!";
			}
			else {
				delitem 7014,1;//Items: Retrato Velho,
				additem 636,1;//Items: Sem Destinatário,
				show "Sim!Eu estarei sempre lhe devendo essa!";
				show " ";
				show "Aqui está a carta de amor perdida como prometido! Novamente - Obrigado!";
			}
			close;
		case 3:
			show "[Evan]";
			show "*suspiros*";
			close;
	}
}

gef_fild07,183,239,5	script	Velho Ferreiro::acess_munak	120,{
	show "[Velho Ferreiro]";
	show "Ah, aqui em cima não é tão calmo - Eu costumava ser um dos lendários ferreiros de midgard...infelizmente minhas habilidades lentamente se acabaram.";
	next;
	show "[Velho Ferreiro]";
	if(countitem(7110)<1){ //Items: Espada Chanfrada,
		show "Hmm, você parece estar esquecendo alguma coisa... provavelmente aquele 'Oridecon'.";
		close;
	}
	show "Hmm, parece que você precisa algo. O que poderia ser?";
	next;
	if(select("Você pode consertar essa espada quebrada?:Nada de importante...")==1){
		show "[Velho Ferreiro]";
		show "Ahhh, esta é uma espada bem velha e sim eu posso concertá-la, eu precisarei um 'Oridecon' para reparar os estragos porém.";
		next;
		show "[Velho Ferreiro]";
		if(countitem(7110)<1 || countitem(984)<1) //Items: Espada Chanfrada, Oridecon,
			show "Hmm, você parece estar esquecendo alguma coisa... provavelmente aquele 'Oridecon'.";
		else {
			delitem 7110,1;//Items: Espada Chanfrada,
			delitem 984,1;//Items: Oridecon,
			additem 10020,1;//Items: Espada chinesa,
			show "Ah sim aqui vamos nós!";
			show " ";
			show "Haha!Vovô ainda tem aquele velho toque mágico, aqui vai você!";
		}
		close;
	}
	show "[Velho Ferreiro]";
	show "...ah bem, vá embora e viva a vida jovem. O tempo nunca para!";
	close;
}