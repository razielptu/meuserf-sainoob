//===== Cronus Script =======================================
//= Quests de Umbala
//===== Por: ==================================================
//= sabernet09 & eAthena Team
//===== Versão Atual: =====================================
//= 2.5a
//===== Compatível Com: =====================================
//= Cronus SVN
//===== Descrição: =========================================
//= [Aegis Conversion]
//= Umbalian Language Quest, Umbala Skeletal Gate Quest,
//= Umbala Event, Create Essence/Dismantle Stone
//	Phase1.Learning the Language
//	1	(Elder)First discussion
//	2	(Elder)Asking about learning the language
//	3	(Elder)Final step (understanding NPC speech)
//	------------------------------------------
//	Phase2.Create Essence/Dismantle Stone
//	4	(Shaman)Get permission to speak with her from the chief
//	5	(Elder)Ask about conditions needed to be fulfilled to get permission
//	6	(Elder)Get permission
//	7	(Shaman)Use the shaman to create essences and dismantle elemental stones.
//= Umbala Domestic Dispute?
//= - This quest is only avaliable if you ahve not done language quest.
//= - Variables in use: um_wind, MISC_QUEST (Bit 32768)
//===== Comentários Adicionais: =================================
//= fixed by x[tsk], Lupus, PoW
//= Quest completion is now registered
//= 1.1 fixed Poporing Egg ID -> Poring Egg, fixed condition
//= 1.2-1.6 fixed some Umbalian Language quests [Lupus]
//= 1.7 Moved Umbala Event, Create Essence/Dismantle Stone, and Skeletal Gate from npc/cities/umbala.txt [Evera]
//= 1.7a Removed all custom quests to custom folder [Lupus]
//= 1.8 Added official chance of failure to Create Essence/Dismantle Stone [Lupus]
//= 1.9 Added functionality to allow players to dismantle more
//= than a stone at once, up to 10. [SinSloth]
//= 2.0 Updated hack check and added a weight check to dismantling. [SinSloth]
//= 2.1 Rescripted to Aegis 10.3 standards. Added strange quest. [L0ne_W0lf]
//= 2.2 Readded the ability to dismantle multiple stones at once. [L0ne_W0lf]
//= 2.2a Fixed weight check from previous update. [L0ne_w0lf]
//= 2.5 Traduzido para o português [. kurono~]
//= 2.5a Revisão geral do script [Asus]
//= - Obrigado Renan freak e LEozin pela ajuda
//============================================================

//==============================================================================
// Umbala Language Quest
//==============================================================================
um_in,39,122,5	script	Chefe Utan	784,{
	if (event_umbala == 0) {
		show "[Karkatan]";
		show "Huh huh, uma pessoa de Rune-Midgard.";
		show "Eu acho que essa é a sua primeira";
		show "visita à minha vila, não é?";
		next;
		show "[Karkatan]";
		show "Todo mundo de Rune-Midgard";
		show "que eu conheci tem as";
		show "mesmas expressões faciais";
		show "que você está tendo quando";
		show "vem aqui pela primeira vez.";
		next;
		show "[Karkatan]";
		show "Talvez porque não dá para se";
		show "comunicar com a gente usando a";
		show "mesma língua, então";
		show "eles não tem idéia de onde";
		show "estão. Sim, eu entendo...";
		show "Enfim, bem vindo a minha aldeia.";
		next;
		show "[Karkatan]";
		show "Meu nome é Karkatan, e eu";
		show "sou o chefe da tribo Utan.";
		show "Você deve está espantado como";
		show "eu posso falar a sua língua.";
		next;
		show "[Karkatan]";
		show "Eu aprendi a muito tempo atrás";
		show "com um aventureiro de sua";
		show "terra. Foi a muito tempo atrás,";
		show "e eu não sei o que aconteceu";
		show "com ele...";
		next;
		show "[Karkatan]";
		show "Enfim, eu aprendi muitas coisas";
		show "sobre a cultura e a língua";
		show "de Rune-Midgard.";
		next;
		show "[Karkatan]";
		show "Algumas vezes, eu ensino a";
		show "língua Utan, mas eu não dou a";
		show "todo mundo esse privilégio.";
		show "Se pessoas erradas aprenderem";
		show "a língua Utan, eles poderão";
		show "trazer desgraça a nossa tribo.";
		next;
		show "[Karkatan]";
		show "Antes de você poder aprender a";
		show "língua Utan, primeiro aprendan a";
		show "cultura Utan explorando a nossa aldeia.";
		next;
		show "[Karkatan]";
		show "Embora você não seja capaz de se";
		show "comunicar com o meu povo";
		show "agora, tentar compreender";
		show "nosso modo de vida através de";
		show "observações";
		next;
		show "[Karkatan]";
		show "Preste atenção nas roupas,";
		show "na aparência e no estilo de vida da";
		show "população local. Quando você pensar que";
		show "aprendeu o suficiênte da cultura";
		show "Utan, volte e me mostre o que você aprendeu.";
		set event_umbala,1;
		close;
	}
	else if (event_umbala == 1) {
		show "[Karkatan]";
		show "Oh, é você de novo...";
		show "Você aprendeu sobre a cultura";
		show "Utan? Eu quero ouvir a sua";
		show "opinião, assim como sua impressão.";
		next;
		show "[Karkatan]";
		show "Existem ainda alguns aldeões";
		show "que são muito ingênuos";
		show "a população de Rune-Midgard.";
		show "Normalmente, eles temem encontros";
		show "com o seu povo e se escondem.";
		next;
		show "[Karkatan]";
		show "Então...";
		show "Posso te ajudar em alguma coisa?";
		show "Eu presumo que você não teve muitos";
		show "problemas em visitar a aldeia,";
		show "aldeia, mas parece que você tem algo a me pedir.";
		next;
		switch(select("Eu quero aprender a língua Utan.:Umbabah Umbabah?:Nada.")) {
		case 1:
			if (isequipped(2278) || isequipped(2297) || isequipped(2288) || isequipped(2292) || isequipped(5005) || isequipped(2281) || isequipped(5043)) {
				show "[Karkatan]";
				show "Hmmm... é uma máscara impressionante";
				show "que você está usando. Nós, Utans";
				show "usamos máscaras para não exibir";
				show "nossas expressões faciais.";
				next;
				show "[Karkatan]";
				show "É por isso que usamos máscaras";
				show "o tempo todo. Nós acreditamos que";
				show "a interação e o tratamento com as";
				show "outras pessoas não devem depender";
				show "de como a olhamos.";
				next;
				show "[Karkatan]";
				show "Então. Eu tenho certeza que você";
				show "está qualificado para aprender a";
				show "língua Utan. Eu vou te ensinar como";
				show "falar e ler em Utan apartir de agora.";
				next;
				show "[Karkatan]";
				show "No entanto, eu preciso que você me";
				show "traga itens para podermos proseguir";
				show "com as lições. Primeiro, precisamos";
				show "de dois tipos diferentes de papel.";
				show "^3377FF10 Papel Amarelado^000000 and ";
				show "^3377FF5 Pedaço de Papel^000000.";
				next;
				show "[Karkatan]";
				show "Nós iremos precisar escrever";
				show "também. Vamos usar";
				show "^3377FF1 Tinta de Polvo^000000 e";
				show "^3377FF1 Plumas de Ave^000000.";
				show "Por favor traga esses itens, e eu";
				show "vou ensinar quando você estiver pronto.";
				set event_umbala,2;
				close;
			}
			else {
				show "[Karkatan]";
				show "Não parece que você aprendeu";
				show "a nossa cultura ainda. Você não";
				show "pode aprender uma outra língua se";
				show "você não entender a cultura.";
				next;
				show "[Karkatan]";
				show "Enquanto você tiver aquela expressão";
				show "em sua face, Utans irão ficar";
				show "intimidados... apenas não ficarão";
				show "se você não mostrar sua face";
				show "para os outros, pois somos";
				show "muito vulneráveis a expressões faciais.";
				next;
				show "[Karkatan]";
				show "Vá explorar a nossa vila mais um";
				show "pouco. Você poderá voltar assim";
				show "que achar que estiver pronto.";
				next;
				show "[Karkatan]";
				show "Qualquer coisa, o que você pensa";
				show "sobre minha máscara? É a";
				show "tendência dos Utans... você não";
				show "acha isso interessante?";
				close;
			}
		case 2:
			show "[Karkatan]";
			show "Haha~ quando você não está prestando";
			show "atenção no som, nada faz sentido.";
			show "A língua é um sistema de mútua";
			show "para a comunicação de pensamentos e sentimentos";
			next;
			show "[Karkatan]";
			show "Lamento dizer que parece que";
			show "hoje em dia, não são todos os povos";
			show "dispostos a aprender outras culturas.";
			next;
			show "[Karkatan]";
			show "Eu vejo pessoas que desprezam ou";
			show "ridicularizam outros que não os";
			show "compreendêem. É realmente";
			show "triste que esse fanatismo ainda existe...";
			next;
			show "[Karkatan]";
			show "Se você estiver interessado na";
			show "língua Utan, tente entender nossa";
			show "cultura melhor e volte";
			show "quando estiver pronto. Eu vou";
			show "te ensinar os significados desses";
			show "sons que você está ouvindo.";
			close;
		case 3:
			show "[Karkatan]";
			show "Algumas vezes é bom para passear";
			show "sem propósito. Mas é";
			show "melhor definir uma meta para";
			show "uma viagem se você quiser";
			show "aprender alguma coisa fora do";
			show "seu comum.";
			close;
		}
	}
	else if (event_umbala == 2) {
		if ((countitem(7151) > 9) && (countitem(7111) > 4) && (countitem(1024) > 0) && (countitem(916) > 0)) {
			show "[Karkatan]";
			show "Certo, eu acho que nós estamos";
			show "prontos para ir.";
			show "Vamos começar as lições.";
			show "Espero que você se comunique melhor";
			show "com os Utans quando as lições";
			show "tiverem sido completas.";
			next;
			show "[Karkatan]";
			show "..............";
			next;
			show "[Karkatan]";
			show "..............";
			show ".....................";
			next;
			show "[Karkatan]";
			show "..............";
			show ".....................";
			show "............................";
			next;
			show "[Karkatan]";
			show "Certo, é isso. Apenas esqueça";
			show "como você se sentiu quando viu os";
			show "Utans pela primeira vez";
			show "antes de ter aprendido a língua Utan.";
			show "Agora vá tentar falar com os Utans.";
			show "Conversação é um importante método";
			show "para entender os outros.";
			delitem 7151,10; //Papel Amarelado
			delitem 7111,5; //Pedaço de Papel
			delitem 1024,1; //Tinta de Polvo
			delitem 916,1; //Plumas de Ave
			set event_umbala,3;
			next;
			show "[Karkatan]";
			show "Ok, se você tiver qualquer negócio";
			show "em nossa vila, fique a vontade";
			show "para falar comigo. Eu vou tentar te";
			show "ajudar no que eu puder.";
			close;
		}
		else {
			show "[Karkatan]";
			show "Eu acho que você não está";
			show "pronto ainda... Você se";
			show "esqueceu dos itens que você";
			show "precisa? Eu vou te falar";
			show "de novo, então por favor traga-me";
			show "e então começaremos a lição.";
			next;
			show "[Karkatan]";
			show "^3377FF10 Papel Amarelado^000000,";
			show "^3377FF5 Pedaço de Papel^000000,";
			show "^3377FF1 Tinta de Polvo^000000,";
			show "^3377FF1 Plumas de Ave^000000.";
			show "Quando você trouxer todos os,";
			show "itens, eu irei te ensinar a língua Utan.";
			close;
		}
	}
	else if (event_umbala >= 3) {
		if (event_umbala == 4) {
			show "[Karkatan]";
			show "Puchuchartan deve ter mandado você";
			show "aqui. Eu preciso verificar se";
			show "você está ou não qualificado";
			show "para o pedido de ajuda dela...";
			show "Nós, Utan não queremos";
			show "para ajudar as pessoas do mal";
			next;
			show "[Karkatan]";
			show "Hmmmm....";
			show "Seria bom ter uma máscara";
			show "que é feita em Rune-Midgard...";
			next;
			show "[Karkatan]";
			show "Eu gostaria de ter ^3377FF1 Máscara Feliz^000000.";
			show "Para os Utans, receber uma máscara";
			show "de presente é considerada uma";
			show "honra. Talvez Puchuchartan";
			show "quer que você mostre respeito";
			show "perante a nossa aldeia.";
			set event_umbala,5;
			close;
		}
		else if (event_umbala == 5) {
			if (countitem(2278) > 0) {
				show "[Karkatan]";
				show "Oh, você a trouxe! Sim, eu";
				show "sempre quis ter essa máscara!";
				show "Isso é uma verdadeira honra.";
				show "Obrigado, aventureiro de Rune-Midgard.";
				next;
				show "[Karkatan]";
				show "Eu vou dizer a Puchuchartan que";
				show "eu confirmei sua qualificação.";
				show "Vá falar com ela. Embora eu";
				show "não esteja certo que da ajuda que";
				show "ela possa te dar.";
				show "Eu espero que ela possa";
				show "retribuir o seu favor.";
				delitem 2278,1; //Máscara Feliz
				set event_umbala,6;
				close;
			}
			else {
				show "[Karkatan]";
				show "Eu te disse o que você precisa?";
				show "^3377FF1 Máscara Feliz^000000?";
				show "Por favor, traga-me como prova";
				show "de sua eficiência, assim como";
				show "o senso de sua honra.";
				close;
			}
		}
		else {
			show "[Karkatan]";
			show "Como está indo?";
			show "Gostaria de poder orientá-lo ao redor";
			show "da aldeia, mas não posso ignorar";
			show "meu dever, como chefe da tribo";
			next;
			show "[Karkatan]";
			show "Comandar uma tribo não é tão";
			show "fácil quanto parece. Você deve entender";
			show "se você estivesse no meu lugar.";
			show "No entanto, eu espero que você aprecie sua estada na nossa aldeia.";
			close;
		}
	}
}

//=====================================================================
//                    Create Essence/Dismantle Stone
//=====================================================================
um_in,44,71,2	script	Utan Shaman	782,{
	if (checkweight(908,600) == 0) {
		show "^3355FFEspero um segundo!";
		show "Certo, você está carregando";
		show "muita coisa com você.";
		show "Por favor, volte assim que";
		show "estiver usado o serviços Kafra";
		show "para depositar alguns itens.^000000";
		close;
	}
	switch(event_umbala) {
	default:
		show "[??????????]";
		show "Umbah umbah umbabah Utan umbah";
		show "Umbah mookala umbabah..";
		show "Umbabahumbah umbabah";
		show "Umbabah umbaba umbaumbah umbah";
		show "Hum umbah umbah.";
		close2;
		warp "umbala",217,186;
		end;
	case 3:
		show "[Puchuchartan]";
		show "Eu não esperava que mais";
		show "Rune-Midgardianos iriam";
		show "encontrar a minha aldeia. Tenho medo";
		show "de que a Mãe Terra pode";
		show "ser causado devido";
		show "a este sofrimento ...";
		next;
		show "[Puchuchartan]";
		show "Vocês, Rune-Midgardianos são de uma";
		show "tribo do mal... sempre realizando";
		show "seus objetivos, com meios maus";
		show "ou injusto, nunca hesitando em";
		show "estragar a propriedade dos outros para";
		show "chegar ao que vocês querem.";
		next;
		show "[Puchuchartan]";
		show "Eu estou preucupado com continuar";
		show "o contato com os outros estranhos";
		show "sem que afete nosso futuro...";
		next;
		show "[Puchuchartan]";
		show "Ninguém de Rune-Midgard visitou";
		show "sem algum interesse e eu";
		show "não acho que você seja excessão";

		next;
		show "[Puchuchartan]";
		show "Eu só uso o meu poder para a";
		show "serviço da minha tribo, e não";
		show "para ajudar a estranhos. Se você";
		show "realmente precisa da minha ajuda, vai pedir";
		show "a permissão do chefe";
		next;
		show "[Puchuchartan]";
		show "Além disso, eu não aprovo estranhos";
		show "falando com a minha tribo mais";
		show "do que o necessário...";
		show "Nós queremos viver uma vida pacífica, então não nos perturbe.,";
		set event_umbala,4;
		close;
	case 4:
	case 5:
		show "[Puchuchartan]";
		show "Eu já te falei para pegar a";
		show "aprovação do chefe. Existe também";
		show "a questão de cuidar de meus próprios negócios.";
		next;
		show "[Puchuchartan]";
		show "Eu sei que você tem passado por";
		show "muitas dificuldades para chegar aqui";
		show ", mas você tem que sair agora.";
		close;
	case 6:
		show "[Puchuchartan]";
		show "Eu ouvi dizer que o chefe";
		show "deu a sua aprovação...";
		show "No entanto eu não gostei, mas eu";
		show "vou manter minha promessa. Mas eu verei";
		show "se você realmente precisa de ajuda ou não.";
		next;
		show "[Puchuchartan]";
		show "Meu poder permiti criar pedra com";
		show "poderes divinos e dividir ";
		show "pedras encantadas em pedras brutas.";
		show "Então eu poderei te ajudar desta forma.";

		next;
		show "[Puchuchartan]";
		show "Então venha e fale comigo quando";
		show "você pensar em que eu posso te ajudar.";
		set event_umbala,7;
		close;
	case 7:
		show "[Putsuchiritan]";
		show "Eu não sei se o meu talento";
		show "será útil para você, mas eu vou";
		show "ajudá-lo mesmo assim.";
		next;
		show "[Putsuchiritan]";
		show "Eu posso criar essencias elementais from natural objects,";
		show "de objetos naturais, ou desfazer";
		show "pedras elementais e seus componentes.";
		show "O que você gostaria de fazer?";
	}
	next;
	show "[Puchuchartan]";
	show "Rune-Midgardian, que pediu a minha";
	show "ajuda... no entanto eu não estou certo";
	show "se você realmente precisa de";
	show "meu poderes, eu vou tentar";
	show "dar o meu apoio.";
	next;
	if (checkweight(1101,10) == 0) {
		show "[Puchuchartan]";
		show "Espere--!";
		show "algo em sua posse está";
		show "pertubando a paz em minha";
		show "mente. Não deveria ser assim...";
		next;
		show "[Puchuchartan]";
		show "Vá e deixa algumas coisa";
		show "para trás, e apenas traga os itens";
		show "que você realmente precise.";
		next;
		show "[Puchuchartan]";
		show "Se você se recusar a fazê-lo,";
		show "Eu não posso fazer nada por você.";
		show "Esteja pronto e depois";
		show "volte e fale comigo";
		close;
	}
	show "[Puchuchartan]";
	show "Agora, o que você gostaria de fazer?";
	show "Meu poder perminte criar pedras";
	show "comuns em pedras encantadas e divir";
	show "pedras encantadas.";
	next;
	switch (select("Criar pedra encantada:Divida uma pura pedra encantada:Sair.")) {
	case 1:
		show "[Puchuchartan]";
		show "Você gostaria de fazer uma";
		show "pedra encantada? Qual propriedade";
		show "você gostaria que eu criasse?";
		show "Terra, Água, Fogo, Vento...";
		show "...escolha uma.";
		next;
		switch (select("Terra:Água:Fogo:Terra")) {
		case 1:
			set .@consume,947; //Horn
			set .@amount,15;
			set .@success,993; //Yellow_Live
			break;
		case 2:
			set .@consume,946; //Snail's_Shell
			set .@amount,20;
			set .@success,991; //Crystal_Blue
			break;
		case 3:
			set .@consume,904; //Scorpion's_Tail
			set .@amount,20;
			set .@success,990; //Boody_Red
			break;
		case 4:
			set .@consume,1013; //Colorful_Shell
			set .@amount,25;
			set .@success,992; //Wind_Of_Verdure
			break;
		}
		if (countitem(.@consume) >= .@amount) {
			show "[Puchuchartan]";
			show "Eu vou tentar completar o";
			show "poder secreto dos objetos naturais";
			show "em ordem para criar as pedras";
			show "encantadas. Escolha um número de";
			show "'1' à '9.' Se você quizer cancelar";
			show "esse pedido, digite '0.'";
			next;
			while(1) {
				digit .@input,0,10;
				if (.@input == 0) {
					show "[Puchuchartan]";
					show "Entendo. É você quem manda.";
					show "Volte quando precisar..";
					close;
				}
				else if (.@input > 9) {
					show "[Puchuchartan]";
					show "Lembre-se que o número tem";
					show "que ser de 1 à 9.";
					next;
				}
				else {
					break;
				}
			}
			show "[Puchuchartan]";
			switch(.@success) {
			case 990:
				show "Estou pondo essas Caudas de Escorpião dentro da"; break; //No original estava somente tails.
			case 991:
				show "Estou pondo esses Casco Arco-Íris dentro da"; break; //No original estava somente shells.
			case 992:
				show "Estou pondo esses Casco Arco-Íris dentro da"; break; //No original estava somente shells.
			case 993:
				show "Estou pondo esses Chifres dentro da"; break; //No original estava somente horns.
			}
			show "panela, e conjurando o";
			show "sagrado encantamento. Lembre-se";
			show "do número que você colocou.";
			next;
			show "[Puchuchartan]";
			show "Amba Omba Zatumba! Umba! Ti!";
			show "Umputaun Eulukaba! Umba! Ha!";
			show "Julu Humba Rulala! Umba! La!";
			show "Datuha Ombabalaka! Umba! Si!";
			show "Sunutaba Abulumba! Umba! Si!";
			next;
			if (rand(1,10) == 1) {
				show "[Puchuchartan]";
				show "Eu acho que meu poder não é suficiente.";
				show "O Poder natural que eu fiz com minha";
				show "magia perdeu o foco e se espalhou...";
				delitem .@consume,.@amount;
				additem 910,1; //Garleta
				next;
				show "[Puchuchartan]";
				show "Parece que os espíritos da natureza";
				show "não estão em harmonia nesse momento.";
				show "No entanto, se você voltar mais tarde,";
				show "eu irei tentar te ajudar. Claro que,";
				show "essa tentativa vai ter a mesma";
				show "possibilidade de falhar.";
				close;
			}
			else {
				show "[Puchuchartan]";
				show "Aqui está a pedra encantada que";
				show "você queria ter. Eu a criei";
				show "com muito esforço, então faça um";
				show "bom uso dela.";
				delitem .@consume,.@amount;
				additem .@success,1;
				close;
			}
		}
		else {
			show "[Puchuchartan]";
			switch (.@success) {
			case 990:
				show "Propriedade de fogo...";
				show "Eu vou precisar de";
				show "objetos naturais que são";
				show "relacionados ao espírito do fogo.";
				next;
				show "[Puchuchartan]";
				show "Escorpiões que resistem ao";
				show "ardente calor do deserto";
				show "são cheios de energia de fogo.";
				show "Eu preciso de ^3377FF20 Caudas de Escorpião^000000.";
				break;
			case 991:
				show "Propriedade de água...";
				show "Eu vou precisar de";
				show "objetos naturais que são";
				show "relacionados ao espírito de água.";
				next;
				show "[Puchuchartan]";
				show "Ambernites ... o espírito da";
				show "água está contida na sua";
				show "concha protetora...";
				show "Eu preciso de ^3377FF20 Casco de Caramujo.^000000";
				break;
			case 992:
				show "Propriedade de vento...";
				show "Eu vou precisar de";
				show "objetos naturais que são";
				show "relacionados ao espírito do vento.";
				next;
				show "[Puchuchartan]";
				show "Percevejos ... o besouro";
				show "que voa até o céu";
				show "tem o espírito do vento.";
				show "Eu preciso de ^3377FF25 Casco Arco-Íris^000000.";
				break;
			case 993:
				show "Propriedade de terra...";
				show "Eu vou precisar de";
				show "objetos naturais que são";
				show "relacionados ao espírito da terra.";
				next;
				show "[Puchuchartan]";
				show "Besouros-chifre... que vivem na floresta,";
				show "são preenchidos com os espíritos da";
				show "terra e madeira...";
				show "Eu preciso de ^3377FF15 Chifre^000000.";
				break;
			}
			show "[Puchuchartan]";
			show "É tudo que eu preciso...";
			show "Volte quando estiver";
			show "pronto. Estarei aqui.";
			close;
		}
	case 2:
		show "[Puchuchartan]";
		show "Você gostaria de dividir a pedra";
		show "encantadas em partes?";
		show "Qual propriedade você gostaria";
		show "de dividir? Terra, Água, Fogo, Vento... ";
		show "Escolha um.";

		next;
		switch(select("Terra:Água:Fogo:Vento")) {
		case 1:
			set .@divide,997; //Great_Nature
			break;
		case 2:
			set .@divide,995; //Mistic_Frozen
			break;
		case 3:
			set .@divide,994; //Flame_Heart
			break;
		case 4:
			set .@divide,996; //Rough_Wind
			break;
		}
		show "[Puchuchartan]";
		show "Por favor, coloque o";
		show "número de pedras encantadas";
		show "que você gostaria de dividir.";
		show "Eu só posso dividir até 10";
		show "de cada vez.";
		next;
		input .@input,0,11;
		if ((.@input > 0) && (.@input < 11)) {
			if (countitem(.@divide) >= .@input) {
				if (checkweight(908,(.@input * 30)) == 0) {
					show "[Puchuchartan]";
					show "Você está carregando";
					show "muitos itens nesse momento.";
					show "Ponha algumas coisas no";
					show "armazém da Kafra, e depois";
					show "volte e fale comigo, ok?";
					close;
				}
				show "[Puchuchartan]";
				show "Eu vou tentar tentar reverter essas";
				show "pedras encantadas para a sua";
				show "forma original. Digite um número de";
				show "1 à 9, ou introduza 0 se";
				show "você decidir cancelar.";
				next;
				while(1) {
					digit .@input2,0,10;
					if (.@input2 == 0) {
						show "[Puchuchartan]";
						show "Você quer cancelar?";
						show "Bem, se você mudar de idéia,";
						show "fique à vontade para";
						show "pedir minha ajuda a";
						show "qualquer momento.";
						close;
					}
					else if (.@input2 > 9) {
						show "[Puchuchartan]";
						show "Hm? Você tem que colocar";
						show "um número de 1 à 9.";
						next;
					}
					else {
						break;
					}
				}
				show "[Puchuchartan]";
				show "Agora eu vou introduzir";
				show "as palavras sagradas. Lembre-se";
				show "do número que você colocou.";
				next;
				show "[Puchuchartan]";
				show "Umba Umba Kalapum! Umba! Ta!";
				show "Lukura Ukulele Um! Umba! Ka!";
				show "Abulaka Tabulakan! Umba! La!";
				show "Ombaludu Zan Kunu! Umba! Ku!";
				show "Kum Tum Lakulakun! Umba! Ha!";
				next;
				show "[Puchuchartan]";
				show "Aqui está a pedra que";
				show "você queria ter. Eu a criei";
				show "com muito esforço, então faça um";
				show "bom uso dela.";
				while(1) {
					if (.@sha_man == .@input) {
						break;
					}
					else {
						set .@shaman_max,.@shaman_max + rand(6,10);
						set .@sha_man,.@sha_man + 1;
					}
				}
				switch(.@divide) {
				case 994:
					getitem 990,.@shaman_max; //Boody_Red
					break;
				case 995:
					getitem 991,.@shaman_max; //Crystal_Blue
					break;
				case 996:
					getitem 992,.@shaman_max; //Wind_Of_Verdure
					break;
				case 997:
					getitem 993,.@shaman_max; //Yellow_Live
				}
				delitem .@divide,.@input;
				close;
			}
			else {
				show "[Puchuchartan]";
				show "Entçao, você que deseja ter uma pedra";
				switch(.@divide) {
				case 994:
					show "bruta de Fogo? Então eu preciso que";
					show "você me traga "+input_want+" pedra de fogo pura.";
					break;
				case 995:
					show "bruta de Água? Então eu preciso que";
					show "você me traga "+input_want+" pedra de água pura.";
					break;
				case 996:
					show "bruta de Vento? Então eu preciso que";
					show "você me traga "+input_want+" pedra de vento pura.";
					break;
				case 997:
					show "bruta de Terra? Então eu preciso que";
					show "você me traga "+input_want+" pedra de terra pura.";
					break;
				}
				show "^3377FF"+.@input+" "+getitemname(.@divide)+"^000000.";
				next;
				show "[Puchuchartan]";
				show "É tudo que eu preciso...";
				show "Volte quando estiver pronto.";
				show "Estarei sempre aqui.";
				close;
			}
		}
		else {
			show "[Puchuchartan]";
			show "Hm? Você precisa colocar";
			show "um número de 1 à 10.";
			close;
		}
	case 3:
		show "[Puchuchartan]";
		show "Entendo. Que seja.";
		show "Volte quando precisar.";
		close;
	}
}

umbala,221,193,1	script	#Skulldoor	111,{
	if (event_umbala >= 7) {
		warp "um_in",32,71;
		end;
	}
	else {
		show "^3355FFO crânio humano preocupante";
		show "trava o lado da porta. A porta está";
		show "trancado com aperto, por isso você não pode entrar.";
		show "Você olha pela fechadura e";
		show "pode ver alguém que se desloca no interior da sala.^000000";
		next;
		if (select("Examinar o crânio.:Sair.") == 1) {
			show "^ 3355FFVocê ve que o olho";
			show "do crânio estão vazios";
			show "Engraçado...";
			show "Parece que pedras";
			show "poderia caber perfeitamente";
			show "dentro deles. ^000000";
			next;
			show "^3355FFVocê vê o olho esquerdo do";
			show "crânio. O que você quer fazer?^000000";
			next;
			switch(select("Deixar como está.:Inserir pedra azul.:Inserir pedra amarela.:Inserir pedra vermelha.")) {
			case 1:
				show "^3355FFVocê deixou o crânio quieto.^000000";
				next;
				break;
			case 2:
				set .@insert,717;
				break;
			case 3:
				set .@insert,715;
				break;
			case 4:
				set .@insert,716;
				break;
			}
			if (.@insert) {
				if (countitem(.@insert) > 0) {
					show "^3355FFVocê inserio a "+getitemname(.@insert)+"";
					show "no olho do crânio.^000000";
					next;
					show "^3355FFA pedra rolou até a";
					show "boca do crânio.^000000";
					set .@skulldoor,.@skulldoor+1;
					switch (.@insert) {
					case 715: set .@skull,2; break;
					case 716: set .@skull,3; break;
					case 717: set .@skull,1; break;
					}
					delitem .@insert,1;
					getitem .@insert,1;
					next;
				}
				else {
					show "^3355FFVocê esqueceu de carregar a "+getitemname(.@insert)+"";
					show "com você. Então você não pôde fazer";
					show "o que você queria.^000000";
					next;
				}
			}
			show "^3355FFVocê vê o olho direito do";
			show "crânio. O que você quer fazer?^000000";
			next;
			switch(select("Deixar como está.:Inserir pedra azul.:Inserir pedra amarela.:Inserir pedra vermelha.")) {
			case 1:
				show "^3355FFVocê deixou o crânio quieto.^000000";
				next;
				break;
			case 2:
				set .@insert2,717;
				break;
			case 3:
				set .@insert2,715;
				break;
			case 4:
				set .@insert2,716;
				break;
			}
			if (.@insert2) {
				if (countitem(.@insert2) > 0) {
					show "^3355FFVocê inseriu a "+getitemname(.@insert2)+"";
					show "dentro do olho do crânio.^000000";
					next;
					show "^3355FFA pedra rolou até a";
					show "boca do crânio.^000000";
					if (.@insert2 == .@insert) {
						set .@skulldoor,.@skulldoor+1;
					}
					else {
						set .@skulldoor,.@skulldoor+2;
					}
					delitem .@insert2,1;
					getitem .@insert2,1;
					next;
				}
				else {
					show "^3355FFVocê esqueceu de carregar a "+getitemname(.@insert)+"";
					show "com você. Então você não pôde fazer";
					show "o que você queria.^000000";
					next;
				}
			}
			show "^3355FF..............................^000000";
			next;
			show "^3355FF..............................";
			show "..............................^000000";
			next;
			show "^3355FF..............................";
			show "..............................";
			show "..............................^000000";
			next;
			switch(.@skulldoor) {
			case 3:
				if (rand(1,4) != 1) {
					set .@skullopen,1;
				}
				break;
			case 2:
				if (rand(1,2) == 2) {
					set .@skullopen,1;
				}
				break;
			case 1:
				if (rand(1,4) == 1) {
					set .@skullopen,1;
				}
				break;
			default:
				break;
			}
			if (.@skullopen == 0) {
				show "^3355FFNada aconteceu.";
				show "Você tem a sensação de que o";
				show "crânio sorri para você. Mas...";
				show "É provavelmente apenas um truque da luz.^000000";
				close;
			}
			else {
				show "^3355FFDerrepente, um som vem";
				show "a partir do olho e da boca do crânio e";
				show "a porta é aberta. Antes de conhecer,";
				show "você caminha no interior, como se fosse guiado";
				show "por uma força invisível...^000000";
				close2;
				warp "um_in",32,71;
				end;
			}
		}
		show "^ 3355FFVocê decidiu passar pela porta.";
		show "Parece que ela é difícil demais de abrir.^000000";
		close;
	}
}


//============================================================
// Umbala Domestic Dispute
//============================================================
um_in,139,48,5	script	Phrenetan	783,{
	if (event_umbala >= 3) {
		show "[Phrenetan]";
		show "Estou farta e cansada do";
		show "meu marido! É como se ele";
		show "flertasse com cada mulher";
		show "da aldeia!";
		next;
		show "[Phrenetan]";
		show "Se eu ver ele flertando";
		show "com outra mulher de novo...";
		show "eu juro...";
		show "Vou lhe mostrar o inferno!!";
		close;
	}
	if (BaseJob == Job_Novice && Upper != 2) {
		show "[Phrenetan]";
		show "Umba~ umbaumbah!";
		show "Umbah woomumum!";
		show "Umbah woomum umbabah!";
		close;
	}
	if (um_wind <= 3 && (MISC_QUEST & 32768) == 0) {
		if (um_wind) set um_wind,1;
		emotion e_an;
		show "[Phrenetan]";
		show "Umbaumbah wooga wooga";
		show "Umbaumbabah babababah!";
		show "Umbaum!";
		next;
		emotion e_oh;
		show "[Phrenetan]";
		show "Umbah umbaumba umbah";
		show "Umbabababah wooga woo!";
		show "Wooga wooga umbabah umbaum!";
		next;
		emotion e_go;
		close;
	}
	else if (um_wind == 6 || MISC_QUEST & 32768) {
		emotion e_an;
		show "[Phrenetan]";
		show "Umbabah! Umbaumbah.....";
		show "Umbaum Umbaum Wooga wooga!";
		show "Wooga umumum woombababap!!!!!";
		next;
		emotion e_swt;
		close;
	}
	end;

OnEmoteOmg:
	emotion e_omg;
	end;

OnEmoteRock:
	emotion e_rock;
	end;
}

um_in,144,45,5	script	Umpokoriohtan	789,{
	if (event_umbala >= 3) {
		if(rand(1,3) == 2) {
			show "[Umpokoriohtan]";
			show "Olá, pessoa.";
			show "Não importa a esposa...";
			show "por mais que eu ame ela,";
			show "eu sei minhas obrigações, ya dig?";
			next;
			show "[Umpokoriohtan]";
			show "Se um homem tem muitas, ele deve";
			show "dividir o seu amor igualmente";
			show "entre todas.";
			next;
			show "[Umpokoriohtan]";
			show "Se as mão de um homem são";
			show "boas para curar, ele tem de usar ";
			show "essas mãos para ajudar a gente viver.";
			next;
			show "[Umpokoriohtan]";
			show "Se um homem tiver o dom de";
			show "cantar, ele tem o dever de";
			show "cantar as músicas que gostamos.";
			next;
			show "[Umpokoriohtan]";
			show "Mas se sweet lovin' é dourada,";
			show "então... eu tenho o toque de Midas.";
			next;
			show "[Umpokoriohtan]";
			show "Ooh...!";
			show "...........";
			show "Minhas costas--!";
			show "Acalme-se, pessoa, seu turno começa em 1 minuto~";
			donpcevent "Phrenetan::OnEmoteRock";
			emotion e_swt;
			close;
		}
		else {
			show "[Umpokoriohtan]";
			show "Cara ... algumas vezes a minha mulher pode";
			show "ser brava e às vezes demasiado grosseira, talvez";
			show "mesmo mágoa. Mas é legal...";
			show "só significa que ela 'tem fogo'.";
			next;
			show "[Umpokoriohtan]";
			show "Mas algum dia, ela irá aprender";
			show "que eu tenho que dividir o";
			show "coração de minhas outras esposas";
			show "que realmente precisam de um toque";
			show "de amor, entende? É a minha obrigação.";
			next;
			show "[Umpokoriohtan]";
			show "Wainatan, Bertztan, Chabimatan...";
			show "Essas mulheres lindas estão esperando";
			show "há um boooom tempo. Não se preucupe,";
			show "sou um grande homem.";
			next;
			show "[Umpokoriohtan]";
			show "OOOH~! Phrenetan!";
			show "Amor, porque ficou brava?";
			show "Não me bata, ya dig??";
			show "Eu não queria te ofender~";
			donpcevent "Phrenetan::OnEmoteOmg";
			emotion e_wah;
			close;
		}
	}
	emotion e_heh;
	show "[Umpokoriohtan]";
	show "Umbaumbah...........";
	show "Umbahwooga woogawoo!";
	show "Umbah umumbabah umbawoo gaga.";
	next;
	emotion e_an;
	close;

OnInit:
	disablenpc "Umpokoriohtan";
	end;
}

um_in,101,73,3	script	Wainatan	783,{
	if (event_umbala >= 3) {
		show "[Wainatan]";
		show "Estou enjoado e cansada deste homem";
		show "que sempre aparece à noite e e transforma minha vida em um inferno...";
		next;
		show "[Wainatan]";
		show "'Operador liso que recebe o";
		show "trabalho feito?' Oh Meu Deus...!";
		show "Eu odeio-o com uma paixão!";
		show "Desejo que Umpokoriohtan";
		show "seja caia morto.";
		close;
	}
	if (um_wind == 1) {
		if (gettime(3) > 18) {
			set um_wind,2;
			emotion e_an;
			show "[Wainatan]";
			show "Umbaumbah umgagaga.";
			show "Umbaumbawoogawoo gababah.";
			show "Umbahumbabah gawoo.";
			next;
			emotion e_oh;
			show "[Wainatan]";
			show "Wooga wooga woogagagah";
			show "Wogagagah woogagagah";
			show "Gawoo gawoo gah.";
			close;
		}
		else {
			show "[Wainatan]";
			show "Umbabah! Umbaumbah wooga";
			show "Woogawooga umbawooga umum.";
			show "Umbabababababababababah.";
			close;
		}
	}
	else {
		show "[Wainatan]";
		show "Umbaumbah umbaumbah umbah";
		show "Wooga wooga woogawooga wooga";
		show "Umumumum umumumum umum.";
		close;
	}
}

um_in,94,123,5	script	Bertztan	783,{
	if (event_umbala >= 3) {
		show "[Bertztan]";
		show "...* Glup * O tarado ";
		show "Umpo-o de novo! Eu disse a ele";
		show "que não gosto dele, mas ele";
		show "simplesmente não escuta!";
		show "Gostaria... gostaria que ele";
		show "simplesmente desaparecesse!";
		close;
	}
	if (um_wind == 2) {
		if (gettime(3) > 18) {
			set um_wind,3;
			emotion e_an;
			show "[Bertztan]";
			show "Umbaumbah umgagaga.";
			show "Umbaumbawoogawoo gababah.";
			show "Umbahumbabah gawoo.";
			next;
			emotion e_oh;
			show "[Bertztan]";
			show "Wooga umbar umbar umbah!";
			show "Umbar woogagaga woo! Woo! Woo!";
			show "Wooga~ wooga~ Woo woo woo umbar.";
			close;
		}
		else {
			show "[Bertztan]";
			show "Umbar woogaumbarumbah um!";
			show "Um~ wooga wooga umbarum.";
			show "Umbah...wooum.";
			close;
		}
	}
	else {
		show "[Bertztan]";
		show "Umbar wooga umbar umbah um!";
		show "Um~ woogawooga umbar um.";
		show "Umbah...wooum.";
		close;
	}
}

umbala,145,217,3	script	Chabimatan	783,{
	if (event_umbala >= 3) {
		show "[Chabimatan]";
		show "...*Sigh* Umpokoriohtan parece";
		show "ser para casar. Eu não tenho";
		show "idéia porque ele ainda flerta";
		show "com outras mulheres. Talvez ele";
		show "não seja maduro, ou seja";
		show "irresponsável...";
		next;
		show "[Chabimatan]";
		show "Bem, independente do que ele seja,";
		show "ele certamente não é";
		show "romântico. Essas";
		show "cantadas dele poderiam"; 
		show "ser melhor, talvez até"; 
		show "algumas mais limpas.";
		close;
	}
	if (um_wind == 3) {
		if (gettime(3) > 18) {
			set um_wind,4;
			emotion e_an;
			show "[Chabimatan]";
			show "Umbabah umbarbar woogawooga um";
			show "Umbabah umbarbar woogawooga umbah";
			show "Umumum! Wooga!";
			next;
			emotion e_oh;
			show "[Chabimatan]";
			show "Umbabah~~~~~~~";
			show "Woogawooga umbar umbar woo!";
			show "Wooga umbar woogawoogagah.";
			enablenpc "Umpokoriohtan";
			enablenpc "#!@#$%";
			close;
		}
		else {
			show "[Chabimatan]";
			show "Umbabah~~~~~~~";
			show "Woogawooga umbar umbar woo";
			show "woo woo! Nook nook~";
			show "Wooga umbar wooga umbar";
			show "wooga woogagah.";
			close;
		}
	}
	else {
		show "[Chabimatan]";
		show "Umbabah~~~~~~~";
		show "Woogawooga umbar umbar";
		show "woo woo woo nook nook.";
		show "Wooga umbar wooga umbar";
		show "wooga woogagah.";
		close;
	}
}

um_in,141,46,0	script	#!@#$%	-1,8,8,{
OnInit:
	disablenpc "#!@#$%";
	end;

OnTouch:
	if (um_wind == 4) {
		set um_wind,5;
		show "^3355FFQuando você entra na casa";
		show "você acaba de testemunhar";
		show "Phrenetan batendo em um cara";
		show "impiedosamente.^000000";
		next;
		emotion e_an;
		show "[Phrenetan]";
		show "Umbaumbaumbaumbah!";
		show "Umbaumbahumbah!!";
		show "Umbaumbahumbah!!!!!!";
		next;
		show "^3355FFVocê foi expulso da casa";
		show "por Phrenetan.^000000";
		next;
		enablenpc "#unpc";
		disablenpc "#!@#$%";
		warp "umbala",94,181;
	}
	end;
}

umbala,94,181,0	script	#unpc	-1,1,1,{
OnInit:
	disablenpc "#unpc";
	end;

OnTouch:
	if (um_wind == 5) {
		show "^3355FFVocê percebe o que acontece";
		show "assim que foi expulso da casa,";
		show "você vê a folha sobre o terreno";
		show "perto de onde você está em pé.^000000";
		next;
		if (select("Pegar.:Não pegar.") == 1) {
			close2;
			set um_wind,0;
			set MISC_QUEST,MISC_QUEST | 32768;
			getitem 610,1; //Leaf_Of_Yggdrasil
			disablenpc "#unpc";
			end;
		}
		set um_wind,0;
		set MISC_QUEST,MISC_QUEST | 32768;
		show "[" + strcharinfo(0) + "]";
		show "Eu não devo pegar o que";
		show "pertence a outra pessoa.";
		next;
		show "[" + strcharinfo(0) + "]";
		show "Sim, eu sou uma pessoa boa.";
		close2;
		disablenpc "#unpc";
	}
	end;
}