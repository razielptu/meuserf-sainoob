//===== Cronus Script ========================================
//= Quest de Classe - Arqueiro
//===== Por: ================================================= 
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: ======================================== 
//= 1.8b
//===== Compatibilidade: ===================================== 
//= Cronus SVN
//===== Descrição: ===========================================
//= Fazer um aprendiz se tornar um arqueiro
//===== Comentários Adicional: =============================== 
//= v1.1 Added instant job change for High Novice [Lupus]
//= v1.3 Added Baby Class support [Lupus]
//= v1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc [Lupus]
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=	Arrow reward is now equal to the type sof trunks brought.
//=	No longer uses function "F_ToHigh"
//= 1.7a Fixed minor dialogue bug at Grade==25 [Lupus]
//= 1.8 Fixed mistake in condition check. [L0ne_W0lf]
//= 1.8a Traduzido. [.zuport]
//= 1.8b Revisado. [Heaven]
//============================================================

payo2,73,169,4	script	Guia de Arqueiro#archer	85,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy)) {
			show "[Guia de Arqueiro]";
			show "Ei, eu te conheço.";
			show "Você já fez esse teste";
			show "antes, não fez?";
			next;
			show "[Guia do Arqueiro]";
			show "Ah, você deve ter ido";
			show "para Valhalla e já renasceu.";
			show "Uau, isso é tão impressionante!";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				show "[Guia do Arqueiro]";
				show "Err...";
				show "Seria melhor que você aprendesse todas Habilidades Básicas primeiro antes de se tornar um Arqueiro.";
				next;
				show "[Guia do Arqueiro]";
				show "Tudo bem, até logo.";
				close;
			}
			show "[Guia do Arqueiro]";
			show "Ok então. Eu não";
			show "preciso dizer mais nada.";
			show "Eu sei que você virou um ótimo Arqueiro...";
			next;
			skill 143,0,0;
			setjob Job_Archer_high;
			skill 147,1,0;
			skill 148,1,0;
			show "[Guia do Arqueiro]";
			show "Ainda não há nenhuma";
			show "recompensa para você desta vez, eu espero que você compreenda. Cuide de si mesmo.";
			close;
		}
		else {
			show "[Guia do Arqueiro]";
			show "Oh...?";
			show "Ei, o que você";
			show "está fazendo aqui...?";
			next;
			show "[Guia do Arqueiro]";
			show "Eu posso dizer que você não é capacitado para ser um Arqueiro. Parece que você pretende fazer";
			show "alguma outra coisa...";
			close;
		}
	}
	show "[Guia do Arqueiro]";
	show "Bom dia. Como posso ajudá-lo?";
	next;
	switch(select("Eu quero ser um Arqueiro.:Eu preciso dos requerimentos, por favor.:Nada, obrigado.")) {
	case 1:
		if (BaseJob == Job_Archer) {
			show "[Guia do Arqueiro]";
			show "Você já é um Arqueiro...";
			close;
		}
		else if (BaseJob != Job_Archer && BaseJob != Job_Novice) {
			show "[Guia do Arqueiro]";
			show "Hmm...";
			show "Você não se parece muito com um Aprendiz...";
			next;
			show "[Guia do Arqueiro]";
			show "De qualquer maneira, seja o que for que você seja, você não pode escolher uma classe como Arqueiro porque você já tem uma classe.";
			close;
		}
		if (job_archer_q == 0) {
			show "[Guia do Arqueiro]";
			show "Você quer ser um Arqueiro?";
			show "Caso a resposta seja sim, você precisa preencher esse formulário de cadastro.";
			next;
			if (select("Aplicar.:Cancelar") == 1) {
				set job_archer_q,1;
				show "[Guia do Arqueiro]";
				show "Ok, cadastre-se aqui. Tudo bem, hmm, vou te promover assim que você preencher todos requerimentos.";
				next;
				show "[Guia do Arqueiro]";
				show "Se você acha que já preencheu todos, podemos checar isso agora.";
				show "Está pronto?";
				next;
				if (select("Sim, eu estou.:Não, ainda não.") == 1) {
					show "[Guia do Arqueiro]";
					show "Tubo bem, deixe-me ver.";
					next;
				}
				else {
					show "[Guia do Arqueiro]";
					show "Eu entendo. Seja meu visitante se você deseja olhar os requerimentos.";
					close;
				}
			}
			show "[Guia do Arqueiro]";
			show "Tudo bem.";
			show "Vejo você na próxima vez.";
			close;
		}
		show "[Guia do Arqueiro]";
		show "Você é..." + strcharinfo(0) + "?";
		next;
		if (getskilllv("NV_BASIC") < 9) {
			show "[Guia do Arqueiro]";
			show "Bem, você não está no nível de classe correto. Por favor verifique os requerimentos novamente.";
			next;
			show "[Guia do Arqueiro]";
			show "Seu nível de classe deve ser 10, e não se esqueça que você deve aprender todas Habilidades Básicas. Após ter feito isso, volte aqui.";
			close;
		}
		if (job_archer_q == 1) {
			set .@archer_item1,countitem(1066) * 5;
			set .@archer_item2,countitem(1067) * 3;
			set .@archer_item3,countitem(1068) * 2;
			set .@archer_item4,countitem(1019);
			set .@total_archer,.@archer_item1 + .@archer_item2 + .@archer_item3 + .@archer_item4;
			set .@total_archer2,(((.@archer_item2 + .@archer_item3) * 2) + .@archer_item4);
			show "[Guia do Arqueiro]";
			show "Excelente!";
			show "Então agora,";
			show "vamos ver...";
			next;
			show "[Guia do Arqueiro]";
			show "Vou avaliar o valor dos diferentes tipos de Troncos, necessários para produzir um Arco, que você trouxe.";
			next;
			if (countitem(1066) == 0 && countitem(1067) == 0 && countitem(1068) == 0 && countitem(1019) == 0) {
				show "[Guia do Arqueiro]";
				show "Hmm...";
				show "Infelizmente você não trouxe nenhum dos itens exigidos. Não há nada para ser avaliado.";
				close;
			}
			show "[Guia do Arqueiro]";
			if (countitem(1066) != 0) show " Série S : " + countitem(1066) + " ea, Pontos : " + .@archer_item1 + " . ";
			if (countitem(1067) != 0) show " Série A : " + countitem(1067) + " ea, Pontos : " + .@archer_item2 + " . ";
			if (countitem(1068) != 0) show " Série B : " + countitem(1068) + " ea, Pontos : " + .@archer_item3 + " . ";
			if (countitem(1019) != 0) show " Série C : " + countitem(1019) + " ea, Pontos : " + .@archer_item4 + " . ";
			if (.@total_archer < 25) {
				show "Total de Pontos: ^FF0000" + .@total_archer + "^000000 / 40";
				next;
				show "[Guia do Arqueiro]";
				show "Menos que 25!? Você precisa ter ao menos 25 pontos! Vamos, esforça-te mais!";
				close;
			}
			else {
				show "Total de Pontos: ^0000FF" + .@total_archer + "^000000 / 40";
				next;
				show "[Guia do Arqueiro]";
				if (.@total_archer > 40) {
					show "Uau! Mais que 40!";
					show "Excelente! Parabéns!";
				}
				else if (.@total_archer > 30) {
					show "Mais que 30! Bom trabalho!";
					show "Parabéns!";
				}
				else {
					show "*Suspiro* Bem, você apenas passou mal... De qualquer maneira, bem feito.";
				}
			}
			next;
			show "[Guia do Arqueiro]";
			show "Vou transferir esses Troncos ao nosso Departamento de Produção de Arcos. Agora que você já cumpriu os requisitos, deixe-me te promover agora mesmo!";
			if (countitem(1066) != 0) delitem 1066,countitem(1066); //Tree_Of_Archer_1
			if (countitem(1067) != 0) delitem 1067,countitem(1067); //Tree_Of_Archer_2
			if (countitem(1068) != 0) delitem 1068,countitem(1068); //Tree_Of_Archer_3
			if (countitem(1019) != 0) delitem 1019,countitem(1019); //Wooden_Block
		}
		next;
		callfunc "Job_Change",Job_Archer;
		callfunc "F_ClearJobVar";
		show "[Guia do Arqueiro]";
		show "Parabéns!";
		show "Agora você é um Arqueiro!";
		next;
		show "[Guia do Arqueiro]";
		show "É claro, nós esperamos que você vai ajudar a contribuir para o futuro do Clã de Arqueiros com seus esforços.";
		next;
		show "[Guia do Arqueiro]";
		show "Ah, seu arco chegou do Departamento de Produção de Arcos. Aqui, pegue isso! São seus~";
		additem 1702,1; //Bow_
		additem 1750,.@total_archer2; //Arrow
		next;
		show "[Guia do Arqueiro]";
		show "Now, off you go. Hunt with pride, knowing you were trained by one of the best!";
		close;
	case 2:
		show "[Guia do Arqueiro]";
		show "Eu explicarei os requerimentos para se tornar um Arqueiro.";
		if (BaseJob != Job_Novice) {
			if (BaseJob == Job_Archer) {
				next;
				show "[Guia do Arqueiro]";
				show "Mas...";
				show "Você já é um Arqueiro. Você já deveria saber disso...";
			}
			else {
				next;
				show "[Guia do Arqueiro]";
				show "Espere um segundo. Você já escolheu uma classe diferente. Você não precisa saber disso~";
			}
			show "Então...Sim...não há razão para te dizer os requerimentos...";
		}
		next;
		show "[Guia do Arqueiro]";
		show "Primeiro de tudo, você deve ter Nível 9 de Classe como Aprendiz, e conheçer todas Habilidades Básicas.";
		next;
		show "[Guia do Arqueiro]";
		show "Um Arqueiro precisa ter concentração e reflexos extremamente altos, então nós não aceitamos aqueles que tem pouca paciência.";
		next;
		show "[Guia do Arqueiro]";
		show "Você também tem de recolher ^FF0000Troncos^000000. Existem 4 tipos de Troncos, de qualidades diferentes. Você receberá pontuações diferentes nos seus troncos, dependendo da qualidade.";
		next;
		show "[Guia do Arqueiro]";
		show "Para se tornar um Arqueiro, você precisa obter no mínimo ^0000FF25^000000 pontos de 40. Você pode obter Troncos de 'Salgueiro', a árvore. Tenha cuidado, afinal, eles podem ser monstros fortes.";
		close;
	case 3:
		close;
	}
}