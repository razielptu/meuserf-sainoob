//===== Cronus Script ========================================
//= Quest de Classe - Gatuno
//===== Por: ================================================= 
//= eAthena dev team
//===== Versão Atual: ======================================== 
//= 1.7b
//===== Compatibilidade: ===================================== 
//= Cronus SVN
//===== Descrição: ===========================================
//= Fazer um aprendiz se tornar um gatuno
//===== Comentários Adicional: =============================== 
//= v1.1 Added instant job change for High Novice [Lupus]
//= 1.3 Added Baby Class support [Lupus]
//= v1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= Fixed possible EXP abuse [Lupus]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc [Lupus] 1.6a typos
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=	No longer uses function "F_ToHigh"
//= 1.7a Traduzido. [.zuport]
//= 1.7b Revisado. [Heaven]
//============================================================

moc_prydb1,39,129,2	script	Guia de Gatuno	69,{
	if (Upper == 1) {
		if (ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) {
			if (Class == Job_Novice_High) {
				show "[Guia de Gatuno]";
				show "Huh? Eu te conheço? É assustador que você parece tão familiar. Você não tem um irmão gêmeo, não é?";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					show "[Guia de Gatuno]";
					show "O quê, você quer ser um Gatuno? Sinto muito, mas parece que você precisa de mais treinamento.";
					next;
					show "[Guia de Gatuno]";
					show "Não tenha pressa e aprenda todas Habilidades Básicas, você vai? Ok então, vejo você mais tarde~!";
					close;
				}
				show "[Guia de Gatuno]";
				show "Bem, eu tive o pressentimento que você teve uma vida de lutas, então estou te promovendo para um Gatuno neste minuto. É melhor eu dar o que você quer...";
				next;
				skill 143,0,0;
				setjob Job_Thief_High;
				skill 149,1,0;
				skill 150,1,0;
				skill 151,1,0;
				skill 152,1,0;
				show "[Guia de Gatuno]";
				show "Desde que você se tornar um Gatuno, viver como um Gatuno. Agora, vá por isso! Próximo~";
				close;
			}
			else {
				show "[Guia de Gatuno]";
				if (sex)
					show "Ei, cara.";
				else
					show "Ei, bebê~";
				close;
			}
		}
		else {
			show "[Guia de Gatuno]";
			if (sex)
				show "Ei, cara.";
			else
				show "Ei, bebê.";
			show "...Ei! Você parece muito cuti-cuti para querer ser um Gatuno! Agora suma, estou ocupado. Próximo!";
			close;
		}
	}
	if (BaseJob == Job_Thief) {
		show "[Guia de Gatuno]";
		show "Se você tem um problema, sinta-se a vontade para falar comigo quando quiser, ok?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		show "[Guia de Gatuno]";
		show "Que diabos...?";
		switch(Class) {
		case 1:
			show "Huh.";
			show "Agora, isso é";
			show "uma grande espada.";
			next;
			show "[Guia de Gatuno]";
			show "Então...";
			show "Tentando ficar a";
			show "par de alguma coisa";
			show "...Amigo?";
			break;
		case 2:
			show "O que um Mago está fazendo aqui? Você não deveria estar fazendo truques com cartas em outro local? Oh bem, esse é um país livre...";
			next;
			show "[Guia de Gatuno]";
			show "Oh espere,";
			show "não é não...";
			show "Caia fora daqui!";
			break;
		case 3:
			show "Cara, vocês Arqueiros";
			show "não deveriam estar";
			show "caçando na floresta";
			show "ou algo do tipo?";
			break;
		case 4:
			show "Você sabe que roubamos para viver, certo? O que você está fazendo nesse tipo de lugar, Noviço?";
			break;
		case 5:
			show "Você é um Mercador,";
			show "certo? Por que você está";
			show "andando em um covil";
			show "de Gatunos?!";
			next;
			show "[Guia de Gatuno]";
			show "Parece que você está nos tentando";
			show "para roubarmos você!";
			show "Vamos, corra e";
			show "caia fora daqui~";
			break;
		case 8:
			show "Oh meu Deus...";
			show "Eu estou morrendo?";
			next;
			show "[Guia de Gatuno]";
			show "Por que mais um Sacerdote viria aqui? Eu acho que é melhor eu começar a confessar todos meus pecados.";
			close;
		case 12:
			show "Você não era um de nós?! Cara, você mudou. Você parece realmente perigoso agora...";
			close;
		case 17:
			show "Cara, você ficou realmente frio de repente! Você deve ter algumas habilidades que eu sonho ter um dia!";
			close;
		}
		next;
		show "[Guia de Gatuno]";
		show "*Suspiro* Olha, não há realmente nenhuma necessidade para você estar neste tipo de lugar. Você deveria seguir seu caminho...";
		close;
	}
	if (job_thief_q == 3 && countitem(1069) > 0 || countitem(1070) > 0) {
		show "[Guia de Gatuno]";
		show "Hmmm?";
		show "Você ajuntou os Cogumelos para";
		show "o Teste de Gatuno, certo?";
		next;
		show "[Guia de Gatuno]";
		show "Aqui, fale com o outro cara perto de mim. Ele que verifica os Cogumelos.";
		close;
	}
	else if (job_thief_q == 3) {
		show "[Guia de Gatuno]";
		show "Então como foi a";
		show "plantação de Cogumelos?";
		show "Foi divertido?";
		next;
		if (select("Sim, bem legal.:Aquilo foi horrível.") == 1) {
			show "[Guia de Gatuno]";
			show "Heh heh! Boa atitude. Em nosso trabalho, você precisa curtir suas mãos ficando sujas, de uma forma ou outra.";
			close;
		}
		show "[Guia de Gatuno]";
		show "Sim? Eu estive lá também, então eu posso ver o porque daquele lugar não ser o copo de chá de ninguém. Ainda, ser um Gatuno não é uma vida de glamour e moda notura.";
		close;
	}
	if (job_thief_q == 2) {
		show "[Guia de Gatuno]";
		show "Ei, o que você está fazendo aqui? Você não deveria estar pegando Cogumelos? Ou você precisa que eu explique tudo denovo?";
		next;
		if  (select("Sim.:Não, tudo bem.") == 1) {
			show "[Guia de Gatuno]";
			show "*Suspiro* Bem, há sempre um no bando. Certo, escute cuidadosamente.";
			next;
			show "[Guia de Gatuno]";
			show "Vamos lá, para seu teste, você precisa roubar Cogumelos de uma plantação. Não se preocupe, o cara que é dono da fazenda merece ser roubado.";
			next;
			show "[Guia de Gatuno]";
			show "Enfim, você tem que juntar dois tipos de Cogumelos: ^0000FFCogumelo Venenoso^000000 e ^0000FFCogumelo Borrachento^000000.";
			next;
			show "[Guia de Gatuno]";
			show "Tenha cuidado, pois existem monstros na plantação que estão lá para proteger os Cogumelos. Então isso não será uma caminhada no parque.";
			next;
			show "[Guia de Gatuno]";
			show "Quando você voltar aqui depois de juntar os Cogumelos, você será pontuado de acordo com os Cogumelos que coletou.";
			next;
			show "[Guia de Gatuno]";
			show "Cada Cogumelo Venenoso vale 3 pontos, e você recebe 1 ponto por cada Cogumelo Borrachento. Você precisa de 25 pontos para passar no teste.";
			next;
			show "[Guia de Gatuno]";
			show "Vá lá fora e continue indo até o campo oriental das Pirâmides. E você verá um de nossos camaradas entre duas colunas.";
			next;
			show "[Guia de Gatuno]";
			show "Fale com aquele cara, e ele te levará para o plantação pela porta dos fundos.";
			next;
			show "[Guia de Gatuno]";
			show "Naquele campo, eu acho que as coordenadas são '^FF0000141, 125^000000.' Apenas digite ^3355FF/where^000000 no canto direito da caixa de chat para checar suas coordenadas atuais.";
			close;
		}
		show "[Guia de Gatuno]";
		show "Huh. Por um segundo ali, Eu pensei que você tinha algo realmente importante para me dizer.";
		close;
	}
	show "[Guia de Gatuno]";
	if (job_thief_q == 0) {
		show "O que o trouxe aqui";
		show "nesse buraco de rato?";
	}
	else {
		show "Ah...";
		show "Você voltou.";
		show "Você tem certeza que está";
		show "pronto para tentar novamente?";
	}
	next;
	if (select("Ei, eu voltei aqui para virar um Gatuno!:Não, estou apenas dando uma olhada.") == 1) {
		if (job_thief_q == 0) {
			show "[Guia de Gatuno]";
			show "Heh, eu gosto de sua confiança. Mesmo assim, você sabe que se tornar um Gatuno não é tudo o que é rachado até ser.";
			next;
			show "[Guia de Gatuno]";
			show "Ainda...";
			show "Você realmente";
			show "quer ser";
			show "um Gatuno?";
			next;
			switch(select("Sim.:Não, apenas gastando seu tempo.:Por que você virou um Gatuno?")) {
			case 1:
				show "[Guia de Gatuno]";
				show "Realmente...";
				break;
			case 2:
				show "[Guia de Gatuno]";
				show "É...";
				show "Eu vi.";
				break;
			case 3:
				show "[Guia de Gatuno]";
				show "Eu...?";
				show "Eu não tive escolha no momento. Era roubar ou passar fome. Mas eu não preciso te contar a história da minha vida.";
				break;
			}
			next;
			show "[Guia de Gatuno]";
			show "Então você quer";
			show "se inscrever para ser";
			show "um Gatuno ou não?";
			next;
			switch(select("Sim, eu quero.:Eu estou com muito medo de virar um Gatuno!")) {
			case 1:
				show "[Guia de Gatuno]";
				show "Correto, diga-me";
				show "seu nome.";
				next;
				show "[Guia de Gatuno]";
				show "" + strcharinfo(0) + "?";
				show "Que tipo de nome é " + strcharinfo(0) + "? De qualquer modo, espere um segundo.";
				set job_thief_q,1;
				next;
				show "[Guia de Gatuno]";
				show "Tudo certo, seu registro foi processado. Ok, você pode começar o teste se estiver pronto.";
				next;
				switch(select("Sim, estou pronto.:Não, ainda não estou pronto.")) {
				case 1:
					break;
				case 2:
					show "[Guia de Gatuno]";
					show "Não está pronto?";
					show "Como você pode";
					show "não estar pronto?!";
					close;
				}
				break;
			case 2:
				show "[Guia de Gatuno]";
				show "Está com muito medo?!?";
				show "Hahahahahahah!";
				show "Oh, por favor...!";
				show "Isso é hilário!";
				close;
			}
		}
		else {
			show "[Guia de Gatuno]";
			show "Ok...";
			show "Espere";
			show "um segundo.";
			next;
		}
		show "[Guia de Gatuno]";
		show "Seu nome é...";
		show "" + strcharinfo(0) + "? Hmm, aonde está isso? Ah, aqui está. Deixe-me ver...";
		next;
		show "[Guia de Gatuno]";
		if (getskilllv("NV_BASIC") < 9) {
			show "Não é bonitinho? Eu posso ver que você é ambicioso, mas você precisa aprender as Habilidades Básicas antes de se tornar um Gatuno.";
			close;
		}
		show "Tudo certo. Eu olhei sua ficha criminal, e você parece ter uma história muito interessante. Você deve ter o que é preciso para ser um Gatuno.";
		next;
		show "[Guia de Gatuno]";
		show "Porque eu sinto isso, eu agora decreto que você passou nessa entrevista. Bom trabalho!";
		set job_thief_q,2;
		next;
		show "[Guia de Gatuno]";
		show "Agora, suas habilidades atuais serão testadas. Você conheçe alguam coisa sobre o teste?";
		next;
		switch(select("Sim, eu conheço.:Desculpe, eu não conheço.")) {
		case 1:
			show "[Guia de Gatuno]";
			show "É mesmo? Bem, isso tornará as coisas mais fáceis.";
			break;
		case 2:
			show "[Guia de Gatuno]";
			show "Vamos lá, deixe-me te informar então. Escute cautelosamente. Esse teste decide se você é digno de virar um Gatuno.";
			next;
			show "[Guia de Gatuno]";
			show "Você irá secretamente até a plantação do Shibu. Ele é o pior Mercador, em termos de caráter, em Morroc.";
			next;
			show "[Guia de Gatuno]";
			show "Vamos lá, para seu teste, você precisa roubar Cogumelos da plantação dele. Não se preocupe, ele merece ser roubado.";
			next;
			show "[Guia de Gatuno]";
			show "Enfim, você tem que juntar dois tipos de Cogumelos: ^0000FFCogumelo Venenoso^000000 e ^0000FFCogumelo Borrachento^000000.";
			next;
			show "[Guia de Gatuno]";
			show "Tenha cuidado, pois existem monstros na plantação que estão lá para proteger os Cogumelos. Então isso não será uma caminhada no parque.";
			next;
			show "[Guia de Gatuno]";
			show "Quando você voltar aqui depois de juntar os Cogumelos, você será pontuado de acordo com os Cogumelos que coletou.";
			next;
			show "[Guia de Gatuno]";
			show "Cada Cogumelo Venenoso vale 3 pontos, e você recebe 1 ponto por cada Cogumelo Borrachento. Você precisa de 25 pontos para passar no teste.";
			next;
			show "[Guia de Gatuno]";
			show "Vá lá fora e continue indo até o campo oriental das Pirâmides. E você verá um de nossos camaradas entre duas colunas.";
			next;
			show "[Guia de Gatuno]";
			show "Fale com aquele cara, e ele te levará para o plantação pela porta dos fundos.";
			next;
			show "[Guia de Gatuno]";
			show "Naquele campo, eu acho que as coordenadas são '^FF0000141, 125^000000.' Apenas digite ^3355FF/where^000000 no canto direito da caixa de chat para checar suas coordenadas atuais.";
		}
		next;
		show "[Guia de Gatuno]";
		show "Não se esqueça de fazer planos e se preparar antes de entrar na plantação. Ande o mais rápido que conseguir e tente não ser morto, ok?";
		close;
	}
	close;
}

moc_prydb1,42,133,2	script	Camarada	118,{
	if (BaseJob == Job_Thief) {

		show "[Brad]";
		show "Nós não temos nenhum evento especial ainda. Venha outra hora quando tiver notícias, ok?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		show "[Camarada]";
		show "Um...";
		show "Você não se parece";
		show "com um Gatuno.";
		next;
		show "[Camarada]";
		show "Que diabos você";
		show "está fazendo aqui afinal?";
		close;
	}
	if (job_thief_q == 0 || job_thief_q == 1) {
		show "[Camarada]";
		show "Qual é o problema? Se você quer se tornar um Gatuno, fale com a garota ao meu lado.";
		close;
	}
	else if (job_thief_q == 2) {
		show "[Camarada]";
		show "Você não passou na entrevista?";
		show "Então o que está esperando?";
		close;
	}
	else if (job_thief_q == 3) {
		show "[Camarada]";
		show "Ah, o mentor me falou de você. Então, deixe-me verificar seus Cogumelos...";
		if (countitem(1069) == 0 && countitem(1070) == 0) {
			next;
			show "[Camarada]";
			show "Que diabos...";
			show "Você não tem nenhum Cogumelo! Volte lá e pegue-os. Caso contrário, você não passará no teste e não se tornará um Gatuno!";
			close;
		}
		next;
		set .@thief_item1,countitem(1069) * 3;
		set .@thief_item2,countitem(1070);
		set .@total_thief,.@thief_item1 + .@thief_item2;
		set .@money_thief,((.@thief_item1 * 5) + (.@thief_item2 * 2)) + 200;
		show "[Comrade]";
		if (countitem(1069) != 0) {
			show "Primeiro, deixe-me checar quantos Cogumelos Venenoso você pegou.";
			show "Huh, " + countitem(1069) + " deles.";
		}
		if (countitem(1070) != 0)
			show "Agora vou checar seus Cogumelos Borrachento. Você pegou " + countitem(1070) + " deles.";
		next;
		show "[Camarada]";
		show "Então você trouxe";
		show "um total de...";
		next;
		show "[Camarada]";
		show "Hmmm. " + .@total_thief + " graus, multiplicados pela velocidade da luz, dividida pelo integral de pi vezes o peso extra do valor absoluto dos políticos...";
		next;
		show "[Camarada]";
		show "Ok!";
		show "Eu consegui.";
		next;
		show "[Camarada]";
		if (.@total_thief > 25) {
			show "Você conseguiu mais";
			show "que 25 pontos!";
			show "Impressionante!";
		}
		else if (.@total_thief == 25) {
			show "Exatamente 25 pontos!";
			show "Você conseguiu!";
		}
		else {
			show "Definitivamente menos que os 25 pontos que você precisa para passar. Volte lá e pegue mais Cogumelos!";
			close;
		}
		next;
		show "[Camarada]";
		show "" + strcharinfo(0) + "...";
		show "Você passou no teste oficial de Gatuno. Agora você é um de nós.";
		if (countitem(1069) != 0) delitem 1069,countitem(1069); //Mushroom_Of_Thief_1
		if (countitem(1070) != 0) delitem 1070,countitem(1070); //Mushroom_Of_Thief_2
		callfunc "Job_Change",Job_Thief;
		callfunc "F_ClearJobVar";
		next;
		show "[Camarada]";
		show "Parabéns por ter se tornado um Gatuno! Apartir de agora, seja um representante honorável do Clã de Gatunos.";
		next;
		show "[Camarada]";
		show "Se você trouxer desgraça para nosso clã, você será assassinado. Eu espero que você traga orgulho para nós.";
		next;
		show "[Brad]";
		show "*Ahem* Bem-vindo ao Clã, camarada! Eu sou Brad, e sou encarregado dos recursos humanos aqui.";
		next;
		show "[Brad]";
		show "Aqui está uma pequena ajuda para um iniciante como você. Gaste com o que você quiser. Tudo certo então, vejo você por ai~";
		set zeny,zeny+.@money_thief;
		close;
	}
}

moc_ruins,141,125,3	script	Sr. Irrelevante	83,{
	if (BaseJob == Job_Thief) {
		show "[Sr. Irrelevante]";
		show "Ah, eu vi que agora você é um Gatuno. Eu sempre soube que você iria se juntar a nós.";
		next;
		show "[Sr. Irrelevante]";
		show "Roubar de uma plantação de cogumelos é muito fácil pra você agora. Você deve aumentar suas habilidades e aprender nossa profissão.";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		show "[Sr. Irrelevante]";
		switch(rand(4)) {
		case 1:
			show "Eu poderia beber uma boa bebida agora.";
			close;
		case 2:
			show "Me dê seu dinheiro.";
			next;
			show "[Sr. Irrelevante]";
			show "Brincadeira, estou fora do horário.";
			close;
		case 3:
			show "QUEM VOCÊ ESTÁ CHAMANDO DE PSICOPATA?!?!";
			close;
		case 4:
			show "Eu não tenho nada pra te dizer. Importa-se de me deixar sozinho?";
			close;
		default:
			show "Hoje parece um bom dia para ir nas pirâmides e caçar com alguns amigos.";
			close;
		}
	}
	if (job_thief_q == 3) {
		show "[Sr. Irrelevante]";
		show "Hahahahaha~!";
		show "Você não passou";
		show "no teste ainda?";
		show "Ok, eu deixarei você entrar...";
		close2;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 2) {
		show "[Sr. Irrelevante]";
		show "Hmm...";
		show "Você veio aqui por causa do teste, certo? Eu posso ver nos seus olhos que você sabe alguma coisa.";
		next;
		show "[Sr. Irrelevante]";
		show "Seu nome é " + strcharinfo(0) + "? Ah, está na lista. Tudo certo, eu deixarei você entrar na Plantação de Cogumelos, mas não posso garantir sua segurança...";
		close2;
		set job_thief_q,3;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 1) {
		show "[Sr. Irrelevante]";
		show "Tem um cheiro estranho vindo de... Você. Agora, por que razão?";
		close;
	}
	show "[Sr. Irrelevante]";
	show "Ei Aprendiz! Por que você não junta-se do Clã dos Gatunos? Vocês iniciantes são sempre bem-vindos para juntar-se a nós e nossa causa egoísta.";
	next;
	show "[Sr. Irrelevante]";
	show "Você pode obter mais informações na sala do subsolo da Pirâmide 1 BF.";
	close;
}

// Monsters
//============================================================
job_thief1,0,0,0,0	monster	Cogumelo Laranja	1182,180,0,0,0
job_thief1,0,0,0,0	monster	Fabre	1184,50,0,0,0
job_thief1,0,0,0,0	monster	Chonchon	1183,50,0,0,0
job_thief1,0,0,0,0	monster	Esporo	1014,30,0,0,0

//============================================================
// mapflag
//============================================================
job_thief1	mapflag	nomemo
job_thief1	mapflag	noteleport
job_thief1	mapflag	nosave	SavePoint
job_thief1	mapflag	nopenalty
job_thief1	mapflag	nobranch