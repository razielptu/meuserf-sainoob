//===== Cronus Script ========================================
//= Quest para Classe Ninja
//===== Por: =================================================
//= SoulBlaker
//===== Versão Atual: ========================================
//= 1.0
//===== Compatível Com: ======================================
//= Cronus
//===== Descrição: ===========================================
//= Quest para se tornar um Ninja.
//= [ Oficial do bRO ]
//===== Comentários Adicionais: ==============================
//= 1.0 Iniciado o NPC. [SoulBlaker]
//= - Obrigado Protimus por me ajudar.
//============================================================
alberta,30,65,3	script	Akagi#nq	730,{

	if(Class == Job_Novice)
	{
		if(JobLevel > 9)
		{
			show "[Akagi]";
			show "Hmm...";
			show "Você deve ter vindo";
			show "sentindo que alguém";
			show "estava esperando por você aqui.";
			show "Diga-me, você procura";
			show "o caminho da força?";
			next;
			if(select("Não:Sim") == 1)
			{
				show "[Akagi]";
				show "Entendo.";
				show "Cada um faz suas escolhas,";
				show "suponho. Tome cuidado";
				show "e boa sorte.";
				close;
			}
			else
			{
				show "[Akagi]";
				show "Muito bem.";
				show "Então, deixe-me";
				show "prepará-lo para";
				show "seguir o caminho imediatamente...";
				close2;
				switch (rand(3)) {
					case 1:  warp "amatsu", 170, 229; break;
					case 2:  warp "amatsu", 216, 188; break;
					default: warp "amatsu", 178, 176; break;
				}
				end;
			}
		}
		else
		{
			show "[Akagi]";
			show "Hmm? Eu não posso ajudá-lo";
			show "até que você";
			show "se torne";
			show "um pouco mais forte.";
			close;
		}
	}
	else
	{
		show "[Akagi]";
		show "Humm...";
		show "Você e eu...";
		show "Somos muito parecidos em termos de habilidade e combate.";
		show "Talvez possamos treinar";
		show "juntos no futuro.";
		close;
	}
}

que_ng,30,65,3	script	Cougar#nq	730,{

	if(Class == Job_Novice && JobLevel > 9)
	{
		switch(NINJ_Q)
		{
			case 0:
				show "["+strcharinfo(0)+"]";
				show "Com licença.";
				show "O-olá?";
				next;
				show "[Cougar]";
				show "......................";
				show "Como você fez isso?";
				next;
				show "["+strcharinfo(0)+"]";
				show "Fiz o quê? Eu não";
				show "fiz nada, acho que não...";
				next;
				show "[Cougar]";
				show "Como você consegue";
				show "me ver? Eu deveria estar";
				show "invisível a olhos nus.";
				show "Ah, agora eu entendi. Wildcat Joe";
				show "deve ter mandando você para me";
				show "assassinar!  Mas eu não cairei";
				show "nos seus truqes! Morra!";
				next;
				show "["+strcharinfo(0)+"]";
				show "E-espere! E-eu nem sei";
				show "quem é esse tal de Wildcat Joe!";
				show "Calma, não precisa";
				show "apelar para a violência!";
				next;
				show "[Cougar]";
				show "Quê...?";
				show "Como você se esquivou";
				show "de todos os meus ataques?";
				show "Você tem talento,";
				show "devo admitir.";
				next;
				show "["+strcharinfo(0)+"]";
				show ".......................";
				show "Eu vim aqui esperando";
				show "trocar minha classe";
				show "para Ninja.";
				next;
				show "[Cougar]";
				show "... Oh. É só isso?";
				show "Hmm, você tem um grande";
				show "potencial, mas não posso ajudá-lo";
				show "agora. Eu tenho muitos";
				show "inimigos, e não posso";
				show "baixar a guarda nem por um segundo.";
				next;
				show "[Cougar]";
				show "Wildcat Joe é";
				show "muito cruel...!";
				show "Elepoderia atacar a qualquer momento!";
				show "Ele fará qualquer coisa para alcançar";
				show "a vitória sobre seus inimigos!";
				next;
				show "[Cougar]";
				show "Espere, espere, eu acabei";
				show "de pensar em algo.";
				show "Talvez você possa me ajudar.";
				show "Faça o que eu peço e eu ensinarei";
				show "a você algumas das minhas habilidades, se";
				show "realmente quiser tornar-se um Ninja.";
				next;
				switch( select( "Claro.","Não, obrigado." ) )
				{

					case 1:
						show "[Cougar]";
						show "Ótimo! Eu gostaria de";
						show "perguntar a Wildcat Joe se ele";
						show "concordaria com uma trégua temporária";
						show "Sei que nós dois";
						show "já não temos mais armas, então";
						show "devemos nos equipar antes de lutar.";
						next;
						show "[Cougar]";
						show "Por favor, tome esta carta";
						show "e entregue-a a Wildcat";
						show "Joe em Einbroch. Ele é um mestre";
						show "dos disfarces, então fique atento";
						show "para encontrá-lo. Ah, e procure";
						show "por ele em um lugar alto.";
						next;
						show "[Cougar]";
						show "Isso mesmo, Wildcat Joe";
						show "sempre gostou de se";
						show "esconder em lugares altos.";
						show "Enfim, depois de entregar-lhe";
						show "a carta, volte e";
						show "me diga qual foi sua resposta.";
						set NINJ_Q,1;
						close;

					case 2:
						show "[Cougar]";
						show "Hm? Certo, certo.";
						show "Mesmo assim, eu não entendo";
						show "porque não podemos nos ajudar";
						show "mutuamente nessa";
						show "situação desagradável...";
						close;
				}

			case 1:
				show "[Cougar]";
				show "Mesmo que essa tarefa";
				show "não seja urgente,";
				show "por favor, apresse-se e vá para";
				show "Eibroch para entregar";
				show "minha carta a Wildcat Joe.";
				close;

			case 2:
				show "[Cougar]";
				show "Não se esqueça de me trazer a resposta de volta.";
				show "Tenho certeza de que ele não vai rejeitar a proposta.";
				close;

			case 3:
				show "[Cougar]";
				show "Ahm você retornou.";
				show "Então, Wildcat Joe o enviou";
				show "de volta com sua";
				show "resposta? Ótimo, ótimo,";
				show "por favor, deixe-me ler.";
				next;
				show "[Cougar]";
				show "Quê...? Como ele pode";
				show "rejeitar minha proposta";
				show "de trégua?! Isso só pode";
				show "significar que ele fez outra";
				show "Kunai. Poxa! Tenho que alcançá-lo";
				show "ou estarei arruinado!";
				next;
				show "[Cougar]";
				show "Escute, você deve me ajudar";
				show "mais uma vez! Eu preciso que você";
				show "busque alguns materiais para que";
				show "eu possa fazer minha própria Kunai";
				show "para enfrentar Wildcat Joe. Então,";
				show "eu cumprirei nosso trato";
				show "e mudarei sua classe para Ninja.";
				next;
				show "[Cougar]";
				show "Tudo o que você tem";
				show "de me trazer são";
				show "^0000ff5 Cyfars^000000 e";
				show "^0000ff1 Fracon^000000.";
				show "Por favor, consiga esses";
				show "materiais o mais rápido possível!";
				next;
				show "["+strcharinfo(0)+"]";
				show "Hã? Que engraçado,";
				show "Wildcat Joe também";
				show "me pediu para conseguir";
				show "esses mesmos materiais.";
				next;
				show "[Cougar]";
				show "Maldição! Então, isso significa...";
				show "Que você ajudou Joe";
				show "a fazer sua Kunai! Não!";
				show "Eu devia ter pensando nisso";
				show "antes! Bem, agora";
				show "é tarde demais. Apenas";
				show "a-apresse-se!";
				set NINJ_Q,4;
				close;

			case 4:
				show "[Cougar]";
				if(countitem(7053) < 5 || countitem(1010) < 1)
				{
					show "Apresse-se e me traga";
					show "^0000ff5 Cyfars^000000 e";
					show "^0000ff1 Fracon^000000";
					show "para que eu possa fazer";
					show "minha própria Kunai para";
					show "enfrentar Wildcat Joe!";
					close;
				}
				if(skillpoint >0)
				{
					show "Por favor volte depois que você aprender todas as habilidades básicas.";
					close;
				}
				show "Ah, você voltou com";
				show "todos os que preciso.";
				show "E você voltou antes do que";
				show "eu esperava. Ótimo,";
				show "como prometido, eu irei";
				show "torná-lo um Ninja.";
				next;
				show "[Cougar]";
				show "Permita que eu me";
				show "apresente. Eu sou o Mestre Ninja Cougar";
				show "do Grupo Ninja Touga, e";
				show "estou encarregado do grupo";
				show "de busca para encontrar Sir Kazma.";
				next;
				show "[Cougar]";
				show "Sir Kazma é o líder";
				show "de minha vila, mas ele";
				show "foi embora. Sua fuga é o resultado";
				show "de um conflito interno no";
				show "Grupo Ninja. As coisas estão";
				show "muito instáveis no momento...";
				next;
				show "[Cougar]";
				show "No início, eu não queria";
				show "aceitá-lo como um Ninja devido";
				show "a essa situação complicada.";
				show "Porém, você provou ser";
				show "realmente digno de juntar-se";
				show "ao nosso grupo de Ninjas.";
				next;
				show "[Cougar]";
				show "De acordo com esta carta, até mesmo";
				show "Joe reconhece seu valor. Apenas";
				show "Lembre-se que, como um Ninja, sua";
				show "missão é sua maior prioridade. Mas";
				show "não deixe que os objetivos de sua missão";
				show "tornem-se mais importantes que sua consciência.";
				next;
				show "[Cougar]";
				show "\"Sigilo acima de tudo.\"";
				show "Para manter nossos segredos";
				show "em sigilo, você só pode comprar";
				show "ou vender armas Ninja em ";
				show "vendedores autorizados. Por favor,";
				show "não se esqueça disso.";
				next;
				show "[Cougar]";
				show "A partir de hoje, você";
				show " é um membro do esplêndido";
				show "Grupo Ninja Touga. Seja ágil";
				show "Como o vento, e";
				show "quieto como o cair das sombras.";
				nude;
				callfunc "Job_Change",Job_Ninja;
				callfunc "F_ClearJobVar";				
				additem 13010,1;
				close;

		}
	}
	else if(Class == Job_Novice && JobLevel < 10)
	{
		show "[Cougar]";
		show "It looks like you need to train more.";
		show "Come back after you gained more muscle.";
		close;
	}
	else if(Class == Job_Ninja)
	{
		show "[Cougar]";
		show "Como está indo?";
		show "Treine com afinco: você quer";
		show "ser capaz de desaparecer";
		show "sem deixar rastros. Se você";
		show "puder fazer isso, conquistará";
		show "o respeito de todos os Ninjas.";
		close;
	}
	else if(Class == Job_Baby)
	{
		show "[Cougar]";
		show "What the...! A super baby!";
		show "Has this location been so exposed that even babies know where it is?";
		show "Oh no~ Ah no~";
		close;
	}
	else
	{
		show "[Cougar]";
		show "What the.. What's that stench?!";
		show "I was wondering what was causing that stench -- it was you.";
		show "Why, you're so young, there's still a milk mustache on your lips from drinking from your mother's bosoms.";
		show "Get lost, squirt.";
		close;
	}
}

einbroch,184,194,3	script	Homem Suspeito#nq	881,{

	if(Class == Job_Ninja)
	{
		show "[Red Leopard Joe]";
		show "Oh, você é um Ninja!";
		show "Espero que continue";
		show "a treinar para dominar";
		show "todas as técnicas ninjas";
		show "que puder. E lembre-se de sempre";
		show "ser um com as sombras.";
		close;
	}
	else if(!NINJ_Q)
	{
		show "[Turista]";
		show "Eu já viajei para muitos";
		show "países, mas nunca";
		show "tive em uma construção tão alta";
		show "quanto a Torre de Einbroch. Todos os";
		show "prédios da minha cidade natal";
		show "são tão pequenos quando comparados com ela...";
		close;
	}
	switch(NINJ_Q)
	{
		case 1:
			show "[Homem Suspeito]";
			show "Eu já viajei para muitos";
			show "países, mas nunca";
			show "tive em uma construção tão alta";
			show "quanto a Torre de Einbroch. Todos os";
			show "prédios da minha cidade natal";
			show "são tão pequenos quando comparados com ela...";
			next;
			show "["+strcharinfo(0)+"]";
			show "Oh, você é de";
			show "Amatsu? Estou procurando";
			show "por alguém de lá";
			show "que atenda pelo nome de Wildcat Joe.";
			next;
			show "[Homem Suspeito]";
			show "... Não. Na verdade, eu";
			show "sou de Izlude, e só vim";
			show "até Einbroch em busca de alguns";
			show "minerais. Diga-me, por que você";
			show "está procurando por esse tal Wildcat Joe?";
			next;
			show "["+strcharinfo(0)+"]";
			show "Bem, eu preciso entregar";
			show "esta carta para ele e";
			show "esperar por sua resposta para que";
			show "possa me tornar um Ninja.";
			next;
			show "[Homem Suspeito]";
			show " É mesmo? Pensando bem,";
			show "acho que eu o encontrei";
			show "uma ou duas vezes";
			show "nesta cidade. Mas ele prefere ser";
			show "chamado de \"Rede Leopard Joe,\"";
			show "em vez de \"Wildcat Joe.\"";
			next;
			show "[Homem Suspeito]";
			show "Eu gostaria de ajudá-lo";
			show "a encontrar esse homem, mas";
			show "primeiro preciso";
			show "encontrar minerais que";
			show "estou procurando. Se não lhe causar";
			show "problemas, você não gostaria de me ajudar?";
			show "Então eu poderia ajudá-lo a";
			show "encontrar Joe.";
			next;
			switch( select( "Não se preocupe, eu irei encontrá-lo sozinho.","Claro, eu ajudo." ) )
			{
				case 1:
					show "[Homem Suspeito]";
					show "Tem certeza...?";
					show "Red Leopard Joe é um verdadeiro";
					show "mestre dos disfarces. Você irá";
					show "precisar de toda ajuda que conseguir";
					show "para encontrá-lo.";
					close;

				case 2:
					show "[Homem Suspeito]";
					show "Ótimo, fico feliz em";
					show "Ouvir isso. Por favor,";
					show "Ajude-me a encontrar";
					show "^0000FF5 Cyfars^000000 e";
					show "^0000FF1 Fracon^000000.";
					set NINJ_Q,2;
					close;
			}

		case 2:
			if(countitem(1010) < 1 || countitem(7053) < 5){
				show "[Turista]";
				show "Por favor, traga";
				show "^0000FF5 Cyfars^000000 e";
				show "^0000FF1 Fracon^000000 para mim";
				show "o mais rápido que puder. Então,";
				show "eu poderia ajudá-lo a encontrar";
				show "Red Leopard Joe.";
				close;
			}
			show "[Homem Suspeito]";
			show "Bom, bom. Você";
			show "trouxe os minerais...";
			show "Agora é a minha vez de";
			show "ajudá-lo. Vamos,";
			show "deixe-me ver essa carta.";
			next;
			show "["+strcharinfo(0)+"]";
			show "?????!!";
			next;
			show "[Homem Suspeito]";
			show "Por quê? Você não trouxe uma carta de Cougar para mim?";
			next;
			show "["+strcharinfo(0)+"]";
			show "Você é...";
			show "Você é Wildcat Joe?";
			next;
			show "[Homem Suspeito]";
			show "... Sim, mas eu prefiro";
			show "ser chamado de Red Leopard Joe.";
			show "Cougar o enviou, nã é mesmo?";
			show "Ele é o único que me chama";
			show "assim. Então, você quer ser";
			show "um Ninja, não? Hmm, muito bem.";
			next;
			show "[Red Leopard Joe]";
			show "Se você quer se tornar um Ninja,";
			show "deveria estar sempre atento";
			show "ao que vê e em quem ^ff0000 ^000000 confia.";
			show "Não se esqueça que se";
			show "seus segredos forem descobertos,";
			show " você estará acabado como um Ninja.";
			next;
			show "[Red Leopard Joe]";
			show "Lembre-se de se mover";
			show "rapidamente, e também de sempre";
			show "desaparecer sem deixar rastros.";
			show "Porque a capacidade de se esconder";
			show "nas sombras representa nosso";
			show "poder definitivo.";
			next;
			show "["+strcharinfo(0)+"]";
			show "Entendo...";
			show "......";
			next;
			show "[Red Leopard Joe]";
			show "Por hora, deixa-me ler";
			show "esta carta. Vamos ver...";
			show "Hm. Eu achava que Cougar";
			show "gostaria de me desafiar";
			show "novamente, mas na verdade ele quer";
			show "propor uma trégua? Ha!";
			next;
			show "[Red Leopard Joe]";
			show "Graças à sua ajuda,";
			show "eu tenho os minerais";
			show "que preciso para fazer uma Kunai!";
			show "Hahaha! Eu não concordarei com uma";
			show " trégua quando tenho a vantagem!";
			next;
			show "[Red Leopard Joe]";
			show "Mas enfim, vou escrever minha";
			show "Resposta para ele. Eu também";
			show "vou escrever uma recomendação para você...";
			show "Acho que você se tornará um grande";
			show "Ninja, mesmo que eu tenha acabado de";
			show "enganá-lo. Heh heh heh!";
			next;
			show "["+strcharinfo(0)+"]";
			show "......";
			show ".........";
			show "............";
			next;
			show "[Red Leopard Joe]";
			show "Aqui está.";
			show "Por favor, leve esta";
			show "carta para Cougar.";
			show "Levará algum tempo para";
			show "voltar à Amatsu, então permita-me";
			show "enviar você diretamente para lá...";
			delitem 1010,1;
			delitem 7053,5;
			set NINJ_Q,3;
			close2;
			warp "amatsu",113,127;
			end;
		case 3:
			show "[Red Leopard Joe]";
			show "Hein? Não sei o que";
			show "aconteceu, mas parece que";
			show "você ainda não entregou";
			show "minha resposta para Cougar.";
			show "Devo mandá-lo diretamente";
			show "para Amatsu agora mesmo?";
			next;
			switch( select( "Não, obrigado.","Sim, por favor." ) )
			{
				case 1 :
					show "[Red Leopard Joe]";
					show "Certo. Bem, eu estava";
					show "apenas tentando economizar";
					show "algum tempo para você.";
					close;

				case 2:
					show "[Red Leopard Joe]";
					show "Então está bem.";
					show "Adeus, por enquanto.";
					close2;
					warp "amatsu",113,127;
					end;
			}

		case 4:
			show "[Red Leopard Joe]";
			show "Cougar pediu que você";
			show "também conseguisse alguns";
			show "materiais? Ah, bem, acho que";
			show "não posso culpá-lo por isso.";
			show "Além disso, sou capaz de";
			show "derrotá-lo em uma luta justa!";
			close;
	}
}