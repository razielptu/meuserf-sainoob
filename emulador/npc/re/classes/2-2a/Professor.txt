//==========================================================
//  ___  ___       ___   ____    ____  ___      
// |   \/   |     /   \  \   \  /   / /   \     
// |  \  /  |    /  ^  \  \   \/   / /  ^  \    
// |  |\/|  |   /  /_\  \  \_    _/ /  /_\  \   
// |  |  |  |  /  _____  \   |  |  /  _____  \  
// |__|  |__| /__/     \__\  |__| /__/     \__\ 
//
//==========================================================
//= Quest do Professor
//===== Por: =========================================================
//= rA dev Team; LyDe
//===== Versão Atual: ================================================
//= 1.0
//===== Compatível com: ==============================================
//= eAthena; rAthena; Cronus; brAthena; Maya SVN.
//===== Descrição: ===================================================
//= Mudança de Classe para Professor
//===== Comentários Adicionais: ======================================
//= 1.0 Traduzido. [LyDe]
//====================================================================

valkyrie,53,47,3	script	Professor#Valkyrie	743,{
	if (ADVJOB == 0 || Upper != 1) {
		mes "[Professor]";
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "Parabéns.";
			mes "Honra aos guerreiros!";
			close;
		}
		mes "Isso leva uma vida toda...";
		mes "Literalmente uma vida toda";
		mes "para acumular o conhecimento";
		mes "necessário para se tornar";
		mes "um Professor...";
		next;
		mes "[Professor]";
		mes "Isso é esmagador.";
		mes "Quanto mais você aprende,";
		mes "mais você descobre";
		mes "aquilo que ainda não sabe.";
		mes "Não existe fim no";
		mes "processo de aprendizagem...";
		close;
	} else if (ADVJOB == Job_Professor && Class == Job_Mage_High && JobLevel > 39) {
		mes "[Professor]";
		mes "Rune-Midgard não";
		mes "tem Professores suficientes";
		mes "para ajudar o povo nessa nova era";
		mes "de prosperidade. O";
		mes "mundo precisa de você...";
		next;
		mes "[Professor]";
		mes "Você aceitará essa";
		mes "incrível responsabilidade?";
		mes "Você servirá Rune-Midgard";
		mes "como um Professor?";
		next;
		if (select("Não.:Sim.") == 1) {
			mes "[Professor]";
			mes "Quando estiver pronto,";
			mes "sinta-se livre para voltar.";
			mes "Honra aos guerreiros!";
			close;
		}
		mes "[Professor]";
		if (SkillPoint) {
			mes "Você ainda tem pontos de habilidade. Por favor,";
			mes "use todos os pontos de habilidade restantes.";
			close;
		}
		jobchange Job_Professor;
		set ADVJOB,0;
		mes "Parabéns!";
		mes "Como Professor, eu espero";
		mes "que você fará sua parte";
		mes "ativa trazendo";
		mes "a luz da sabedoria";
		mes "onde há escuridão.";
		close;
	}
	mes "[Professor]";
	mes "Bem-vindo";
	mes "a Valhalla,";
	mes "o Salão da Honra.";
	next;
	mes "[Professor]";
	mes "Por favor sinta-se";
	mes "confortável enquanto";
	mes "estiver aqui.";
	mes "Honra aos guerreiros!";
	close;
}

