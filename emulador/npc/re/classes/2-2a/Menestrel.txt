//==========================================================
//  ___  ___       ___   ____    ____  ___      
// |   \/   |     /   \  \   \  /   / /   \     
// |  \  /  |    /  ^  \  \   \/   / /  ^  \    
// |  |\/|  |   /  /_\  \  \_    _/ /  /_\  \   
// |  |  |  |  /  _____  \   |  |  /  _____  \  
// |__|  |__| /__/     \__\  |__| /__/     \__\ 
//
//==========================================================
//= Quest do Clown
//===== Por: =========================================================
//= rA dev Team; LyDe
//===== Versão Atual: ================================================
//= 1.0
//===== Compatível com: ==============================================
//= eAthena; rAthena; Cronus; brAthena; Maya SVN.
//===== Descrição: ===================================================
//= Mudança de Classe para Clwon
//===== Comentários Adicionais: ======================================
//= 1.0 Traduzido. [LyDe]
//====================================================================

valkyrie,53,54,3	script	Menestrel#Valkyrie	741,{
	if (ADVJOB == 0 || Upper != 1) {
		mes "[Menestrel]";
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "Parabéns.";
			mes "Honra aos guerreiros!";
			close;
		}
		mes "Você quer cantar";
		mes "uma canção comigo?";
		mes "Sha lá lá lá lá~";
		close;
	} else if (ADVJOB == Job_Clown && Class == Job_Archer_High && JobLevel > 39) {
		mes "[Menestrel]";
		mes "O mundo aborrecido dos";
		mes "mortais está necessitando";
		mes "de uma canção mais alegre.";
		mes "você vai trazer isto para";
		mes "eles e virar a maré na batalha";
		mes "contra o mal?";
		next;
		mes "[Menestrel]";
		mes "Você faria isso";
		mes "por Rune-Midgard...";
		mes "Como um Menestrel?";
		next;
		if (select("Não.:Sim.") == 1) {
			mes "[Menestrel]";
			mes "Quando você estiver pronto,";
			mes "Sinta-se livre para voltar.";
			mes "Honra aos guerreiros!";
			close;
		}
		mes "[Menestrel]";
		if (SkillPoint) {
			mes "Você ainda tem pontos de habilidades. Por favor,";
			mes "use todos os seus pontos de habilidade restantes, e depois volte aqui.";
			close;
		}
		jobchange Job_Clown;
		set ADVJOB,0;
		mes "Parabéns!";
		mes "Como um Menestrel, suas";
		mes "canções trarão a";
		mes "esperança aos seus aliados, e o";
		mes "desespero aos seus inimigos.";
		close;
	}
	mes "[Menestrel]";
	mes "Bem-vindo";
	mes "a Valhalla,";
	mes "o Salão da Honra.";
	next;
	mes "[Menestrel]";
	mes "Por favor";
	mes "sinta-se confortável";
	mes "enquanto estiver aqui.";
	mes "Honra aos guerreiros!";
	close;
}
