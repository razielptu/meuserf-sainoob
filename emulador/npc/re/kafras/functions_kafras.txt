//====================================================================
//          _________                                                  
//          \_   ___ \_______  ____   ____  __ __  ______             
//          /    \  \/\_  __ \/    \ /    \|  |  \/  ___/              
//          \     \____|  | \(  ( ) )   |  \  |  /\___ \               
//           \______  /|__|   \____/|___|  /____//____  >              
//                  \/                   \/           \/               
//                                                               
//====================== Cronus Script ===============================
//= Functions das Kafras
//===== Por: =========================================================
//= Schrwaizer
//===== Versão Atual: ================================================
//= 1.1
//===== Compatível com: ==============================================
//= Cronus GIT
//===== Descrição: ===================================================
//= Functions dos Scripts das Kafras
//===== Comentários Adicionais: ======================================
//= 1.1 Traduzido do rAthena de acordo com os diálogos oficiais do bRO. [Schrwaizer]
//====================================================================


// Funções principais ======================================================
//=   arg(0): Usado para determinar qual mensagem de boas-vindas será exibida.
//=   arg(1): Usado para determinar qual menu será exibido.
//=   arg(2): Usado para determinar se o menu de info. será exibido em F_KafInfo.
//=   arg(3): Taxa de uso de Armazém
//=   arg(4): Taxa de Aluguel de Carrinho
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; //Limpeza ultrapassada, variáveis em desuso

	//Mostra a mensagem de boas-vindas Kafra
	switch(getarg(0)){
		//Mensagem padrão (obsoleta)
		default:
		case 0:
			mes "[Funcionária Kafra]";
			mes "Bem-vindo a ";
			mes "Corporação Kafra.";
			mes "Nós estamos sempre";
			mes "Ao seu lado.";
			mes "como posso ajudá-lo?";
			break;

		//Mensagem exibida em Niflheim
		case 1:
			mes "[Funcionária Kafra]";
			mes "^666666W-b-b-eee-m-vindo...";
			mes "a-a... c-cor-poração k-kafra...";
			break;

		//Mensagem de Kafras Feudais
		case 2:
			set @GID,getcharid(2);
			mes "[Funcionária Kafra]";
	 		mes "Seja bem-vindo, membro da ^ff0000" + GetGuildName(@GID) + "^000000.";
			mes "A Corporação Kafra te acompanhará onde quer que esteja.";
			break;

		//Mensagem específica de Amatsu (Obsoleta)
		case 3:
			mes "[Funcionária Kafra]";
			mes "Então, você veio de muito longe para estudar nossa cultura, ou está apenas a passeio?";
			mes "De qualquer forma, por que não fica um pouco?";
			mes "O ar estará eternamente perfumado";
			mes "pelo gentil aroma das plantas selvagens.";
			break;

		//Mensagem específica de Louyang e Ayothaya (Obsoleta)
		case 4:
			mes "[Funcionária Kafra]";
			mes "Com os serviços kafra se";
			mes "estendendo por todos os lugares,";
			mes "você nunca estará longe de casa.";
			break;

		//No caso de uma mensagem própria do NPC. (Não exibe nada)
		case 5:
			break;
	}
	next;

	M_Menu:
		cleararray @K_Menu0$[0],"",7;
	switch(getarg(1)){
		// Save and Storage only
		case 1:
			setarray @K_Menu0$[0],"Salvar","Usar Armazém","Cancelar";
			break;

		// Storage only
		case 2:
			setarray @K_Menu0$[0],"Usar Armazém","Cancelar";
			break;

		// No Teleport (Common)
		case 3: 
			setarray @K_Menu0$[0],"Save","Use Storage","Rent a Pushcart","Guide","Check Other Information","Cancel";
			break;


		// Sem salvar, ou teleporte. (comum)
		case 5:
			setarray @K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Checar outras informações","Guia","Cancelar";
			break;

		// Apenas armazém e checagem de informações.
		case 6:
			setarray @K_Menu0$[0],"Usar Armazém","Guia","Checar outras informações","Cancelar";
			break;

		// Salvar, armazém, e alugar carrinhos.
		case 7:
			setarray @K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Cancelar";
			break;

		// Salvar, armazem, e outras informações. (turbo track)
		case 8:
			setarray @K_Menu0$[0],"Salvar","Usar Armazém","Checar outras informações","Cancelar";
			break;

		// Funcionaria Kafra Comum
		default:
			setarray @K_Menu0$[0],"Salvar","Usar Armazém","Teleporte","Alugar Carrinho","Guia","Checar outras informações","Cancelar";
			break;
	}


	// Kafra feudal - Menu (Teleporte, Armazém e Carrinho gratuitos)
	if(getarg(0)==2) {
		cleararray @K_Menu0$[0],"",7;
		setarray @K_Menu0$[0],"Usar Armazém","Usar Armazém do Clã","Alugar Carrinho","Teleporte","Cancel";
	}
	menu 	@K_Menu0$[0],K_Menu0,@K_Menu0$[1],K_Menu1,@K_Menu0$[2],K_Menu2,
		@K_Menu0$[3],K_Menu3,@K_Menu0$[4],K_Menu4,@K_Menu0$[5],K_Menu5,
		@K_Menu0$[6],K_Menu6,@K_Menu0$[7],K_Menu7;
	K_Menu0:
		set @num,0;
		goto K_Menuf;
	K_Menu1:
		set @num,1;
		goto K_Menuf;
	K_Menu2:
		set @num,2;
		goto K_Menuf;
	K_Menu3:
		set @num,3;
		goto K_Menuf;
	K_Menu4:
		set @num,4;
		goto K_Menuf;
	K_Menu5:
		set @num,5;
		goto K_Menuf;
	K_Menu6:
		set @num,6;
		goto K_Menuf;
	K_Menu7:
		set @num,7;
	K_Menuf:
		if (@K_Menu0$[@num] == "Salvar") return;
		if (@K_Menu0$[@num] == "Usar Armazém"){
			// Do not charge for Guild Storage
			if(getarg(0) == 2) callfunc "F_KafStor",2,0,0;
			else callfunc "F_KafStor",0,getarg(3),getarg(0);
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Teleporte"){
			// Mostra mensagem de 'Teleporte indisponivel' em Schwaltzvalt
			if(getarg(1) == 4){
				mes "[Funcionária Kafra]";
				mes "Devido ao ^FF0000Acordo";
				mes "de Restrição de Teleporte^000000, a";
				mes "Corporação Kafra não pode";
				mes "fornecer serviços de teleporte";
				mes "na República de Schwaltzvalt.";
				next;
				mes "[Funcionária Kafra]";
				mes "Pedimos que, ao invés disso,";
				mes "você use o serviço de Aeroplano";
				mes "para chegar a localização desejada.";
				mes "Agradecemos a compreensão.";
				next;
				goto M_Menu;
			}
			callfunc "F_KafTele",getarg(0);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Alugar Carrinho"){
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1) next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Guia"){
			callfunc "F_KafGuide";
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Checar outras informações"){
			callfunc "F_KafInfo",getarg(2);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Cancelar"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		}
		if (@K_Menu0$[@num] == "Usar armazém do Clã"){
			callfunc "F_KafStor",1,0;
			next;
			goto M_Menu;
		}

}


// Função de Armazem =======================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage() == 1){
			mes "[Funcionária Kafra]";
			mes "Sinto muito mas, algum outro companheiro de clã está utilizando";
			mes "o armazém agora. Por favor, aguarde até que ele tenha temrinado.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}

	// Habilidades insuficientes para acessar armazém
	if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
		mes "[Funcionária Kafra]";
		// Mensagem específica de Niflheim
		if (getarg(2) == 1) {
			mes "^666666S-p-perd-dão, mas...";
			mes "vo-você é um a-aprendiz...";
			mes "v-volte depois d-de aprender..";
			mes "Habilidades b-básicas nível 6...^000000";
			return;
		}
		mes "Sinto muito, mas você";
		mes "precisa de 'Habilidades Básicas'";
		mes "Nível 6 para utilizar o";
		mes "serviço de armazém.";
		return;
	}

	// Acessando Armazém Normal
	if(getarg(0) != 2){
		// Consumo de "Vale-Armazém" está ativado.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[Funcionária Kafra]";
				// Mensagem Específica de Niflheim
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "M-mais z-zeny...!";
					mes "N-necessita de 150 zeny...";
					mes "Ergh! T-tomarei s-seu sangue~!^000000";
					return;
				}
				//Mensagem padrão
				mes "Sinto muito, mas você não";
				mes "possui zeny suficiente para";
				mes "utilizar nosso serviço de armazém.";
				mes "Nossa taxa de armazém é de "+getarg(1)+" zeny.";
				return;
			}
			set Zeny, Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}

	// Elimina a mensagem final em castelos
	if (getarg(0) != 2) {
		// Niflheim msg
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Funcionária Kafra]";
			mes "^666666Obrig-gado porrr usar...";
			mes "Obrig-gado porrr usar...";
			mes "Obrig-gado porrr usar...";
			mes "Obrig-gado porrr usar...";
			mes "Obrig-gado porrr usar...^000000";
		}
		// Msg normal
		else {
			mes "[Funcionária Kafra]";
			mes "Obrigado por utilizar";
			mes "os nossos serviços.";
			mes "Deixe-me abrir o seu";
			mes "armazém agora mesmo.";
		}
	}
	callfunc("F_CheckKafCode");	//Checa a senha do armazém, caso setada

	close2;
	openstorage;
	cutin "", 255;
	end;
}


// Funçao de teleporte ==================================================
function	script	F_KafTele	{
	mes "[Funcionária Kafra]";
	mes "Por favor, escolha";
	mes "o seu destino.";
	next;

	menu 	@wrpC$[0],M_Wrp0, @wrpC$[1],M_Wrp1, @wrpC$[2],M_Wrp2, @wrpC$[3],M_Wrp3, 
		@wrpC$[4],M_Wrp4, @wrpC$[5],M_Wrp5, @wrpC$[6],M_Wrp6;

	M_Wrp0:
		set @num, 0;
		goto L_Warp;
	M_Wrp1:
		set @num, 1;
		goto L_Warp;
	M_Wrp2:
		set @num, 2;
		goto L_Warp;
	M_Wrp3:
		set @num, 3;
		goto L_Warp;
	M_Wrp4:
		set @num, 4;
		goto L_Warp;
	M_Wrp5:
		set @num, 5;
		goto L_Warp;
	M_Wrp6:
		set @num, 6;

	L_Warp:
		if (@wrpC$[@num] == "Cancelar") return;
		// O consumo do Vale-Teleporte está ativado
		// Mas ele não é consumido dentro de castelos
		if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
		else {
			if (Zeny<@wrpP[@num]){
				mes "[Funcionária Kafra]";
				mes "Sinto muito, mas você não";
				mes "possui zeny suficiente para";
				mes "o serviço de teleporte. Nossa taxa";
				mes "para "+@wrpD$[@num]+" é de "+@wrpP[@num]+" zeny.";
				close2;
				cutin "", 255;
				end;
			}
			set Zeny, Zeny-@wrpP[@num];
			set RESRVPTS, RESRVPTS + (@wrpP[@num]/16);
		}
		if (@wrpD$[@num] == "Alberta") warp "alberta", 117, 56;
		if (@wrpD$[@num] == "Al De Baran") warp "aldebaran",168,112;
		if (@wrpD$[@num] == "Aldebaran") warp "aldebaran",168,112;
		if (@wrpD$[@num] == "Comodo") warp "comodo", 209, 143;
		if (@wrpD$[@num] == "Izlude") warp "izlude", 128, 98;
		if (@wrpD$[@num] == "Geffen") warp "geffen", 120, 39;
		if (@wrpD$[@num] == "Morroc") warp "morocc", 156, 46;
		if (@wrpD$[@num] == "Payon") warp "payon", 161, 58;
		if (@wrpD$[@num] == "Prontera") warp "prontera", 116, 72;
		if (@wrpD$[@num] == "Mjolnir") warp "mjolnir_02", 99, 351;
		if (@wrpD$[@num] == "Farol de Pharos") warp "cmd_fild07", 127, 134;
		if (@wrpD$[@num] == "Vila dos Orcs") warp "gef_fild10", 52, 326;
		if (@wrpD$[@num] == "Umbala") warp "umbala", 100, 154;
		if (@wrpD$[@num] == "Juno") warp "yuno", 158, 125;
		cutin "", 255;
		end;
}


// Função de Carrinho ========================================================
function	script	F_KafCart	{
	// Garante que a classe que ganha um carrinho é mercador
	if(baseClass != Job_Merchant){
		mes "[Funcionária Kafra]";
		mes "Sinto muito, mas o";
		mes "serviço de Aluguel de Carrinho";
		mes "está disponível apenas para mercadores,";
		mes "Ferreiros, Mestres-Ferreiros,";
		mes "Alquimistas e Criadores.";
		return 1;
	}
	// Garante que o personagem já não tem um carrinho
	else if(checkcart() == 1){
		mes "[Funcionária Kafra]";
		mes "Você já possui";
		mes "um carrinho equipado.";
		mes "Infelizmente, não podemos";
		mes "alugar mais de um carrinho";
		mes "simultâneamente para o mesmo cliente.";
		return 1;
	}

	// Consumo de Vale-Carrinho está ativado
	// Não ir consumir caso esteja dentro de castelo
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Funcionária Kafra]";
		mes "A taxa de aluguel de carrinhos";
		mes "é de "+getarg(1)+" zenys. Você";
		mes "gostaria de alugar um carrinho?";
		next;
		menu "Alugar Carrinho.",-, "Cancelar.",M_End;
			if(Zeny<getarg(1)){
				mes "[Funcionária Kafra]";
				mes "Sinto muito, mas você";
				mes "não possui zeny suficiente";
				mes "para pagar a taxa de "+getarg(1)+"";
				mes "zeny para aluguel de carrinho.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;

M_End:
	return 0;
}


// Função de passe ===============================================================
function	script	F_KafGuide	{
	mes "[Funcionária Kafra]";
	mes "WIP...";
	return;
}

// Pontos reservados especiais ===========================================
function	script	F_KafInfo	{

	sM_Menu:
//Descomente a linha a seguir para bloquear a checagem de senha de armazens
//	if(getarg(0) == 0) menu "Check Special Reserve Points.",sM_ResChk, "Kafra Employee Locations",sM_KafLoc, "Cancel",sM_End;
	if(getarg(0) == 0) menu "Checar pontos reservados.",sM_ResChk, "Abrir Armazém",sM_KafCode, "Localização das Kafras",sM_KafLoc, "Cancelar",sM_End;

	sM_ResChk:
		mes "[Funcionária Kafra]";
		mes "Deixe-me ver...";
		mes strcharinfo(0) + "...";
		mes "Ah, você possui um total de";
		mes RESRVPTS+ " Pontos reservados.";
		next;
		mes "[Funcionária Kafra]";
		mes "Você pode trocar seus pontos";
		mes "reservados por prêmios na sede";
		mes "da Corporação Kafra em Al de Baran. Continua utilizando";
		mes "nossos serviços e acumulando pontos em nosso programa.";
		next;
		if(getarg(0) == 1) return;
		goto sM_Menu;

	sM_KafLoc:
		viewpoint 1,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 1,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 1,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 1,@viewpX[3],@viewpY[3],4,0xFF00FF;
		next;
		viewpoint 2,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 2,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 2,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 2,@viewpX[3],@viewpY[3],4,0xFF00FF;
		goto sM_Menu;

	sM_KafCode:
		callfunc("F_SetKafCode","[Funcionária Kafra]","Serviços Kafra");

	sM_End:
		return;
}


// encerrar função =====================================================
//  arg(0): Usado para determinar qual mensagem mostar
//  arg(1): Usado para determinar se a mensagem de savepoint é mostrada
//  arg(2): Usado para determinar a mensagem de qual área você está
//===================================================================
function	script	F_KafEnd	{
	mes "[Funcionária Kafra]";
	//Salvar
	if(getarg(1)==1) {
		mes "Seu ponto de retorno";
		mes "foi salvo bem aqui, em";
		mes getarg(2)+".";
		mes "Obrigado por utilizar";
		mes "os serviços Kafra.";
	}

	// Fim genérico
	else if(getarg(0)==0 || getarg(0)==5) {
		mes "Nós, da Corporação Kafra,";
		mes "estamos sempre nos esforçando para proporcionar os melhores serviços. Esperamos sempre cumprir suas expectativas de um serviço de qualidade.";
	}

	// Fim de Niflheim
	else if(getarg(0)==1) {
		percentheal 0,- 25;
		mes "^666666Kaffffra n-nunca";
		mes "morrrrre-e-eeee. No...";
		mes "No s-seeeeu la-aado-o~^000000";
	}

	close2;
	cutin "", 255;
	end;
}

// Função - Checa senha do armazém ====================
function	script	F_CheckKafCode	{
	if(#kafra_code==0) return;
	mes "Digite a senha do seu armazém:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "Senha incorreta.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// Seta / Muda / Limpa Função senha do armazém ====================
// getarg(0) = Nome do NPC, getarg(1) = Nome da companhia
function	script	F_SetKafCode	{
	mes getarg(0);
	if(#kafra_code) {
		mes "Seu armazém é protegido por senha. O que deseja fazer?";
		next;
		menu "Alterar Senha Atual -> 5000z",-,
		"Remover Senha Atual -> 1000z",M_CLEAR,
		"Cancelar",M_END;
	} else {
		mes "Os "+getarg(1)+" apresentam um novo serviço:";
		mes "Proteção adicional do armazém com uma senha.";
		next;
		menu "Escolher nova senha -> 5000z",M_SET,
		"Cancelar",M_END;
	}

	mes getarg(0);
	mes "Digite a sua ^0000FFsenha atual^000000.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0 || @code != #kafra_code) {
		mes "Senha incorreta. Você não pode trocar sua senha.";
		emotion e_hmm;
		goto M_END;
	}
	next;

M_SET:
	mes getarg(0);
	mes "Agora, digite a sua ^FF0000nova senha^000000.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "A senha não será alterada.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 5000) goto L_ZENY;
	set Zeny,Zeny-5000;
	//set RESRVPTS, RESRVPTS + (5000/50);	//Descomente a linha para ativar ganho de pontos por troca de senha

	set #kafra_code,@code;
	mes "Você trocou a senha do armazém.";
	mes "Obrigado por utilizar os "+getarg(2)+".";
	emotion e_thx;
	goto M_END;

M_CLEAR:
	mes getarg(0);
	mes "Por favor, digite a senha para removê-la.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "A senha não foi removida.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 1000) goto L_ZENY;
	set Zeny,Zeny-1000;
	//set RESRVPTS, RESRVPTS + (1000/50);	////Descomente a linha para ativar ganho de pontos por troca de senha
	if(@code == #kafra_code) {
		set #kafra_code,0;
		mes "Você acaba de remover a senha do seu armazém.";
		mes "Obrigado por utilizar os "+getarg(2)+".";
		emotion e_thx;
	} else {
		mes "Senha incorreta. Não devolveremos seus 1000z.";
		mes "Por favor, da próxima vez, digite a senha correta.";
		emotion e_sry;
	}
	goto M_END;

L_ZENY:
	mes "Você não tem zeny suficiente.";
	emotion e_cash;
M_END:
	close2;
	cutin "",255;
	end;
}

// Validação básica de senha ====================
function	script	F_EntKafCode	{
	mes "Digite um numero entre 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: Tried to fit storage password.";
	}
	if(input(@code_) == 1) {
		mes "Você não pode utilizar uma senha tão grande.";
		return 0;
	}
	if(@code_<1000) {
		mes "Você não pode utilizar uma senha tão pequena.";
		return 0;
	}
	return @code_;
}
