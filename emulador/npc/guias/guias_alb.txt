//===== Cronus Script ========================================
//= Guida de Alberta (Oficial do bRo)
//===== Por: =================================================
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.5c
//===== Compatível com: ======================================
//= Cronus SVN
//===== Changelog: ===========================================
//= Fully working
//= 1.1 Fixed Armory coords, thanks to Freya team
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.5a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//= 1.5b Traduzido o script de acordo com o bRo [Asus]
//= 1.5c Revisado. [RoM]
//============================================================

alberta,23,238,4	script	Guia#alb::AlbGuide	105,{
	cutin "prt_soldier",2;
	show "[Guia de Alberta]";
	show "Bem-Vind"+strsex("o","a")+" à Alberta, a cidade portuária.";
	show "Nós estamos aqui para ajudar os visitantes da cidade. Se você tem alguma dúvida, consulte-nos.";
	show "Se esta é a sua primeira utilizado um guia, por favor selecione 'Tutorial'.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Indicações:Limpar Indicações:Tutorial:Cancelar")) {
		case 1:
			mes "[Guia de Alberta]";
			mes "Por favor, selecione umas das opções.";
			if (.@compass_check == 0) {
				show "Você gostaria que as indicações permaneçam no mini-mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Guilda dos Mercadores^000000:Loja de Armas:Loja de Utilidades:Estalagem:Cancelar")) {
				case 1:
					show "[Guia de Alberta]";
					show "A Guilda dos Mercadores é o lugar";
					show "onde os Aprendizes podem tornar-se";
					show "Mercadores. Ela se encontra ao";
					show "sudoeste da cidade.";
					if (.@compass_check)
						viewpoint 1,33,41,2,0xFF0000;
					break;
				case 2:
					show "[Guia de Alberta]";
					show "A Loja de Armas se encontra no";
					show "sul da cidade.";
					if (.@compass_check)
						viewpoint 1,117,37,3,0xFF00FF;
					break;
				case 3:
					show "[Guia de Alberta]";
					show "A Loja de Utilidades se encontra";				
					show "bem no centro da cidade.";
					if (.@compass_check)
						viewpoint 1,98,154,4,0xFF00FF;
					break;
				case 4:
					show "[Guia de Alberta]";
					show "A 'Estalagem' se encontra no norte";
					show "da cidade.";
					if (.@compass_check)
						viewpoint 1,65,233,5,0xFF00FF;
					break;
				case 5:
					show "[Guia de Alberta]";
					show "Se você deseja apagar as indicações";
					show "do seu mini-mapa, por favor";
					show "selecione 'Limpar indicações'.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0xFF0000;
			viewpoint 2,237,41,3,0xFF00FF;
			viewpoint 2,46,345,4,0xFF00FF;
			viewpoint 2,175,220,5,0xFF00FF;
			viewpoint 2,175,220,6,0xFF00FF;
			set .@compass_check,0;
			break;
		case 3:
			show "[Guia de Alberta]";
  			show "Nós fomos exaustivamente treinados para servi-lo da melhor maneira possível e sem falhas.";
			show "Assim você tem a garantia das melhores indicações possíveis.";
  			next;
  			show "[Guia de Alberta]";
 			show "As indicações que lhe daremos podem ser marcadas no seu mini-mapa, aperte 'Ctrl + Tab', ou clique no botão 'Mapa' no menu principal.";
			show "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
			break;
		case 4:
			show "[Guia de Alberta]";
 			show "Espero que você tenha uma jornada segura e aproveite sua estadia.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "prt_soldier",255;
	end;
}

alberta,120,60,3	duplicate(AlbGuide)	Guia#2alb	105