//===== Cronus Script ======================================= 
//= Guia da Cidade de Morroc (Ofical do bRo)
//===== Por: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: ===================================== 
//= 1.4a/1.5a
//===== Compatível com: ===================================== 
//= Cronus  SVN
//===== Description: ========================================= 
//= Guia da cidade de Morroc
//===== Comentários Adicionais: ================================= 
//= Fully working.  Added a guide at every exit.
//= v1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Removed Duplicates [Silent]
//= 1.4 rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//= 1.5 Atualiado e revisado para versão mais recente [Asus]
//============================================================

morocc,153,286,6	script	Guia#moc::MocGuide	707,{
	cutin "moc_soldier",2;
	show "[Guia de Morroc]";
	show "Bem-vindo à Morroc, a cidade do deserto";
	show "Nós estamos aqui para ajudar os";
	show "visitantes da cidade. Se você tem alguma dúvida, consulte-nos.";
	show "Se esta é sua primeira vez utilizando um guia,";
	show "Por favor selecione 'Tutorial'.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Indicações:Limpar indicações:Tutoriais:Cancelar")) {
		case 1:
			show "[Guia de Morroc]";
			show "Por favor, selecione uma das opções.";
			if (.@compass_check == 0) {
				show "Você quer que eu deixe indicações no Míni-mapa?";
				next;
				if (select("Sim.:Não.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				switch(select("^FF0000Guilda dos Gatunos^000000:Loja de Armas:Estalagem:Bar:Covil:Cancelar")) {
				case 1:
					show "[Guia de Morroc]";
					show "Ouvi dizer que a Guilda dos Gatunos, o lugar aonde Aprendizes podem se tornar Gatunos, se encontra na Pirâmide da saída Noroeste da cidade";
					if (.@compass_check)
						viewpoint 1,24,297,2,0xFF0000;
					break;
				case 2:
					show "[Guia de Morroc]";
					show "'Loja de Armas' se encontra na ponta sudeste da cidade.";
					if (.@compass_check)
						viewpoint 1,253,56,3,0xFF00FF;
					break;
				case 3:
					show "[Guia de Morroc]";
					show "As 'Estalagens' se encontram ao Sul e ao nordeste da cidade.";
					if (.@compass_check) {
						viewpoint 1,197,66,4,0xFF00FF;
						viewpoint 1,273,269,5,0xFF00FF;
					}
					break;
				case 4:
					show "[Guia de Morroc]";
					show "O 'Bar' se encontra na parte Noroeste da cidade.";
					if (.@compass_check)
						viewpoint 1,52,259,6,0xFF00FF;
					break;
				case 5:
					show "[Guia de Morroc]";
					show "A 'Covil' se encontra na ponta leste da cidade.";
					if (.@compass_check)
						viewpoint 1,284,171,7,0x00FF00;
					break;
				case 7:
					show "[Guia de Morroc]";
					show "Se você deseja apagar as indicações no seu mini-Mapa";
					show "por favor selecione, 'Limpar Indicações'.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0x00FF00;
			viewpoint 2,237,41,3,0x0000FF;
			viewpoint 2,46,345,4,0x00FF00;
			viewpoint 2,175,220,5,0xFF0000;
			viewpoint 2,175,220,6,0xFF0000;
			viewpoint 2,175,220,7,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			show "[Guia de Morroc]";
			show "Nós fomos exaustivamente treinados";
			show "para servi-lo da melhor maneira";
			show "possível e sem falhas.";
			show "Assim você tem a garantia das";
			show "melhores indicações possíveis.";
			next;
			show "[Guia de Morroc]";
			show "As indicações que lhe daremos podem";
			show "ser marcados no seu mini-mapa.";
			show "Para visualizar o seu mini-mapa, aperte 'ctrl+tab', ou clique no";
			show "botão 'mapa' no seu menu principal.";
			show "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
			break;
		case 4:
			show "[Guia de Morroc]";
			show "Espero que você tenha uma jornada";
			show "segura e aproveite sua estadia";
			close2;
			set .@loop1,0;
		}
	}
	cutin "moc_soldier",255;
	end;
}

morocc,54,97,0	duplicate(MocGuide)	Guia#2moc	707