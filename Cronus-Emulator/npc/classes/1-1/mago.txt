//====================================================================
//          _________                                                  
//          \_   ___ \_______  ____   ____  __ __  ______             
//          /    \  \/\_  __ \/    \ /    \|  |  \/  ___/              
//          \     \____|  | \(  ( ) )   |  \  |  /\___ \               
//           \______  /|__|   \____/|___|  /____//____  >              
//                  \/                   \/           \/               
//                                                               
//====================== Cronus Script ===============================
//= Quest do Mago
//===== Por: =========================================================
//= rA dev Team; LyDe
//===== Versão Atual: ================================================
//= 1.0
//===== Compatível com: ==============================================
//= Cronus GIT
//===== Descrição: ===================================================
//= Mudança de Classe para Mago
//===== Comentários Adicionais: ======================================
//= 1.0 Traduzido. [LyDe]
//====================================================================

geffen_in,164,124,4	script	Instrutora	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			mes "[Instrutora de Magia]";
			if (Class == Job_Novice_High) {
				mes "Hmm, faz muito tempo que não nos vemos, não é? Pensei que você tinha morrido.";
				mes "Hmmm... parece que você voltou à sua antiga aparência.";
				next;
				mes "[Instrutora de Magia]";
				if (getskilllv(1) < 9) {
					mes "Bem, você não está preparado para aprender magia como antes.";
					mes "Você precisa aprender o básico e voltar.";
					next;
					mes "Venha depois que a sua memória voltar...";
					mes "Então poderá aprender magia novamente.";
					close;
				} else {
					mes "Eu não faço outra vez o teste para quem já passou por ele.";
					mes "Você só quer a sua magia de volta o mais rápido possível?";
					next;
					skill 143,0,0;
					jobchange Job_Mage_High;
					skill 157,1,0;
					mes "[Instrutora de Magia]";
					mes "Bom. Agora você me parece familiar.";
					mes "Boa sorte e que sua vida seja melhor!";
				}
				close;
			} else {
				mes "Deseja dizer mais alguma coisa?";
				mes "Vá. Há um mundo à sua espera.";
				close;
			}
		} else {
			mes "[Instrutora de Magia]";
			mes "O quê? você está interessado na Guilda dos Magos?";
			mes "Você não deveria estar aqui.";
			next;
			mes "[Instrutora de Magia]";
			mes "Não sei o que você quer, estou muito ocupada.";
			mes "Se você não tem nada para tratar comigo, por favor, vá embora.";
			close;
		}
	}
	if (BaseJob == Job_Mage) {
		mes "[Instrutora de Magia]";
		mes "Ei, Ei! Você ainda não notou que você já é um Mago?";
		mes "Enfim... Um dia desses você vai perceber o poder mágico dentro de você e que é capaz de acender uma fogueira apenas com a força do pensamento.";
		close;
	} else if (BaseJob != Job_Novice) {
		mes "[Instrutora de Magia]";
		mes "Você não tem mais o que fazer? Você sabe que não pode ser um Mago porque já tem outra Classe.";
		close;
	}
	mes "[Instrutora de Magia]";
	mes strcharinfo(0)+" o que eu posso fazer por você?";
	next;
	switch(select("Quero ser Mago!:O que é preciso para ser um Mago?:Nada.")) {
	case 1:
		mes "[Instrutora de Magia]";
		mes "Você quer ser Mago?";
		next;
		mes "[Instrutora de Magia]";
		if (job_magician_q == 0) {
			mes "Você, "+((Sex)?"é mesmo uma gracinha. Definitivamente, meu tipo...":"até que você é bonitinha, deve fazer um grande sucesso...")+"";
			next;
			mes "[Instrutora de Magia]";
			mes "Aham... Como eu ia dizendo, você quer ser Mago?";
			mes "Então, preencha a ficha de inscrição, por favor.";
			next;
			if (select("Se inscrever para o teste.:Desistir.") == 1) {
				mes "[Instrutora de Magia]";
				mes "Okay. Assine aqui mesmo. Oh, você é muito bom para escrever. Certo. seu nome é... "+strcharinfo(0)+".";
				next;
				mes "[Instrutora de Magia]";
				mes "Agora é a hora que";
				mes "que vou te dar o teste.";
				switch(rand(3)) {
				case 1:
					mes "Me faça uma ^3355FFMixed Solution No. 1^000000";
					mes "e a traga pra mim.";
					set job_magician_q,1;
					break;
				case 2:
					mes "Me faça uma ^3355FFMixed Solution No. 2^000000";
					mes "e a traga pra mim.";
					set job_magician_q,2;
					break;
				case 3:
					mes "Me faça uma ^3355FFMixed Solution No. 3^000000";
					mes "e a traga pra mim.";
					set job_magician_q,3;
					break;
				default:
					mes "Me faça uma ^3355FFMixed Solution No. 4^000000";
					mes "e a traga pra mim.";
					set job_magician_q,4;
				}
				next;
				getitem 1092,1; // Tubo de Ensaio
				mes "[Instrutora de Magia]";
				mes "Você pode achar os ingredientes nos livros da guilda. Então acho melhor dar uma olhada antes de ir embora.";
				next;
				mes "[Instrutora de Magia]";
				mes "Assim que coletar todos os itens, use na máquina do centro para misturar eles e fazer a solução. Boa sorte!";
				close;
			}
			mes "O QUE~?! Mesmo após me dizer que queria ser um mago, você mudou de idéia?! Seja um pouco mais decisivo!";
			close;
		}
		mes "[Instrutora de Magia]";
		mes "Certo? Pronto...?";
		if (getskilllv(1) < 9) {
			mes "Oh, mas que bobalhão. Você não tem os requerimentos para fazer o teste.";
			next;
			mes "[Instrutora de Magia]";
			mes "Volte e alcance level 9 de Classe primeiro. Não esqueça de que você também precisa aprender todas as Habilidades Básicas antes de voltar.";
			close;
		}
		mes strcharinfo(0)+" o que eu posso fazer por você?";
		if (job_magician_q == 1) mes "Preparar a Poção Mágica número 1 e trazer aqui para mim.";
		else if (job_magician_q == 2) mes "Preparar a Poção Mágica número 2 e trazer aqui para mim.";
		else if (job_magician_q == 3) mes "Preparar a Poção Mágica número 3 e trazer aqui para mim.";
		else mes "Preparar a Poção Mágica número 4 e trazer aqui para mim.";
		next;
		mes "[Instrutora de Magia]";
		mes "Ok, agora vou verificar se está tudo certo.";
		next;
		mes "[Instrutora de Magia]";
		if (countitem(1071) == 0 && countitem(1085) == 0 && countitem(1086) == 0 && countitem(1087) == 0 && countitem(1090) == 0) {
			mes "Onde está a Poção Mágica que eu pedi? Não posso saber se você fez certo se não me mostrar.";
			close;
		} else {
			if ((job_magician_q == 1 && countitem(1071) == 0) || (job_magician_q == 2 && countitem(1085) == 0) || (job_magician_q == 3 && countitem(1086) == 0) || (job_magician_q == 4 && countitem(1087) == 0)) {
				mes "... Não, não foi isso que eu pedi.";
				next;
				mes "[Instrutora de Magia]";
				if (job_magician_q == 1) mes "Você deve fazer a Mixed Solution No. 1 e trazê-la de volta para mim. Agora vá e tente novamente.";
				else if (job_magician_q == 2) mes "Você deve fazer a Mixed Solution No. 2 e trazê-la de volta para mim. Agora vá e tente novamente.";
				else if (job_magician_q == 3) mes "Você deve fazer a Mixed Solution No. 3 e trazê-la de volta para mim. Agora vá e tente novamente.";
				else mes "Você deve fazer a Mixed Solution No. 4 e trazê-la de volta para mim. Agora vá e tente novamente.";
				if (countitem(1071)) delitem 1071,1; //Mage_Test_1 (Tubo de Ensaio com Líquido Desconhecido)
				else if (countitem(1085)) delitem 1085,1; //Mage_Test_2 (Tubo de Ensaio com Líquido Desconhecido)
				else if (countitem(1086)) delitem 1086,1; //Mage_Test_3 (Tubo de Ensaio com Líquido Desconhecido)
				else if (countitem(1087)) delitem 1087,1; //Mage_Test_4 (Tubo de Ensaio com Líquido Desconhecido)
				else delitem 1090,1; //Mage_Test_Etc (Tubo de Ensaio com Líquido Desconhecido)
				close;
			}
		}
		if (countitem(1071)) delitem 1071,1; //Mage_Test_1 (Tubo de Ensaio com Líquido Desconhecido)
		else if (countitem(1085)) delitem 1085,1; //Mage_Test_2 (Tubo de Ensaio com Líquido Desconhecido)
		else if (countitem(1086)) delitem 1086,1; //Mage_Test_3 (Tubo de Ensaio com Líquido Desconhecido)
		else if (countitem(1087)) delitem 1087,1; //Mage_Test_4 (Tubo de Ensaio com Líquido Desconhecido)
		else delitem 1090,1; //Mage_Test_Etc (Tubo de Ensaio com Líquido Desconhecido)
		mes "Hmm... Eu vejo que você tentou bastante. Para um iniciante, está muito bom.";
		mes "Grande trabalho!";
		next;
		mes "[Instrutora de Magia]";
		mes "Isso ai! Eu tenho o prazer de lhe parabenizar e dizer que você passou no teste.!";
		next;
		mes "[Instrutora de Magia]";
		mes "*Ahem*";
		mes "Parabéns!";
		mes "Você é um mago agora!";
		next;
		callfunc "Job_Change",Job_Mage;
		callfunc "F_ClearJobVar";
		set Zeny,Zeny+50;
		if(checkquest(1005) != -1) completequest 1005;
		else if(checkquest(1006) != -1) completequest 1006;
		else if(checkquest(1007) != -1) completequest 1007;
		else completequest 1008;
		mes "[Instrutora de Magia]";
		mes "'Bem vindo ao meu mundo~'";
		mes "Heh heh, Eu sempre quis dizer isso. Você sabe, é como um filme já visto.";
		next;
		mes "[Instrutora de Magia]";
		mes "Agora que você é um mago como nós, vamos ser amigos, okay?";
		close;
		end;
	case 2:
		mes "[Instrutora de Magia]";
		mes "Quer ser um mago, huh?";
		mes ((Sex)?"Para uma gracinha como você ficaria feliz em explicar!":"Eu ficaria feliz de explicar para uma garotinha tão bonita como você!");
		next;
		mes "[Instrutora de Magia]";
		mes "Primeiro de tudo, você deve ter pelo menos o nivel maximo de job e todas habilidades aprendidas. E então passar pelo teste de mago.";
		next;
		if (job_magician_q) {
			mes "[Instrutora de Magia]";
			mes "O seu teste é";
			switch(job_magician_q) {
			case 1:
				mes "Fazer uma";
				mes "^3355FFMixed Solution No. 1^000000";
				mes "e trazer para mim.";
				setquest 1005;
				break;
			case 2:
				mes "Fazer uma";
				mes "^3355FFMixed Solution No. 2^000000";
				mes "e trazer para mim.";
				setquest 1006;
				break;
			case 3:
				mes "Fazer uma";
				mes "^3355FFMixed Solution No. 3^000000";
				mes "e trazer para mim.";
				setquest 1007;
				break;
			default:
				mes "Fazer uma";
				mes "^3355FFMixed Solution No. 4^000000";
				mes "e trazer para mim.";
				setquest 1008;
			}
			next;
			mes "[Instrutora de Magia]";
			mes "Você pode olhar os ingredientes para a solução nos livros de magia aqui dentro da Guilda.";
		} else {
			mes "[Instrutora de Magia]";
			mes "Você será informado sobre que solução deve fazer antes de ler os livros sobre elas.";
		}
		next;
		mes "[Instrutora de Magia]";
		mes "Me diga quando estiver pronto para ser um mago, tudo bem?";
		close;
		end;
	case 3:
		mes "[Instrutora de Magia]";
		mes "Nada...?";
		close;
		end;
	}
}

geffen_in,164,112,4	script	Máquina de Mistura	111,{
	mes "[Máquina de Mistura]";
	mes "Esta máquina é propriedade da Guilda dos Magos de Geffen e só pode ser utilizada para soluções e propósitos mágicos.";
	next;
	if (select("Usar a máquina.:Cancelar.") == 1) {
		mes "[Máquina de Mistura]";
		mes "Escolha o";
		mes "Solvente para";
		mes "a Solução.";
		next;
		switch(select("Solução de Payon.:Solução de Morroc.:Nenhum Solvente.")) {
		case 1:
			if (countitem(1089) == 0) {
				mes "[Máquina de Mistura]";
				mes "Erro.";
				mes "Item não encontrado.";
				mes "Por favor, verifique novamente.";
				mes "Processo cancelado.";
				close;
			}
			set .@mixitem2,1;
			break;
		case 2:
			if (countitem(1088) == 0) {
				mes "[Máquina de Mistura]";
				mes "Erro.";
				mes "Item não encontado.";
				mes "Por favor, verifique novamente.";
				mes "Processo Cancelado.";
				close;
			}
			set .@mixitem2,2;
			break;
		case 3:
			set .@mixitem2,0;
			break;
		}
		while (1) {
			if (.@progress == 2) {
				mes "[Máquina de Mistura]";
				if (.@mixitem1_1 != 0) mes getitemname(909)+": "+.@mixitem1_1+" unidades.";
				if (.@mixitem1_2 != 0) mes getitemname(914)+": "+.@mixitem1_2+" unidades.";
				if (.@mixitem1_3 != 0) mes getitemname(519)+": "+.@mixitem1_3+" unidades.";
				if (.@mixitem2 == 0) mes "Solvente: Nenhum.";
				if (.@mixitem2 == 1) mes "Solvente: "+getitemname(1089)+".";
				if (.@mixitem2 == 2) mes "Solvente: "+getitemname(1088)+".";
				next;
				mes "[Máquina de Mistura]";
				mes "Por favor escolha o que você deseja começar a misturar, ou reponha os itens que deseja usar.";
				next;
				switch(select("Começar a misturar.:Repor o número de itens.:Resetar.")) {
				case 1:
					mes "[Máquina de Mistura]";
					mes "Por favor coloque os itens no recipiente. Tenha certeza que eles estão colocados na quantidade correta.";
					next;
					mes "[Máquina de Mistura]";
					mes "Você não pode adicionar ou restaurar os itens que forem colocados no recipiente.";
					next;
					mes "[Máquina de Mistura]";
					mes "Se está tudo correto, aperte o botão 'misturar'. Caso contrário, aperte 'cancelar'.";
					next;
					if (select("Misturar.:Cancelar.") == 1) {
						mes "[Máquina de Mistura]";
						mes "Colocando os itens no recipiente. Por favor, aguarde.";
						next;
						mes "[Máquina de Mistura]";
						if (countitem(909) < .@mixitem1_1) {
							mes getitemname(909)+" insuficiente.";
							mes "Por favor, verifique novamente.";
							mes "Processo interrompido.";
							close;
						} else if (countitem(914) < .@mixitem1_2) {
							mes getitemname(914)+" insuficiente.";
							mes "Por favor, verifique novamente.";
							mes "Processo interrompido.";
							close;
						} else if (countitem(519) < .@mixitem1_3) {
							mes getitemname(519)+" insuficiente.";
							mes "Por favor, verifique novamente.";
							mes "Processo interrompido.";
							close;
						} else if (.@mixitem2 == 1 || .@mixitem2 == 2) {
							if (countitem(1089) == 0 && countitem(1088) == 0) {
								mes "Solução não encontrada.";
							mes "Por favor, verifique novamente.";
							mes "Processo interrompido.";
								close;
							}
						}
						if (.@mixitem1_1) delitem 909,.@mixitem1_1;  //Jellopy
						if (.@mixitem1_2) delitem 914,.@mixitem1_2; //Felpa
						if (.@mixitem1_3) delitem 519,.@mixitem1_3; // Leite
						if (.@mixitem2 == 1) delitem 1089,1; //Solução de Payon
						if (.@mixitem2 == 2) delitem 1088,1; //Solução de Morroc
						mes "Itens estão prontos.";
						mes "Fechando a porta.";
						set .@progress,3;
						next;
					}
					break;
				case 2:
					set .@continue,0;
					next;
					break;
				case 3:
					set .@mixitem1_1,0;
					set .@mixitem1_2,0;
					set .@mixitem1_3,0;
					set .@progress,0;
					set .@continue,0;
					mes "[Máquina de Mistura]";
					mes "Processo resetado.";
					mes "Começar novamente?";
					next;
					if (select("Sim.:Não.") == 1)
						break;
					mes "[Máquina de Mistura]";
					mes "Processo interrompido.";
					mes "Obrigado.";
					close;
				}
				if (.@progress == 3) break;
			} else if (.@progress == 1) {
				mes "[Máquina de Mistura]";
				mes "Nada encontrado.";
				next;
			}
			mes "[Máquina de Mistura]";
			mes "Selecione os itens para a mistura.";
			while(1) {
				switch(select(""+getitemname(909)+".:"+getitemname(914)+".:"+getitemname(519)+".:Pronto para misturar.")) {
				case 1:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Máquina de Mistura]";
							mes "Erro: O limite de itens ultrapassou. Por favor coloque valores menores que 10,000 e tente novamente.";
							next;
						} else {
							if (countitem(909) > 0) set .@mixitem1_1,.@mixitem1_1 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 2:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Máquina de Mistura]";
							mes "Erro: O limite de itens ultrapassou. Por favor coloque valores menores que 10,000 e tente novamente.";
							next;
						} else {
							if (countitem(914) > 0) set .@mixitem1_2,.@mixitem1_2 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 3:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Máquina de Mistura]";
							mes "Erro: O limite de itens ultrapassou. Por favor coloque valores menores que 10,000 e tente novamente.";
							next;
						} else {
						if (countitem(519) > 0) set .@mixitem1_3,.@mixitem1_3 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;

				case 4:
					if (.@progress != 2)
						set .@progress,1;
					set .@continue,1;
					next;
				}
				if (.@continue) break;
			}
		}
		mes "[Máquina de Mistura]";
		mes "Por favor digite o  ";
		mes "número de série do";
		mes "Pó mágico.";
		next;
		while(1) {
			input .@input;
			if (.@input < 1000 || .@input > 9999) {
				mes "[Máquina de Mistura]";
				if (.@input == 0) {
					mes "Deseja pular este menu?";
					next;
					if (select("Sim.:Não.") == 1) {
						break;
					}
				} else {
					mes "Número inválido.";
					mes "Por favor, tente novamente.";
					next;
				}
			}
			else {
				mes "[Máquina de Mistura]";
				mes "O numero de série é  #" + .@input + ", correto?";
				next;
				if (select("Confirmar.:Cancelar.") == 1) {
					if (.@input == 8472) set .@magic_powder,1;
					else if (.@input == 3735) set .@magic_powder,2;
					else if (.@input == 2750) set .@magic_powder,3;
					else if (.@input == 5429) set .@magic_powder,4;
					else set .@magic_powder,5;
				}
				break;
			}
		}
		mes "[Máquina de Mistura]";
		mes "Escolha uma pedra";
		mes "Catalisadora.";
		next;
		switch(select(""+getitemname(715)+".:"+getitemname(716)+".:"+getitemname(717)+".:"+getitemname(730)+".:Pular a etapa.")) {
		case 1: set .@mixitem3,1; break;
		case 2: set .@mixitem3,2; break;
		case 3: set .@mixitem3,3; break;
		case 4: set .@mixitem3,4; break;
		case 5: break;
		}
		mes "[Máquina de Mistura]";
		mes "Tudo pronto.";
		mes "Iniciando";
		mes "Processo de Mistura.";
		mes "Por favor, aguarde.";
		next;
		mes "[Máquina de Mistura]";
		mes "- Provérbio do dia -";
		switch(rand(1,5)) {
		case 1:
			mes "Olho por olho: Quando tira de uma pessoa, você deve retribuí-lo de alguma forma.";
			break;
		case 2:
			mes "O valor da confiança não pode ser medido ou muito menos comprado.";
			break;
		case 3:
			mes "Não tratar os outros da maneira que você não gostaria que te tratariam.";
			mes "Ou seja, quando você machuca alguém você será ferido mais ainda.";
			break;
		case 4:
			mes "Um bom vizinho é melhor do que um irmão distante: quando precisar de ajuda, conte sempre com quem esta perto de você.";
			break;
		default:
			mes "Água mole em pedra dura tanto bate até que fura.";
		}
		next;
		if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 1 && .@mixitem2 == 1 && .@mixitem3 == 1 && .@magic_powder == 1) {
			mes "[Máquina de Mistura]";
			mes getitemname(1071);
			getitem 1071,1; //Mage_Test_1
			next;
		}
		else if (.@mixitem1_1 == 3 && .@mixitem1_2 == 1 && .@mixitem1_3 == 1 && .@mixitem2 == 0 && .@mixitem3 == 2 && .@magic_powder == 2) {
			mes "[Máquina de Mistura]";
			mes getitemname(1085);
			getitem 1085,1; //Mage_Test_2
			next;
		}
		else if (.@mixitem1_1 == 6 && .@mixitem1_2 == 1 && .@mixitem1_3 == 0 && .@mixitem2 == 1 && .@mixitem3 == 3 && .@magic_powder == 3) {
			mes "[Máquina de Mistura]";
			mes getitemname(1086);
			getitem 1086,1; //Mage_Test_3
			next;
		}
		else if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 0 && .@mixitem2 == 2 && .@mixitem3 == 4 && .@magic_powder == 4) {
			mes "[Máquina de Mistura]";
			mes getitemname(1087);
			getitem 1087,1; //Mage_Test_4
			next;
		}
		else {
			mes "[Máquina de Mistura]";
			mes "Erro inesperado";
			mes "ocorrido.";
			getitem 1090,1; //Mage_Test_Etc
			next;
		}
		mes "[Máquina de Mistura]";
		mes "Mistura completa.";
		mes "Obrigado.";
		close;
	}
	close;
}

pay_arche,122,100,0	script	Dollshoi	88,{
	mes "[Dollshoi]";
	mes "Você quer uma solução? Hmm, me dê 50z e um "+getitemname(1092)+".";
	next;
	if (select("Negócio Fechado.:Nah, esquece.") == 2) close;
	mes "[Dollshoi]";
	if (Zeny < 50) {
		mes "Hey! Você não tem 50z.";
		close;
	}
	if (countitem(1092) == 0) {
		mes "Você não pode carregar as soluções! Me traga um tubo.";
		close;
	}
	delitem 1092,1; //Empty_Cylinder
	set zeny,zeny-50;
	getitem 1089,1; //Payon_Potion
	close;
}

moc_ruins,91,150,0	script	Ponka-Hontas	93,{
	mes "[Ponka-Hontas]";
	mes "Você quer uma solução? Hmm, me dê 50z e um "+getitemname(1092)+".";
	next;
	if (select("Negocio fechado.:Nah, esquece.") == 2) close;
	mes "[Ponka-Hontas]";
	if (zeny < 50) {
		mes "Eu sinto muito mas você não tem 50z.";
		close;
	}
	if (countitem(1092) == 0) {
		mes "Você não pode carregar as soluções! Me traga um tubo.";
		close;
	}
	delitem 1092,1; //Empty_Cylinder
	set zeny,zeny-50;
	getitem 1088,1; //Morocc_Potion
	close;
}

geffen_in,177,112,4	script	Estante de Livros	111,{
	mes "[Livro Guia]";
	mes "Este guia é propriedade da Guilda dos Magos de Geffen.";
	next;
	switch(select("Solution No. 1.:Solution No. 2.:Solution No. 3.:Solution No. 4.:Close.")) {
	case 1:
		mes "["+getitemname(1071)+"]";
		mes "* Lista de ingredientes *";
		mes "2 "+getitemname(909);
		mes "3 "+getitemname(914);
		mes "1 "+getitemname(519);
		next;
		mes "["+getitemname(1071)+"]";
		mes "*Solvente *";
		mes getitemname(1089);
		mes "Onde encontrá-la:";
		mes "Perto de uma lagoa na vila dos arqueiros.";
		next;
		mes "["+getitemname(1071)+"]";
		mes "* Código de série do pó mágico *";
		mes "8472";
		next;
		mes "["+getitemname(1071)+"]";
		mes "* Catalisador *";
		mes getitemname(715);
		mes "(Provida pela";
		mes "Máquina de mistura)";
		close;
	case 2:
		mes "["+getitemname(1085)+"]";
		mes "* Lista de ingredientes *";
		mes "3 "+getitemname(909);
		mes "1 "+getitemname(914);
		mes "1 "+getitemname(519);
		next;
		mes "["+getitemname(1085)+"]";
		mes "*Solvente *";
		mes "Nenhum";
		next;
		mes "["+getitemname(1085)+"]";
		mes "* Código de série do pó mágico *";
		mes "3735";
		next;
		mes "["+getitemname(1085)+"]";
		mes "* Catalisador *";
		mes getitemname(716);
		mes "(Provida pela";
		mes "Máquina de mistura)";
		close;
	case 3:
		mes "["+getitemname(1086)+"]";
		mes "* Lista de ingredientes *";
		mes "6 "+getitemname(909);
		mes "1 "+getitemname(914);
		next;
		mes "["+getitemname(1086)+"]";
		mes "*Solvente *";
		mes getitemname(1089);
		mes "Onde encontrá-la:";
		mes "Perto de uma lagoa na vila dos arqueiros.";
		next;
		mes "["+getitemname(1086)+"]";
		mes "* Código de série do pó mágico *";
		mes "2750";
		next;
		mes "["+getitemname(1086)+"]";
		mes "* Catalisador *";
		mes getitemname(717);
		mes "(Provida pela";
		mes "Máquina de mistura)";
		close;
	case 4:
		mes "["+getitemname(1087)+"]";
		mes "* Lista de ingredientes *";
		mes "2 "+getitemname(909);
		mes "3 "+getitemname(914);
		next;
		mes "["+getitemname(1087)+"]";
		mes "*Solvente *";
		mes getitemname(1088);
		mes "Aonde encontrar:";
		mes "Em uma lagoa perto da Pirâmide de Morroc.";
		next;
		mes "["+getitemname(1087)+"]";
		mes "* Código de série do pó mágico *";
		mes "5429";
		next;
		mes "["+getitemname(1087)+"]";
		mes "* Catalisador *";
		mes getitemname(730);
		mes "(Provida pela";
		mes "Máquina de mistura)";
		close;
	}
	close;
}