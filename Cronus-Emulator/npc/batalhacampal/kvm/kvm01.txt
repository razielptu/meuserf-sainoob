//====================================================================
//          _________                                                  
//          \_   ___ \_______  ____   ____  __ __  ______             
//          /    \  \/\_  __ \/    \ /    \|  |  \/  ___/              
//          \     \____|  | \(  ( ) )   |  \  |  /\___ \               
//           \______  /|__|   \____/|___|  /____//____  >              
//                  \/                   \/           \/               
//                                                               
//====================== Cronus Script ===============================
//= Sistema BattleGround - KvM 80-99 
//===== Por: =========================================================
//= rA dev Team; LyDe
//===== Versão Atual: ================================================
//= 1.0
//===== Compatível com: ==============================================
//= Cronus GIT
//===== Descrição: ===================================================
//= NPCs de KvM 80-90.
//===== Comentários Adicionais: ======================================
//= 1.0 Traduzido. [LyDe]
//====================================================================

// NPC's da Sala de Espera
//============================================================
bat_room,169,226,5	script	Sala de Espera do KVM#a::KvM01R_Guillaume	418,{
	end;

OnInit:
	waitingroom "Estação de Batalha de 5 jogadores",5,"KvM01_BG::OnGuillaumeJoin",1;
	end;

OnEnterBG:
	set $@KvM01BG_id1, waitingroom2bg("bat_c01",52,129,"KvM01_BG::OnGuillaumeQuit","KvM01_BG::OnGuillaumeDie");
	end;
}

bat_room,169,220,0	warp	#kvm801	2,2,bat_room,154,150

bat_room,169,205,3	script	Sala de Espera do KVM#b::KvM01R_Croix	414,{
	end;

OnInit:
	waitingroom "Estação de Batalha de 5 jogadores",5,"KvM01_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM01BG_id2, waitingroom2bg("bat_c01",147,55,"KvM01_BG::OnCroixQuit","KvM01_BG::OnCroixDie");
	end;
}

bat_room,169,211,0	warp	#kvm802	2,2,bat_room,154,150

// Mecanismo de Início
//============================================================
-	script	KVM01::CellEffect	-1,{
	end;

OnKvM01One:
	misceffect EF_CHANGECOLD;
	end;

OnKvM01Two:
	misceffect EF_CHANGEPOISON;
	end;
}

bat_c01,54,124,3	duplicate(CellEffect)	#RedcellA1-1	139
bat_c01,55,124,3	duplicate(CellEffect)	#RedcellA2-1	139
bat_c01,56,124,3	duplicate(CellEffect)	#RedcellA3-1	139
bat_c01,57,124,3	duplicate(CellEffect)	#RedcellA4-1	139
bat_c01,57,125,3	duplicate(CellEffect)	#RedcellA5-1	139
bat_c01,57,126,3	duplicate(CellEffect)	#RedcellA6-1	139
bat_c01,57,127,3	duplicate(CellEffect)	#RedcellA7-1	139

bat_c01,145,59,3	duplicate(CellEffect)	#RedcellB1-1	139
bat_c01,144,59,3	duplicate(CellEffect)	#RedcellB2-1	139
bat_c01,143,59,3	duplicate(CellEffect)	#RedcellB3-1	139
bat_c01,142,59,3	duplicate(CellEffect)	#RedcellB4-1	139
bat_c01,142,56,3	duplicate(CellEffect)	#RedcellB5-1	139
bat_c01,142,57,3	duplicate(CellEffect)	#RedcellB6-1	139
bat_c01,142,58,3	duplicate(CellEffect)	#RedcellB7-1	139

bat_c01,54,128,3	script	#A_camp_start01	139,4,4,{
	end;

OnInit:
	disablenpc "#A_camp_start01";
	end;

OnEnable:
	enablenpc "#A_camp_start01";
	end;

OnDisable:
	disablenpc "#A_camp_start01";
	end;

OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c01,146,56,3	script	#B_camp_start01	139,4,4,{
	end;

OnInit:
	disablenpc "#B_camp_start01";
	end;

OnEnable:
	enablenpc "#B_camp_start01";
	end;

OnDisable:
	disablenpc "#B_camp_start01";
	end;

OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Engine do Campo de Batalha do KvM
//============================================================
-	script	KvM01_BG	-1,{
	end;

OnInit:
	setwall "bat_c01",54,122,6,7,0,"batc01wall_a";
	setwall "bat_c01",55,122,5,7,0,"batc01wall_b";
	setwall "bat_c01",140,56,6,7,0,"batc01wall_c";
	setwall "bat_c01",140,57,5,7,0,"batc01wall_d";
	disablenpc "Oficial do KVM#KVM01A";
	disablenpc "Oficial do KVM#KVM01B";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM01_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM01BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM01_BG::OnCroixWin";
		else {
			mapannounce "bat_c01", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c01", "O número de Croixes é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM01BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM01_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c01", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c01", "O número de Croixes é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnReadyCheck:
	if( $@KvM01BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM01R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM01R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM01BG, 1; // Starting
	donpcevent "KvM01R_Croix::OnEnterBG";
	donpcevent "KvM01R_Guillaume::OnEnterBG";
	donpcevent "KvM01_BG::OnStart";
	end;

OnStart:
	disablenpc "Oficial do KVM#KVM01A";
	disablenpc "Oficial do KVM#KVM01B";
	set $@KvM01BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Em 1 minuto, Batalha pelo KVM será iniciada.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "O tempo limite para a Batalha pelo KvM é 5 minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c01", "Por favor prepare-se para a Batalha pelo KVM.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Você já pode abençoar seus aliados.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start01::OnEnable";
	donpcevent "#B_camp_start01::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start01::OnDisable";
	donpcevent "#B_camp_start01::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c01", "30 segundos restantes para a Batalha pelo KvM se iniciar.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c01", "15 segundos restantes para a Batalha pelo KvM se iniciar.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c01", "10 segundos restantes para a Batalha pelo KvM se iniciar.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c01", "5 segundos restantes para a Batalha pelo KvM se iniciar.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c01", "Que começe a Batalha pelo KvM!",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM01BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM01BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		set $@KvM01BG_Victory, 3;
		set $@KvM01BG, 3;
		mapannounce "bat_c01","Não há jogadores suficientes para dar início a Batalha.",1,0x696969;
		stopnpctimer;
		donpcevent "KvM01_BG::OnStop";
		end;
	}
	set $@KvM01BG, 2; // Playing
	bg_warp $@KvM01BG_id1,"bat_c01",61,120;
	bg_warp $@KvM01BG_id2,"bat_c01",138,63;
	end;

OnTimer300000:
	mapannounce "bat_c01", "1 minuto restante para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c01", "30 segundos restantes para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c01", "15 segundos restantes para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c01", "10 segundos restantes para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c01", "5 segundos restantes para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c01", "A Batalha pelo KvM terminou!",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM01_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM01_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM01BG, 3;
		set $@KvM01BG_Victory, 3;
		mapannounce "bat_c01", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "O número de Croixes é "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "A batalha terminou em um empate.",bc_map,"0x00ff00";
		donpcevent "KvM01_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 1;
	mapannounce "bat_c01", "O General Guillaume venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Parabéns aos membros de Guillaume.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Todos serão enviados ao ponto de início.",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 2;
	mapannounce "bat_c01", "O Princípe Croix venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Parabéns aos membros de Croix.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Todos serão enviados ao ponto de início.",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "Oficial do KVM#KVM01A";
	enablenpc "Oficial do KVM#KVM01B";
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	donpcevent "KvM01_BG_Out::OnBegin";
	end;

OnReset:
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set $@KvM01BG_Victory, 0;
	if( $@KvM01BG_id1 ) { bg_destroy $@KvM01BG_id1; set $@KvM01BG_id1, 0; }
	if( $@KvM01BG_id2 ) { bg_destroy $@KvM01BG_id2; set $@KvM01BG_id2, 0; }
	disablenpc "Oficial do KVM#KVM01A";
	disablenpc "Oficial do KVM#KVM01B";
	mapwarp "bat_c01","bat_room",154,150;
	maprespawnguildid "bat_c01",0,3; // Just in case someone else
	bg_updatescore "bat_c01",5,5;
	set $@KvM01BG, 0;
	donpcevent "KvM01_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM01_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Por favor fale com o Oficial para adquirir seus Pontos do KvM.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "O Oficial estará distribuindo os pontos durante 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Em 30 segundos, o Oficial será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c01", "Se você não entrar em contato com o Oficial, você não receberá seus pontos.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Por favor tenha cuidado.",bc_map,"0x00ff00";
	end;

OnTimer:55000:
	mapannounce "bat_c01", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	donpcevent "KvM01_BG::OnReset";
	end;
}

// Recompensas do Campo de Batalha
//============================================================
bat_c01,51,130,5	script	Oficial do KVM#KVM01A	419,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 5;
			mes "[Oficial do KvM]";
			mes "Ótima batalha!";
			mes "Que a glória do KvM esteja com você.";
			mes "Você adquiriu os pontos dos vencedores: 5";
			close2;
		}
		else
		{ // Lose
			set kvm_point,kvm_point + 1;
			mes "[Oficial do KvM]";
			mes "Eu sinto muito.";
			mes "Eu te desejo mais sorte na próxima vez.";
			mes "Você adquiriu os pontos dos perdedores: 1";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}

bat_c01,148,53,1	script	Oficial do KVM#KVM01B	415,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 5;
			mes "[Oficial do KvM]";
			mes "Ótima batalha!";
			mes "Que a glória do KvM esteja com você.";
			mes "Você adquiriu os pontos dos vencedores: 5";
			close2;
		}
		else
		{ // Lose
			set kvm_point,kvm_point + 1;
			mes "[Oficial do KvM]";
			mes "Eu sinto muito.";
			mes "Eu te desejo mais sorte na próxima vez.";
			mes "Você adquiriu os pontos dos perdedores: 1";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}